OBJECT Table 1101 Cost Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LOCKTABLE;
               CostJournalTemplate.GET("Journal Template Name");
               CostJournalBatch.GET("Journal Template Name","Journal Batch Name");
               "Reason Code" := CostJournalBatch."Reason Code";

               IF "Source Code" = '' THEN BEGIN
                 SourceCodeSetup.GET;
                 "Source Code" := SourceCodeSetup."Cost Journal";
               END;
             END;

    OnModify=BEGIN
               "System-Created Entry" := FALSE;
             END;

    CaptionML=[ENU=Cost Journal Line;
               FRA=Ligne feuille de coñts];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Cost Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              FRA=Nom modäle feuille] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=N¯ ligne] }
    { 4   ;   ;Cost Type No.       ;Code20        ;TableRelation="Cost Type";
                                                   OnValidate=BEGIN
                                                                IF CostType.GET("Cost Type No.") THEN BEGIN
                                                                  CostType.TESTFIELD(Blocked,FALSE);
                                                                  CostType.TESTFIELD(Type,CostType.Type::"Cost Type");
                                                                  "Cost Center Code" := CostType."Cost Center Code";
                                                                  "Cost Object Code" := CostType."Cost Object Code";
                                                                  Description := CostType.Name;
                                                                END;

                                                                CalcBalance;
                                                              END;

                                                   CaptionML=[ENU=Cost Type No.;
                                                              FRA=N¯ type coñt] }
    { 5   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              FRA=Date comptabilisation];
                                                   ClosingDates=Yes }
    { 7   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              FRA=N¯ document] }
    { 8   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              FRA=DÇsignation] }
    { 9   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Cost Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              FRA=Nom feuille] }
    { 11  ;   ;Bal. Cost Type No.  ;Code20        ;TableRelation="Cost Type";
                                                   OnValidate=BEGIN
                                                                IF CostType.GET("Bal. Cost Type No.") THEN BEGIN
                                                                  CostType.TESTFIELD(Blocked,FALSE);
                                                                  CostType.TESTFIELD(Type,CostType.Type::"Cost Type");
                                                                  "Bal. Cost Center Code" := CostType."Cost Center Code";
                                                                  "Bal. Cost Object Code" := CostType."Cost Object Code";
                                                                END;

                                                                CalcBalance;
                                                              END;

                                                   CaptionML=[ENU=Bal. Cost Type No.;
                                                              FRA=N¯ type coñt de solde] }
    { 16  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                CalcBalance;
                                                                UpdateDebitCreditAmounts;
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              FRA=Montant] }
    { 17  ;   ;Balance             ;Decimal       ;CaptionML=[ENU=Balance;
                                                              FRA=Solde];
                                                   Editable=No }
    { 18  ;   ;Debit Amount        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Amount,"Debit Amount");
                                                              END;

                                                   CaptionML=[ENU=Debit Amount;
                                                              FRA=Montant dÇbit] }
    { 19  ;   ;Credit Amount       ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Amount,-"Credit Amount");
                                                              END;

                                                   CaptionML=[ENU=Credit Amount;
                                                              FRA=Montant crÇdit] }
    { 20  ;   ;Cost Center Code    ;Code20        ;TableRelation="Cost Center";
                                                   OnValidate=BEGIN
                                                                CheckCostCenter("Cost Center Code");
                                                              END;

                                                   CaptionML=[ENU=Cost Center Code;
                                                              FRA=Code centre de coñts] }
    { 21  ;   ;Cost Object Code    ;Code20        ;TableRelation="Cost Object";
                                                   OnValidate=BEGIN
                                                                CheckCostObject("Cost Object Code");
                                                              END;

                                                   CaptionML=[ENU=Cost Object Code;
                                                              FRA=Code coñts associÇs] }
    { 22  ;   ;Bal. Cost Center Code;Code20       ;TableRelation="Cost Center";
                                                   OnValidate=BEGIN
                                                                CheckCostCenter("Bal. Cost Center Code");
                                                              END;

                                                   CaptionML=[ENU=Bal. Cost Center Code;
                                                              FRA=Code centre de coñts de solde] }
    { 23  ;   ;Bal. Cost Object Code;Code20       ;TableRelation="Cost Object";
                                                   OnValidate=BEGIN
                                                                CheckCostObject("Bal. Cost Object Code");
                                                              END;

                                                   CaptionML=[ENU=Bal. Cost Object Code;
                                                              FRA=Code coñts associÇs de solde] }
    { 27  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              FRA=Code motif] }
    { 29  ;   ;G/L Entry No.       ;Integer       ;TableRelation="G/L Entry";
                                                   CaptionML=[ENU=G/L Entry No.;
                                                              FRA=N¯ sÇquence compta.];
                                                   Editable=No }
    { 30  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[ENU=Source Code;
                                                              FRA=Code journal] }
    { 31  ;   ;System-Created Entry;Boolean       ;CaptionML=[ENU=System-Created Entry;
                                                              FRA=êcriture systäme];
                                                   Editable=No }
    { 32  ;   ;Cost Entry No.      ;Integer       ;CaptionML=[ENU=Cost Entry No.;
                                                              FRA=N¯ Çcriture de coñts];
                                                   Editable=No }
    { 33  ;   ;Allocated           ;Boolean       ;CaptionML=[ENU=Allocated;
                                                              FRA=VentilÇ] }
    { 50  ;   ;Allocation Description;Text80      ;CaptionML=[ENU=Allocation Description;
                                                              FRA=Description ventilation] }
    { 51  ;   ;Allocation ID       ;Code10        ;CaptionML=[ENU=Allocation ID;
                                                              FRA=ID ventilation] }
    { 68  ;   ;Additional-Currency Amount;Decimal ;CaptionML=[ENU=Additional-Currency Amount;
                                                              FRA=Montant DR];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 69  ;   ;Add.-Currency Debit Amount;Decimal ;CaptionML=[ENU=Add.-Currency Debit Amount;
                                                              FRA=Montant dÇbit DR];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 70  ;   ;Add.-Currency Credit Amount;Decimal;CaptionML=[ENU=Add.-Currency Credit Amount;
                                                              FRA=Montant crÇdit DR];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 100 ;   ;Budget Name         ;Code10        ;CaptionML=[ENU=Budget Name;
                                                              FRA=Nom du budget];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Line No.;
                                                   SumIndexFields=Balance;
                                                   Clustered=Yes }
    {    ;Cost Type No.,Cost Center Code,Cost Object Code }
    {    ;G/L Entry No.                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SourceCodeSetup@1005 : Record 242;
      CostType@1004 : Record 1103;
      CostJournalBatch@1003 : Record 1102;
      CostJournalTemplate@1002 : Record 1100;
      Text000@1006 : TextConst '@@@="%2 = Cost Center or Cost Object; %3 = Cost Center or Cost Object Code";ENU=Line Type must be %1 or Begin-Total in %2 %3.;FRA=Type ligne doit àtre %1 ou DÇbut total dans %2 %3.';

    LOCAL PROCEDURE CalcBalance@1();
    BEGIN
      CASE TRUE OF
        ("Cost Type No." <> '') AND ("Bal. Cost Type No." <> ''):
          Balance := 0;
        "Bal. Cost Type No." <> '':
          Balance := -Amount;
        ELSE
          Balance := Amount;
      END;
    END;

    LOCAL PROCEDURE CheckCostCenter@2(CostCenterCode@1000 : Code[20]);
    VAR
      CostCenter@1001 : Record 1112;
    BEGIN
      IF CostCenter.GET(CostCenterCode) THEN BEGIN
        IF NOT (CostCenter."Line Type" IN [CostCenter."Line Type"::"Cost Center",CostCenter."Line Type"::"Begin-Total"]) THEN
          ERROR(Text000,CostCenter."Line Type"::"Cost Center",CostCenter.TABLECAPTION,CostCenter.Code);
        CostCenter.TESTFIELD(Blocked,FALSE);
      END;
    END;

    LOCAL PROCEDURE CheckCostObject@3(CostObjectCode@1000 : Code[20]);
    VAR
      CostObject@1001 : Record 1113;
    BEGIN
      IF CostObject.GET(CostObjectCode) THEN BEGIN
        IF NOT (CostObject."Line Type" IN [CostObject."Line Type"::"Cost Object",CostObject."Line Type"::"Begin-Total"]) THEN
          ERROR(Text000,CostObject."Line Type"::"Cost Object",CostObject.TABLECAPTION,CostObject.Code);
        CostObject.TESTFIELD(Blocked,FALSE);
      END;
    END;

    PROCEDURE SetUpNewLine@8(LastCostJournalLine@1000 : Record 1101);
    VAR
      CostJournalLine@1001 : Record 1101;
    BEGIN
      CostJournalTemplate.GET("Journal Template Name");
      CostJournalBatch.GET("Journal Template Name","Journal Batch Name");
      CostJournalLine.SETRANGE("Journal Template Name","Journal Template Name");
      CostJournalLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF CostJournalLine.FINDFIRST THEN BEGIN
        "Posting Date" := LastCostJournalLine."Posting Date";
        "Document No." := LastCostJournalLine."Document No.";
      END ELSE
        "Posting Date" := WORKDATE;

      "Source Code" := CostJournalTemplate."Source Code";
      "Reason Code" := CostJournalBatch."Reason Code";
      "Bal. Cost Type No." := CostJournalBatch."Bal. Cost Type No.";
      "Bal. Cost Center Code" := CostJournalBatch."Bal. Cost Center Code";
      "Bal. Cost Object Code" := CostJournalBatch."Bal. Cost Object Code";
    END;

    PROCEDURE EmptyLine@4() : Boolean;
    BEGIN
      EXIT(Amount = 0);
    END;

    LOCAL PROCEDURE UpdateDebitCreditAmounts@5();
    BEGIN
      CASE TRUE OF
        Amount > 0:
          BEGIN
            "Debit Amount" := Amount;
            "Credit Amount" := 0;
          END;
        Amount < 0:
          BEGIN
            "Debit Amount" := 0;
            "Credit Amount" := -Amount;
          END;
        Amount = 0:
          BEGIN
            "Debit Amount" := 0;
            "Credit Amount" := 0;
          END;
      END;
    END;

    PROCEDURE IsOpenedFromBatch@42() : Boolean;
    VAR
      CostJournalBatch@1002 : Record 1102;
      TemplateFilter@1001 : Text;
      BatchFilter@1000 : Text;
    BEGIN
      BatchFilter := GETFILTER("Journal Batch Name");
      IF BatchFilter <> '' THEN BEGIN
        TemplateFilter := GETFILTER("Journal Template Name");
        IF TemplateFilter <> '' THEN
          CostJournalBatch.SETFILTER("Journal Template Name",TemplateFilter);
        CostJournalBatch.SETFILTER(Name,BatchFilter);
        CostJournalBatch.FINDFIRST;
      END;

      EXIT((("Journal Batch Name" <> '') AND ("Journal Template Name" = '')) OR (BatchFilter <> ''));
    END;

    BEGIN
    END.
  }
}


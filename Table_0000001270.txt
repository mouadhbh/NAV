OBJECT Table 1270 OCR Service Setup
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    OnInsert=BEGIN
               TESTFIELD("Primary Key",'');
             END;

    OnDelete=VAR
               OCRServiceDocTemplate@1000 : Record 1271;
             BEGIN
               DeletePassword("Password Key");
               DeletePassword("Authorization Key");
               OCRServiceDocTemplate.DELETEALL(TRUE)
             END;

    CaptionML=[ENU=OCR Service Setup;
               FRA=Paramätres service OCR];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              FRA=ClÇ primaire] }
    { 2   ;   ;User Name           ;Text50        ;CaptionML=[ENU=User Name;
                                                              FRA=Nom utilisateur] }
    { 3   ;   ;Password Key        ;GUID          ;TableRelation="Service Password".Key;
                                                   CaptionML=[ENU=Password Key;
                                                              FRA=ClÇ mot de passe] }
    { 4   ;   ;Signup URL          ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Signup URL;
                                                              FRA=URL inscription] }
    { 5   ;   ;Service URL         ;Text250       ;OnValidate=VAR
                                                                HttpWebRequestMgt@1000 : Codeunit 1297;
                                                              BEGIN
                                                                IF "Service URL" = '' THEN
                                                                  EXIT;
                                                                HttpWebRequestMgt.CheckUrl("Service URL");
                                                                WHILE (STRLEN("Service URL") > 8) AND ("Service URL"[STRLEN("Service URL")] = '/') DO
                                                                  "Service URL" := COPYSTR("Service URL",1,STRLEN("Service URL") - 1);
                                                              END;

                                                   CaptionML=[ENU=Service URL;
                                                              FRA=URL service] }
    { 6   ;   ;Login URL           ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Login URL;
                                                              FRA=URL login] }
    { 7   ;   ;Authorization Key   ;GUID          ;TableRelation="Service Password".Key;
                                                   CaptionML=[ENU=Authorization Key;
                                                              FRA=ClÇ d'autorisation] }
    { 8   ;   ;Customer Name       ;Text80        ;CaptionML=[ENU=Customer Name;
                                                              FRA=Nom client];
                                                   Editable=No }
    { 9   ;   ;Customer ID         ;Text50        ;CaptionML=[ENU=Customer ID;
                                                              FRA=ID client];
                                                   Editable=No }
    { 10  ;   ;Customer Status     ;Text30        ;CaptionML=[ENU=Customer Status;
                                                              FRA=Statut client];
                                                   Editable=No }
    { 11  ;   ;Organization ID     ;Text50        ;CaptionML=[ENU=Organization ID;
                                                              FRA=ID organisation];
                                                   Editable=No }
    { 12  ;   ;Default OCR Doc. Template;Code20   ;TableRelation="OCR Service Document Template";
                                                   CaptionML=[ENU=Default OCR Doc. Template;
                                                              FRA=Modäle doc. OCR par dÇfaut] }
    { 13  ;   ;Enabled             ;Boolean       ;OnValidate=BEGIN
                                                                IF Enabled THEN BEGIN
                                                                  TESTFIELD("Default OCR Doc. Template");
                                                                  ScheduleJobQueueEntries;
                                                                  IF CONFIRM(JobQEntriesCreatedQst) THEN
                                                                    ShowJobQueueEntry;
                                                                END ELSE
                                                                  CancelJobQueueEntries;
                                                              END;

                                                   CaptionML=[ENU=Enabled;
                                                              FRA=ActivÇ] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EncryptionIsNotActivatedQst@1000 : TextConst 'ENU=Data encryption is not activated. It is recommended that you encrypt data. \Do you want to open the Data Encryption Management window?;FRA=Le chiffrement des donnÇes n''est pas activÇ. Nous vous recommandons de chiffrer les donnÇes. \Voulez-vous ouvrir la fenàtre Gestion du chiffrement des donnÇesˇ?';
      MustBeEnabledErr@1001 : TextConst '@@@="OCR = Optical Character Recognition";ENU=The OCR service is not enabled.\\In the OCR Service Setup window, select the Enabled check box.;FRA=Le service OCR n''est pas activÇ.\\Dans la fenàtre Paramätres service OCR, cochez la case ActivÇ.';
      JobQEntriesCreatedQst@1002 : TextConst 'ENU=Job queue entries for sending and receiving electronic documents have been created.\\Do you want to open the Job Queue Entries window?;FRA=Des Çcritures file d''attente des travaux pour l''envoi et la rÇception de documents Çlectroniques ont ÇtÇ crÇÇes.\\Voulez-vous ouvrir la fenàtre êcritures file d''attente des travauxˇ?';

    PROCEDURE SavePassword@1(VAR PasswordKey@1001 : GUID;PasswordText@1000 : Text);
    VAR
      ServicePassword@1002 : Record 1261;
    BEGIN
      IF ISNULLGUID(PasswordKey) OR NOT ServicePassword.GET(PasswordKey) THEN BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.INSERT(TRUE);
        PasswordKey := ServicePassword.Key;
        MODIFY;
      END ELSE BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.MODIFY;
      END;
      COMMIT;
      IF PasswordText <> '' THEN
        CheckEncryption;
    END;

    PROCEDURE GetPassword@2(PasswordKey@1001 : GUID) : Text;
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      ServicePassword.GET(PasswordKey);
      EXIT(ServicePassword.GetPassword);
    END;

    LOCAL PROCEDURE DeletePassword@4(PasswordKey@1000 : GUID);
    VAR
      ServicePassword@1001 : Record 1261;
    BEGIN
      IF ServicePassword.GET(PasswordKey) THEN
        ServicePassword.DELETE;
    END;

    PROCEDURE HasPassword@3(PasswordKey@1001 : GUID) : Boolean;
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      IF NOT ServicePassword.GET(PasswordKey) THEN
        EXIT(FALSE);
      EXIT(ServicePassword.GetPassword <> '');
    END;

    PROCEDURE SetURLsToDefault@5();
    VAR
      OCRServiceMgt@1000 : Codeunit 1294;
    BEGIN
      OCRServiceMgt.SetURLsToDefaultRSO(Rec);
    END;

    LOCAL PROCEDURE CheckEncryption@6();
    BEGIN
      IF NOT ENCRYPTIONENABLED THEN
        IF CONFIRM(EncryptionIsNotActivatedQst) THEN
          PAGE.RUN(PAGE::"Data Encryption Management");
    END;

    PROCEDURE CheckEnabled@7();
    BEGIN
      IF NOT Enabled THEN
        ERROR(MustBeEnabledErr);
    END;

    LOCAL PROCEDURE ScheduleJobQueueEntries@10();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      JobQueueEntry.ScheduleRecurrentJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"OCR - Send to Service");
      JobQueueEntry.ScheduleRecurrentJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"OCR - Receive from Service");
    END;

    LOCAL PROCEDURE CancelJobQueueEntries@11();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      CancelJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"OCR - Send to Service");
      CancelJobQueueEntry(JobQueueEntry."Object Type to Run"::Codeunit,
        CODEUNIT::"OCR - Receive from Service");
    END;

    LOCAL PROCEDURE CancelJobQueueEntry@16(ObjType@1001 : Option;ObjID@1002 : Integer);
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      IF JobQueueEntry.FindJobQueueEntry(ObjType,ObjID) THEN
        JobQueueEntry.Cancel;
    END;

    PROCEDURE ShowJobQueueEntry@8();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      JobQueueEntry.SETRANGE("Object Type to Run",JobQueueEntry."Object Type to Run"::Codeunit);
      JobQueueEntry.SETFILTER("Object ID to Run",'%1|%2',
        CODEUNIT::"OCR - Send to Service",
        CODEUNIT::"OCR - Receive from Service");
      IF JobQueueEntry.FINDFIRST THEN
        PAGE.RUN(PAGE::"Job Queue Entries",JobQueueEntry);
    END;

    BEGIN
    END.
  }
}


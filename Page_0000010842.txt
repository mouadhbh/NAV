OBJECT Page 10842 Apply G/L Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVFR8.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rimd;
    CaptionML=[ENU=Apply G/L Entries;
               FRA=Lettrage ‚critures comptables];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table17;
    DataCaptionExpr=GetCaption;
    PageType=Worksheet;
    OnAfterGetRecord=BEGIN
                       AfterGetCurrentRecord;
                     END;

    OnNewRecord=BEGIN
                  AfterGetCurrentRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1120008 ;1   ;ActionGroup;
                      CaptionML=[ENU=Application;
                                 FRA=Lettrage];
                      Image=Apply }
      { 1120010 ;2   ;Action    ;
                      Name=SetAppliesToID;
                      ShortCutKey=F7;
                      CaptionML=[ENU=Set Applies-to ID;
                                 FRA=Lettrer];
                      Promoted=Yes;
                      Image=SelectLineToApply;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CLEAR(GLEntry);
                                 GLEntry.COPY(Rec);
                                 CurrPage.SETSELECTIONFILTER(GLEntry);

                                 GLEntry.LOCKTABLE;
                                 GLEntry.SETRANGE(Letter,'');
                                 IF GLEntry.FIND('-') THEN BEGIN
                                   // Make Applies-to ID
                                   IF GLEntry."Applies-to ID" <> '' THEN
                                     EntryApplID := ''
                                   ELSE
                                     // EntryApplID := AppliesToID;
                                     IF EntryApplID = '' THEN BEGIN
                                       EntryApplID := USERID;
                                       IF EntryApplID = '' THEN
                                         EntryApplID := '***';
                                     END;

                                   // Set Applies-to ID
                                   REPEAT
                                     // GLEntry.TESTFIELD(Open,TRUE);
                                     GLEntry."Applies-to ID" := EntryApplID;
                                     GLEntry.MODIFY;
                                   UNTIL GLEntry.NEXT = 0;
                                 END;
                               END;
                                }
      { 1120011 ;2   ;Action    ;
                      Name=PostApplication;
                      ShortCutKey=F9;
                      CaptionML=[ENU=Post Application;
                                 FRA=Valider le lettrage];
                      Promoted=Yes;
                      Image=PostApplication;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 GLEntriesApplication.Validate(Rec);
                               END;
                                }
      { 1120012 ;2   ;Action    ;
                      Name=UnapplyEntries;
                      CaptionML=[ENU=Unapply Entries;
                                 FRA=D‚lettrer les ‚critures];
                      Promoted=Yes;
                      Image=UnApply;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CLEAR(GLEntry);
                                 GLEntry.SETRANGE("G/L Account No.","G/L Account No.");
                                 GLEntry.SETRANGE(Letter,Letter);
                                 IF GLEntry.FIND('-') THEN
                                   REPEAT
                                     GLEntry.Letter := '';
                                     GLEntry."Letter Date" := 0D;
                                     GLEntry.MODIFY;
                                   UNTIL GLEntry.NEXT = 0;
                                 IF Letter <> '' THEN
                                   MESSAGE('%1',Text001);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1120003;2;Field     ;
                SourceExpr=Letter }

    { 1120005;2;Field     ;
                SourceExpr="Letter Date" }

    { 1120001;2;Field     ;
                SourceExpr="Applies-to ID" }

    { 10  ;2   ;Field     ;
                SourceExpr=Amount;
                Editable=FALSE }

    { 1120000;1;Group      }

    { 1120017;2;Field     ;
                CaptionML=[ENU=Appln. Code;
                           FRA=Code lettrage];
                SourceExpr=ApplnCode;
                Editable=FALSE;
                OnLookup=BEGIN
                           GLEntry.RESET;
                           GLEntry.SETRANGE(Letter,Letter);
                           PAGE.RUNMODAL(PAGE::"General Ledger Entries",GLEntry);
                         END;
                          }

    { 1120015;2;Field     ;
                CaptionML=[ENU=Debit (LCY);
                           FRA=D‚bit DS];
                SourceExpr=Debit;
                AutoFormatType=1;
                Editable=FALSE }

    { 1120013;2;Field     ;
                CaptionML=[ENU=Credit (LCY;
                           FRA=Cr‚dit (DS)];
                SourceExpr=Credit;
                AutoFormatType=1;
                Editable=FALSE }

    { 1120007;2;Field     ;
                CaptionML=[ENU=Balance;
                           FRA=Solde];
                SourceExpr=Debit - Credit;
                AutoFormatType=1;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      GLAcc@1120008 : Record 15;
      GLEntry@1120001 : Record 17;
      GLEntriesApplication@1120000 : Codeunit 10842;
      EntryApplID@1120002 : Code[50];
      ApplnCode@1120005 : Code[10];
      Debit@1120003 : Decimal;
      Credit@1120004 : Decimal;
      Text001@1120007 : TextConst 'ENU=Successfully unapplied;FRA=D‚lettrage r‚ussi';

    PROCEDURE CalcAmount@1120002();
    VAR
      GLE@1120000 : Record 17;
    BEGIN
      ApplnCode := Letter;
      Debit := 0;
      Credit := 0;

      IF Letter <> '' THEN BEGIN
        GLE.SETRANGE("G/L Account No.","G/L Account No.");
        GLE.SETRANGE(Letter,Letter);
        IF GLE.FIND('-') THEN
          REPEAT
            Debit := Debit + GLE."Debit Amount";
            Credit := Credit + GLE."Credit Amount";
          UNTIL GLE.NEXT = 0;
      END ELSE BEGIN
        Debit := "Debit Amount";
        Credit := "Credit Amount";
      END;
    END;

    LOCAL PROCEDURE GetCaption@2() : Text[250];
    BEGIN
      IF GLAcc."No." <> "G/L Account No." THEN
        IF NOT GLAcc.GET("G/L Account No.") THEN
          IF GETFILTER("G/L Account No.") <> '' THEN
            IF GLAcc.GET(GETRANGEMIN("G/L Account No.")) THEN;
      EXIT(STRSUBSTNO('%1 %2',GLAcc."No.",GLAcc.Name))
    END;

    LOCAL PROCEDURE AfterGetCurrentRecord@19077479();
    BEGIN
      xRec := Rec;
      CalcAmount;
    END;

    BEGIN
    END.
  }
}


OBJECT Table 914 Posted Assemble-to-Order Link
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 910=d,
                TableData 914=d;
    CaptionML=[ENU=Posted Assemble-to-Order Link;
               FRA=Lien Assembler pour commande valid‚];
  }
  FIELDS
  {
    { 1   ;   ;Assembly Document Type;Option      ;CaptionML=[ENU=Assembly Document Type;
                                                              FRA=Type document d'assemblage];
                                                   OptionCaptionML=[ENU=" ,Assembly";
                                                                    FRA=" ,Assemblage"];
                                                   OptionString=[ ,Assembly] }
    { 2   ;   ;Assembly Document No.;Code20       ;TableRelation=IF (Assembly Document Type=CONST(Assembly)) "Posted Assembly Header" WHERE (No.=FIELD(Assembly Document No.));
                                                   CaptionML=[ENU=Assembly Document No.;
                                                              FRA=Nø document d'assemblage] }
    { 12  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              FRA=Type document];
                                                   OptionCaptionML=[ENU=" ,Sales Shipment";
                                                                    FRA=" ,Exp‚dition vente"];
                                                   OptionString=[ ,Sales Shipment] }
    { 13  ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Type=CONST(Sales Shipment)) "Sales Shipment Line" WHERE (Document No.=FIELD(Document No.),
                                                                                                                                       Line No.=FIELD(Document Line No.));
                                                   CaptionML=[ENU=Document No.;
                                                              FRA=Nø document] }
    { 14  ;   ;Document Line No.   ;Integer       ;CaptionML=[ENU=Document Line No.;
                                                              FRA=Nø ligne document] }
    { 20  ;   ;Assembled Quantity  ;Decimal       ;CaptionML=[ENU=Assembled Quantity;
                                                              FRA=Quantit‚ assembl‚e];
                                                   DecimalPlaces=0:5 }
    { 21  ;   ;Assembled Quantity (Base);Decimal  ;CaptionML=[ENU=Assembled Quantity (Base);
                                                              FRA=Quantit‚ assembl‚e (base)];
                                                   DecimalPlaces=0:5 }
    { 32  ;   ;Assembly Order No.  ;Code20        ;CaptionML=[ENU=Assembly Order No.;
                                                              FRA=Nø ordre d'assemblage] }
    { 33  ;   ;Order No.           ;Code20        ;CaptionML=[ENU=Order No.;
                                                              FRA=Nø commande] }
    { 34  ;   ;Order Line No.      ;Integer       ;CaptionML=[ENU=Order Line No.;
                                                              FRA=Nø ligne commande] }
  }
  KEYS
  {
    {    ;Assembly Document Type,Assembly Document No.;
                                                   Clustered=Yes }
    {    ;Document Type,Document No.,Document Line No. }
    {    ;Order No.,Order Line No.                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PostedAsmHeader@1000 : Record 910;

    PROCEDURE AsmExistsForPostedShipmentLine@1(SalesShipmentLine@1000 : Record 111) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Document Type","Document No.","Document Line No.");
      SETRANGE("Document Type","Document Type"::"Sales Shipment");
      SETRANGE("Document No.",SalesShipmentLine."Document No.");
      SETRANGE("Document Line No.",SalesShipmentLine."Line No.");
      EXIT(FINDFIRST);
    END;

    PROCEDURE DeleteAsmFromSalesShptLine@2(SalesShptLine@1000 : Record 111);
    BEGIN
      IF AsmExistsForPostedShipmentLine(SalesShptLine) THEN BEGIN
        DELETE;

        IF GetPostedAsmHeader THEN BEGIN
          PostedAsmHeader.DELETE(TRUE);
          PostedAsmHeader.INIT;
        END;
      END;
    END;

    PROCEDURE ShowPostedAsm@11(SalesShptLine@1000 : Record 111);
    BEGIN
      IF AsmExistsForPostedShipmentLine(SalesShptLine) THEN BEGIN
        GetPostedAsmHeader;
        PAGE.RUNMODAL(PAGE::"Posted Assembly Order",PostedAsmHeader);
      END;
    END;

    PROCEDURE ShowSalesShpt@13(PostedAsmHeader@1000 : Record 910);
    VAR
      SalesShptHeader@1001 : Record 110;
    BEGIN
      IF GET("Assembly Document Type"::Assembly,PostedAsmHeader."No.") THEN
        IF "Document Type" = "Document Type"::"Sales Shipment" THEN BEGIN
          SalesShptHeader.GET("Document No.");
          PAGE.RUNMODAL(PAGE::"Posted Sales Shipment",SalesShptHeader);
        END;
    END;

    LOCAL PROCEDURE GetPostedAsmHeader@19() : Boolean;
    BEGIN
      IF PostedAsmHeader."No." = "Assembly Document No." THEN
        EXIT(TRUE);
      EXIT(PostedAsmHeader.GET("Assembly Document No."));
    END;

    PROCEDURE FindLinksFromSalesLine@3(SalesLine@1000 : Record 37) : Boolean;
    BEGIN
      CASE SalesLine."Document Type" OF
        SalesLine."Document Type"::Order:
          BEGIN
            SETCURRENTKEY("Order No.","Order Line No.");
            SETRANGE("Order No.",SalesLine."Document No.");
            SETRANGE("Order Line No.",SalesLine."Line No.");
          END;
        SalesLine."Document Type"::Invoice:
          BEGIN
            SETCURRENTKEY("Document Type","Document No.","Document Line No.");
            SETRANGE("Document Type","Document Type"::"Sales Shipment");
            SETRANGE("Document No.",SalesLine."Shipment No.");
            SETRANGE("Document Line No.",SalesLine."Shipment Line No.");
          END;
        ELSE
          SalesLine.FIELDERROR("Document Type");
      END;
      EXIT(FINDSET);
    END;

    BEGIN
    END.
  }
}


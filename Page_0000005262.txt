OBJECT Page 5262 CRM Coupling Unit of Measure
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Microsoft Dynamics CRM Unit Group Coupling;
               FRA=Couplage de groupe d'unit‚s Microsoft Dynamics CRM];
    SourceTable=Table204;
    PageType=StandardDialog;
    OnInit=BEGIN
             SynchAction := SynchAction::PushToCRM;
             SetPushActionCheckboxes;
           END;

    OnAfterGetRecord=VAR
                       CRMIntegrationRecord@1000 : Record 5331;
                       CRMID@1001 : GUID;
                     BEGIN
                       IF CRMIntegrationRecord.FindIDFromRecordID(RECORDID,CRMID) THEN BEGIN
                         CRMUomschedule.GET(CRMID);
                         OriginalCRMUomschedule := CRMUomschedule;
                         SynchAction := SynchAction::DoNotSynch;
                         SetPushActionCheckboxes;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=Coupling;
                           FRA=Couplage];
                GroupType=Group;
                Layout=Rows }

    { 3   ;2   ;Group     ;
                CaptionML=[ENU=Dynamics NAV;
                           FRA=Dynamics NAV];
                GroupType=Group }

    { 4   ;3   ;Field     ;
                CaptionML=[ENU=Unit of Measure;
                           FRA=Unit‚];
                SourceExpr=Code;
                Editable=FALSE }

    { 5   ;3   ;Field     ;
                Name=SynchActionControl;
                CaptionML=[ENU=Synchronize After Coupling;
                           FRA=Synchroniser aprŠs le couplage];
                OptionCaptionML=[ENU=No,Yes - Use the Dynamics NAV data;
                                 FRA=Non,Oui - Utiliser les donn‚es Dynamics NAV];
                SourceExpr=SynchAction;
                Enabled=NOT CreateNewInCRM;
                Editable=NOT CreateNewInCRM;
                OnValidate=BEGIN
                             SetPushActionCheckboxes;
                           END;
                            }

    { 6   ;2   ;Group     ;
                CaptionML=[ENU=Dynamics CRM;
                           FRA=Dynamics CRM];
                GroupType=Group }

    { 7   ;3   ;Field     ;
                Name=CRM Uomschedule Name;
                CaptionML=[ENU=Unit Group;
                           FRA=Groupe d'unit‚s];
                SourceExpr=CRMUomschedule.Name;
                Enabled=NOT CreateNewInCRM;
                OnValidate=VAR
                             ManualCRMUomschedule@1000 : Record 5362;
                             PreviouslySelectedCRMUomschedule@1001 : Record 5362;
                             CRMUomscheduleFound@1002 : Boolean;
                           BEGIN
                             PreviouslySelectedCRMUomschedule := CRMUomschedule;
                             ManualCRMUomschedule.SETRANGE(Name,CRMUomschedule.Name);

                             IF ManualCRMUomschedule.FINDFIRST THEN
                               CRMUomscheduleFound := TRUE
                             ELSE BEGIN
                               ManualCRMUomschedule.SETCURRENTKEY(Name);
                               ManualCRMUomschedule.SETFILTER(Name,'%1',CRMUomschedule.Name + '*');
                               IF ManualCRMUomschedule.FINDFIRST THEN
                                 CRMUomscheduleFound := TRUE;
                             END;

                             IF CRMUomscheduleFound THEN BEGIN
                               CRMUomschedule := ManualCRMUomschedule;
                               IF PreviouslySelectedCRMUomschedule.UoMScheduleId <> CRMUomschedule.UoMScheduleId THEN
                                 HandleNewCRMUomschedule;
                             END ELSE
                               ERROR(STRSUBSTNO(NoSuchCRMRecordErr,CRMUomschedule.Name));
                           END;

                OnLookup=VAR
                           PreviouslySelectedCRMUomschedule@1002 : Record 5362;
                           CRMUnitGroupList@1000 : Page 5362;
                         BEGIN
                           CRMUnitGroupList.SETRECORD(CRMUomschedule);
                           CRMUnitGroupList.SetCurrentlyCoupledCRMUomschedule(OriginalCRMUomschedule);
                           CRMUnitGroupList.LOOKUPMODE(TRUE);
                           PreviouslySelectedCRMUomschedule := CRMUomschedule;

                           IF CRMUnitGroupList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             CRMUnitGroupList.GETRECORD(CRMUomschedule);
                             IF PreviouslySelectedCRMUomschedule.UoMScheduleId <> CRMUomschedule.UoMScheduleId THEN
                               HandleNewCRMUomschedule;
                           END;
                         END;
                          }

    { 8   ;3   ;Field     ;
                Name=CreateNewControl;
                CaptionML=[ENU=Create New Unit Group;
                           FRA=Cr‚er un nouveau groupe d'unit‚s];
                SourceExpr=CreateNewInCRM;
                OnValidate=BEGIN
                             IF CreateNewInCRM THEN BEGIN
                               SavedCRMUomschedule := CRMUomschedule;
                               SavedSynchAction := SynchAction;
                               SynchAction := SynchAction::PushToCRM;
                               CRMUomschedule.INIT;
                             END ELSE BEGIN
                               CRMUomschedule := SavedCRMUomschedule;
                               SynchAction := SavedSynchAction;
                             END;

                             SetPushActionCheckboxes;
                           END;
                            }

    { 9   ;1   ;Group     ;
                CaptionML=[ENU=Coupled Records;
                           FRA=Enregistrements coupl‚s];
                Editable=FALSE;
                GroupType=Group }

    { 10  ;2   ;Group     ;
                GroupType=FixedLayout }

    { 11  ;3   ;Group     ;
                CaptionML=[ENU=Dynamics NAV Unit of Measure;
                           FRA=Unit‚ Dynamics NAV];
                GroupType=Group;
                Layout=Columns }

    { 12  ;4   ;Field     ;
                Name=Code2;
                CaptionML=[ENU=Code;
                           FRA=Code];
                SourceExpr=Code }

    { 13  ;4   ;Field     ;
                SourceExpr=Description }

    { 14  ;4   ;Field     ;
                SourceExpr="International Standard Code" }

    { 15  ;4   ;Field     ;
                Name=PushToCRMControl;
                CaptionML=[ENU=Use for Synchronization;
                           FRA=Utiliser pour la synchronisation];
                SourceExpr=PushToCRM }

    { 16  ;3   ;Group     ;
                CaptionML=[ENU=Dynamics CRM Unit Group;
                           FRA=Groupe d'unit‚s Dynamics CRM];
                GroupType=Group }

    { 17  ;4   ;Field     ;
                SourceExpr=CRMUomschedule.Name }

    { 18  ;4   ;Field     ;
                SourceExpr=CRMUomschedule.BaseUoMName }

    { 19  ;4   ;Field     ;
                SourceExpr=CRMUomschedule.StateCode }

    { 20  ;4   ;Field     ;
                SourceExpr=CRMUomschedule.StatusCode }

    { 21  ;4   ;Field     ;
                Name=PushToNAVControl;
                SourceExpr=PushToNAV }

  }
  CODE
  {
    VAR
      CRMUomschedule@1001 : Record 5362;
      OriginalCRMUomschedule@1010 : Record 5362;
      IntegrationTableMapping@1009 : Record 5335;
      SavedCRMUomschedule@1007 : Record 5362;
      CreateNewInCRM@1000 : Boolean;
      PushToCRM@1003 : Boolean;
      PushToNAV@1004 : Boolean;
      InitialSynchDisabledErr@1006 : TextConst 'ENU=No initial synchronization direction was specified because initial synchronization was disabled.;FRA=Aucune direction de synchronisation initiale n''a ‚t‚ sp‚cifi‚e car la synchronisation initiale a ‚t‚ d‚sactiv‚e.';
      NoSuchCRMRecordErr@1011 : TextConst '@@@="%1 = The Unit Group name entered by the user";ENU=Unit Group with name %1 does not exist in Microsoft Dynamics CRM.;FRA=Le groupe d''unit‚s portant le nom %1 n''existe pas dans Microsoft Dynamics CRM.';
      SynchAction@1012 : 'DoNotSynch,PushToCRM';
      SavedSynchAction@1013 : Integer;

    LOCAL PROCEDURE SetPushActionCheckboxes@14();
    BEGIN
      IF SynchAction = SynchAction::DoNotSynch THEN BEGIN
        PushToCRM := FALSE;
        PushToNAV := FALSE;
      END ELSE BEGIN
        PushToCRM := SynchAction = SynchAction::PushToCRM;
        PushToNAV := NOT PushToCRM;
      END
    END;

    LOCAL PROCEDURE HandleNewCRMUomschedule@7();
    VAR
      CRMIntegrationRecord@1001 : Record 5331;
    BEGIN
      IF OriginalCRMUomschedule.UoMScheduleId <> CRMUomschedule.UoMScheduleId THEN
        CRMIntegrationRecord.AssertRecordIDCanBeCoupled(RECORDID,CRMUomschedule.UoMScheduleId);
    END;

    PROCEDURE GetCRMID@6() : GUID;
    BEGIN
      EXIT(CRMUomschedule.UoMScheduleId);
    END;

    PROCEDURE GetCreateNewInCRM@19() : Boolean;
    BEGIN
      EXIT(CreateNewInCRM);
    END;

    PROCEDURE GetPerformInitialSynchronization@3() : Boolean;
    BEGIN
      EXIT(SynchAction <> SynchAction::DoNotSynch);
    END;

    PROCEDURE GetInitialSynchronizationDirection@4() : Integer;
    BEGIN
      IF SynchAction = SynchAction::DoNotSynch THEN
        ERROR(InitialSynchDisabledErr);

      EXIT(IntegrationTableMapping.Direction::ToIntegrationTable);
    END;

    BEGIN
    END.
  }
}


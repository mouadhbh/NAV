OBJECT Table 99000763 Routing Header
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               MfgSetup.GET;
               IF "No." = '' THEN BEGIN
                 MfgSetup.TESTFIELD("Routing Nos.");
                 NoSeriesMgt.InitSeries(MfgSetup."Routing Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               Item@1000 : Record 27;
               RtngLine@1001 : Record 99000764;
               MfgComment@1002 : Record 99000770;
             BEGIN
               MfgComment.SETRANGE("Table Name",MfgComment."Table Name"::"Routing Header");
               MfgComment.SETRANGE("No.","No.");
               MfgComment.DELETEALL;

               RtngLine.LOCKTABLE;
               RtngLine.SETRANGE("Routing No.","No.");
               RtngLine.DELETEALL(TRUE);

               RtngVersion.SETRANGE("Routing No.","No.");
               RtngVersion.DELETEALL;

               Item.SETCURRENTKEY("Routing No.");
               Item.SETRANGE("Routing No.","No.");
               IF Item.FINDFIRST THEN
                 ERROR(Text000);
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text002,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[ENU=Routing Header;
               FRA=En-tˆte gamme];
    LookupPageID=Page99000764;
    DrillDownPageID=Page99000764;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Description;
                                                   CaptionML=[ENU=No.;
                                                              FRA=Nø] }
    { 2   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                "Search Description" := Description;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 3   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              FRA=D‚signation 2] }
    { 4   ;   ;Search Description  ;Code50        ;CaptionML=[ENU=Search Description;
                                                              FRA=D‚signation de recherche] }
    { 10  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              FRA=Date dern. modification];
                                                   Editable=No }
    { 12  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Manufacturing Comment Line" WHERE (Table Name=CONST(Routing Header),
                                                                                                         No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              FRA=Commentaires];
                                                   Editable=No }
    { 20  ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN
                                                                  CheckRouting.Calculate(Rec,'');

                                                                IF Status = Status::Closed THEN BEGIN
                                                                  IF CONFIRM(
                                                                       Text001,FALSE)
                                                                  THEN BEGIN
                                                                    RtngVersion.SETRANGE("Routing No.","No.");
                                                                    RtngVersion.MODIFYALL(Status,RtngVersion.Status::Closed);
                                                                  END ELSE
                                                                    Status := xRec.Status;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              FRA=Statut];
                                                   OptionCaptionML=[ENU=New,Certified,Under Development,Closed;
                                                                    FRA=Cr‚ation en cours,Valid‚e,Modification en cours,Cl“tur‚e];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Serial,Parallel;
                                                                    FRA=S‚quentielle,ParallŠle];
                                                   OptionString=Serial,Parallel }
    { 50  ;   ;Version Nos.        ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Version Nos.;
                                                              FRA=Nø version] }
    { 51  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Description                       }
    {    ;Description                              }
    {    ;Status                                   }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Status                   }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This Routing is being used on Items.;FRA=Cette gamme est utilis‚e dans des articles.';
      Text001@1001 : TextConst 'ENU=All versions attached to the routing will be closed. Close routing?;FRA=Toutes les versions rattach‚es … la gamme vont ˆtre cl“tur‚es. Souhaitez-vous cl“turer la gamme ?';
      MfgSetup@1002 : Record 99000765;
      RoutingHeader@1003 : Record 99000763;
      RtngVersion@1004 : Record 99000786;
      CheckRouting@1005 : Codeunit 99000752;
      NoSeriesMgt@1006 : Codeunit 396;
      Text002@1007 : TextConst 'ENU=You cannot rename the %1 when %2 is %3.;FRA=Vous ne pouvez pas renommer l''enregistrement %1 lorsque la valeur %2 est %3.';

    PROCEDURE AssistEdit@2(OldRtngHeader@1000 : Record 99000763) : Boolean;
    BEGIN
      WITH RoutingHeader DO BEGIN
        RoutingHeader := Rec;
        MfgSetup.GET;
        MfgSetup.TESTFIELD("Routing Nos.");
        IF NoSeriesMgt.SelectSeries(MfgSetup."Routing Nos.",OldRtngHeader."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := RoutingHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}


OBJECT Table 474 Job Queue Log Entry
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Queue Log Entry;
               FRA=criture journal file d'attente des travaux];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              FRA=Nø s‚quence] }
    { 2   ;   ;ID                  ;GUID          ;CaptionML=[ENU=ID;
                                                              FRA=ID] }
    { 3   ;   ;User ID             ;Text65        ;TableRelation=User."User Name";
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              FRA=Code utilisateur] }
    { 4   ;   ;Start Date/Time     ;DateTime      ;CaptionML=[ENU=Start Date/Time;
                                                              FRA=Date/heure d‚but] }
    { 5   ;   ;End Date/Time       ;DateTime      ;CaptionML=[ENU=End Date/Time;
                                                              FRA=Date/heure fin] }
    { 6   ;   ;Object Type to Run  ;Option        ;CaptionML=[ENU=Object Type to Run;
                                                              FRA=Type objet … ex‚cuter];
                                                   OptionCaptionML=[ENU=,,,Report,,Codeunit;
                                                                    FRA=,,,Report,,Codeunit];
                                                   OptionString=,,,Report,,Codeunit }
    { 7   ;   ;Object ID to Run    ;Integer       ;CaptionML=[ENU=Object ID to Run;
                                                              FRA=ID objet … ex‚cuter] }
    { 8   ;   ;Object Caption to Run;Text250      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type to Run),
                                                                                                                Object ID=FIELD(Object ID to Run)));
                                                   CaptionML=[ENU=Object Caption to Run;
                                                              FRA=L‚gende de l'objet … ex‚cuter] }
    { 9   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              FRA=Statut];
                                                   OptionCaptionML=[ENU=Success,In Process,Error;
                                                                    FRA=SuccŠs,En cours,Erreur];
                                                   OptionString=Success,In Process,Error }
    { 10  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 11  ;   ;Error Message       ;Text250       ;CaptionML=[ENU=Error Message;
                                                              FRA=Message d'erreur] }
    { 12  ;   ;Error Message 2     ;Text250       ;CaptionML=[ENU=Error Message 2;
                                                              FRA=Message d'erreur 2] }
    { 13  ;   ;Error Message 3     ;Text250       ;CaptionML=[ENU=Error Message 3;
                                                              FRA=Message d'erreur 3] }
    { 14  ;   ;Error Message 4     ;Text250       ;CaptionML=[ENU=Error Message 4;
                                                              FRA=Message d'erreur 4] }
    { 15  ;   ;Job Queue Code      ;Code10        ;TableRelation="Job Queue";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Job Queue Code;
                                                              FRA=Code file d'attente des travaux] }
    { 16  ;   ;Processed by User ID;Text65        ;CaptionML=[ENU=Processed by User ID;
                                                              FRA=Trait‚ par Code utilisateur] }
    { 17  ;   ;Job Queue Category Code;Code10     ;TableRelation="Job Queue Category";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Job Queue Category Code;
                                                              FRA=Code cat‚gorie de la file d'attente des travaux] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;ID                                       }
    {    ;Start Date/Time,ID                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=There is no error message.;FRA=Absence de message d''erreur.';
      Text002@1000 : TextConst 'ENU=Are you sure that you want to delete job queue log entries?;FRA=Voulez-vous vraiment supprimer les ‚critures du journal de la file d''attente des travauxÿ?';
      Text003@1002 : TextConst 'ENU=Marked as Error by %1.;FRA=Marqu‚ comme erreur par %1.';
      Text004@1003 : TextConst 'ENU=Only entries with status In Progress can be marked as Error.;FRA=Seules les ‚critures dont le statut est En cours peuvent ˆtre marqu‚es comme Erreur.';

    PROCEDURE GetErrorMessage@1() : Text;
    BEGIN
      EXIT("Error Message" + "Error Message 2" + "Error Message 3" + "Error Message 4");
    END;

    PROCEDURE SetErrorMessage@2(ErrorText@1000 : Text);
    BEGIN
      "Error Message 2" := '';
      "Error Message 3" := '';
      "Error Message 4" := '';
      "Error Message" := COPYSTR(ErrorText,1,250);
      IF STRLEN(ErrorText) > 250 THEN
        "Error Message 2" := COPYSTR(ErrorText,251,250);
      IF STRLEN(ErrorText) > 500 THEN
        "Error Message 3" := COPYSTR(ErrorText,501,250);
      IF STRLEN(ErrorText) > 750 THEN
        "Error Message 4" := COPYSTR(ErrorText,751,250);
    END;

    PROCEDURE DeleteEntries@3(DaysOld@1000 : Integer);
    BEGIN
      IF NOT CONFIRM(Text002) THEN
        EXIT;
      SETFILTER(Status,'<>%1',Status::"In Process");
      SETFILTER("End Date/Time",'<=%1',CREATEDATETIME(TODAY - DaysOld,TIME));
      DELETEALL;
      SETRANGE("End Date/Time");
      SETRANGE(Status);
    END;

    PROCEDURE ShowErrorMessage@8();
    VAR
      e@1000 : Text;
    BEGIN
      e := GetErrorMessage;
      IF e = '' THEN
        e := Text001;
      MESSAGE(e);
    END;

    PROCEDURE MarkAsError@4();
    VAR
      JobQueueEntry@1003 : Record 472;
      ErrorMessage@1002 : Text[1000];
    BEGIN
      IF Status <> Status::"In Process" THEN
        ERROR(Text004);

      ErrorMessage := STRSUBSTNO(Text003,USERID);

      IF JobQueueEntry.GET(ID) THEN
        JobQueueEntry.SetError(ErrorMessage);

      Status := Status::Error;
      SetErrorMessage(ErrorMessage);
      MODIFY;
    END;

    BEGIN
    END.
  }
}


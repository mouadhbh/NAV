OBJECT Report 1136 Copy Cost Acctg. Budget to G/L
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Cost Acctg. Budget to G/L;
               FRA=Copier budget Comptabilit‚ analytique vers cpta. g‚n.];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NoOfCopies = 0 THEN
                     NoOfCopies := 1;
                   IF Factor = 0 THEN
                     Factor := 1;
                 END;

  }
  DATASET
  {
    { 3233;    ;DataItem;                    ;
               DataItemTable=Table1109;
               DataItemTableView=SORTING(Budget Name,Cost Type No.,Date);
               OnPreDataItem=BEGIN
                               IF Factor <= 0 THEN
                                 ERROR(Text000);

                               IF NoOfCopies < 1 THEN
                                 ERROR(Text001);

                               IF (NoOfCopies > 1) AND (DateChange = '') THEN
                                 ERROR(Text002);

                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text008);

                               IF GLBudgetNameTarget.Name = '' THEN
                                 ERROR(Text009);

                               IF NOT CONFIRM(Text004,FALSE,GETFILTER("Budget Name"),GLBudgetNameTarget.Name,Factor,NoOfCopies,GETFILTER(Date),DateChange) THEN
                                 ERROR('');

                               LOCKTABLE;

                               IF GLBudgetEntryTarget.FINDLAST THEN
                                 LastEntryNo := GLBudgetEntryTarget."Entry No.";

                               Window.OPEN(Text005);

                               Window.UPDATE(1,COUNT);
                               Window.UPDATE(2,NoOfCopies);
                             END;

               OnAfterGetRecord=VAR
                                  TempDimSetEntry@1000 : TEMPORARY Record 480;
                                BEGIN
                                  GLBudgetEntryTarget.INIT;
                                  GLBudgetEntryTarget."Budget Name" := GLBudgetNameTarget.Name;

                                  // Get corresponding G/L account
                                  IF NOT CostType.GET("Cost Type No.") THEN BEGIN
                                    NoSkipped := NoSkipped + 1;
                                    CurrReport.SKIP;
                                  END;

                                  GLAcc.SETFILTER("No.",'%1',CostType."G/L Account Range");
                                  IF NOT GLAcc.FINDFIRST THEN BEGIN
                                    NoSkipped := NoSkipped + 1;
                                    CurrReport.SKIP;
                                  END;
                                  GLBudgetEntryTarget."G/L Account No." := GLAcc."No.";
                                  GLBudgetEntryTarget.Date := Date;

                                  CostAccSetup.GET;
                                  IF CostAccMgt.CostCenterExistsAsDimValue("Cost Center Code") THEN
                                    GLBudgetEntryTarget.UpdateDimSet(TempDimSetEntry,CostAccSetup."Cost Center Dimension","Cost Center Code");
                                  IF CostAccMgt.CostObjectExistsAsDimValue("Cost Object Code") THEN
                                    GLBudgetEntryTarget.UpdateDimSet(TempDimSetEntry,CostAccSetup."Cost Object Dimension","Cost Object Code");
                                  GLBudgetEntryTarget."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                                  UpdateBudgetDimensions(GLBudgetEntryTarget);
                                  GLBudgetEntryTarget.Description :=
                                    COPYSTR(STRSUBSTNO(Text006,GETFILTER("Budget Name")),1,MAXSTRLEN(GLBudgetEntryTarget.Description));
                                  GLBudgetEntryTarget."User ID" := USERID;

                                  // Amt * req. window factor
                                  GLBudgetEntryTarget.Amount := ROUND(Amount * Factor,0.01);

                                  // Create entries according to "copies". Increment date.
                                  FOR i := 1 TO NoOfCopies DO BEGIN
                                    LastEntryNo := LastEntryNo + 1;
                                    GLBudgetEntryTarget."Entry No." := LastEntryNo;

                                    // Prepare date for next entry
                                    IF DateChange <> '' THEN
                                      GLBudgetEntryTarget.Date := CALCDATE(DateFormula,GLBudgetEntryTarget.Date);

                                    GLBudgetEntryTarget.INSERT;

                                    NoInserted := NoInserted + 1;
                                    IF (NoInserted MOD 100) = 0 THEN
                                      Window.UPDATE(3,NoInserted);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF NoInserted = 0 THEN BEGIN
                                  MESSAGE(Text010,NoSkipped);
                                  ERROR('');
                                END;

                                IF NOT CONFIRM(Text007,TRUE,NoInserted,GLBudgetNameTarget.Name,NoSkipped) THEN
                                  ERROR('');
                              END;

               ReqFilterFields=Budget Name,Cost Type No.,Cost Center Code,Cost Object Code,Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               GLBudgetNameTarget.INIT;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1   ;2   ;Field     ;
                  Name=Allocation Target Budget Name;
                  Lookup=Yes;
                  CaptionML=[ENU=Allocation Target Budget Name;
                             FRA=Cible de ventilation - Nom du budget];
                  SourceExpr=GLBudgetNameTarget.Name;
                  TableRelation="G/L Budget Name" }

      { 3   ;2   ;Field     ;
                  Name=Amount multiplication factor;
                  CaptionML=[ENU=Amount multiplication factor;
                             FRA=Facteur de multiplication montant];
                  SourceExpr=Factor }

      { 8   ;2   ;Field     ;
                  Name=No. of Copies;
                  CaptionML=[ENU=No. of Copies;
                             FRA=Nombre de copies];
                  SourceExpr=NoOfCopies }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=[ENU=Date Change Formula;
                             FRA=Formule modification date];
                  DateFormula=Yes;
                  SourceExpr=DateChange;
                  OnValidate=BEGIN
                               EVALUATE(DateFormula,DateChange);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLBudgetEntryTarget@1000 : Record 96;
      GLBudgetNameTarget@1001 : Record 95;
      CostType@1002 : Record 1103;
      GLAcc@1003 : Record 15;
      CostAccSetup@1004 : Record 1108;
      CostAccMgt@1005 : Codeunit 1100;
      DimMgt@1006 : Codeunit 408;
      Window@1007 : Dialog;
      DateChange@1008 : Code[10];
      DateFormula@1009 : DateFormula;
      LastEntryNo@1010 : Integer;
      NoOfCopies@1011 : Integer;
      Factor@1012 : Decimal;
      i@1013 : Integer;
      NoSkipped@1014 : Integer;
      NoInserted@1015 : Integer;
      Text000@1016 : TextConst 'ENU=The multiplication factor must not be 0 or less than 0.;FRA=Le facteur de multiplication ne doit pas ˆtre inf‚rieur ou ‚gal … 0.';
      Text001@1017 : TextConst 'ENU=Number of copies must be at least 1.;FRA=Le nombre de copies doit ˆtre au moins d‚fini sur 1.';
      Text002@1018 : TextConst 'ENU=If more than one copy is created, a formula for date change must be defined.;FRA=Si plusieurs copies sont cr‚‚es, une formule de modification de date doit ˆtre d‚finie.';
      Text004@1020 : TextConst '@@@="%3=multiplication factor (decimal);%4=No of copies (integer)";ENU=Cost budget "%1" will be copied to G/L budget "%2". The budget amounts will be multiplied by factor %3. \%4 copies will be created and the posting will be increased from the range of "%5" to "%6".\\Do you want do copy the budget?;FRA=Le budget des co–ts ®ÿ%1ÿ¯ sera copi‚ vers le budget comptabilit‚ ®ÿ%2ÿ¯. Les montants budg‚taires seront multipli‚s par le facteur %3. \%4 copies seront cr‚‚es et la validation passera de ®ÿ%5ÿ¯ … ®ÿ%6ÿ¯.\\Voulez-vous copier le budgetÿ?';
      Text005@1021 : TextConst 'ENU=Copying budget entries\No of entries #1#####\No of copies  #2#####\Copied        #3#####;FRA=Copie ‚critures budget\Nombre d''‚critures #1#####\Nombre d''‚critures #2#####\Copi‚        #3#####';
      Text006@1022 : TextConst '@@@=%1 - Budget Name.;ENU=Copy of cost budget %1;FRA=Copie du budget des co–ts %1';
      Text007@1023 : TextConst '@@@="%2=budget name;%3=integer value";ENU=%1 entries generated in budget %2.\\%3 entries were skipped because there were no corresponding G/L accounts defined.\\Copy entries?;FRA=%1 ‚critures g‚n‚r‚es dans le budget %2.\\%3 ‚critures ont ‚t‚ ignor‚es car aucun compte g‚n‚ral correspondant n''a ‚t‚ d‚fini.\\Copier les ‚crituresÿ?';
      Text008@1024 : TextConst 'ENU=Define name of source budget.;FRA=D‚finissez le nom du budget origine.';
      Text009@1025 : TextConst 'ENU=Define name of target budget.;FRA=D‚finissez le nom du budget cible.';
      Text010@1026 : TextConst 'ENU=No entries were copied. %1 entries were skipped because no corresponding general ledger accounts were defined.;FRA=Aucune ‚criture n''a ‚t‚ copi‚e. %1 ‚critures ont ‚t‚ ignor‚es car aucun compte g‚n‚ral correspondant n''a ‚t‚ d‚fini.';

    LOCAL PROCEDURE UpdateBudgetDimensions@1(VAR GLBudgetEntry@1000 : Record 96);
    VAR
      GLSetup@1001 : Record 98;
      GLBudgetName@1002 : Record 95;
      DimSetEntry@1003 : Record 480;
    BEGIN
      GLSetup.GET;
      WITH GLBudgetEntry DO BEGIN
        GLBudgetName.GET("Budget Name");

        "Global Dimension 1 Code" := '';
        "Global Dimension 2 Code" := '';
        "Budget Dimension 1 Code" := '';
        "Budget Dimension 2 Code" := '';
        "Budget Dimension 3 Code" := '';
        "Budget Dimension 4 Code" := '';

        IF DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 1 Code") THEN
          "Global Dimension 1 Code" := DimSetEntry."Dimension Value Code";
        IF DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 2 Code") THEN
          "Global Dimension 2 Code" := DimSetEntry."Dimension Value Code";
        IF DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 1 Code") THEN
          "Budget Dimension 1 Code" := DimSetEntry."Dimension Value Code";
        IF DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 2 Code") THEN
          "Budget Dimension 2 Code" := DimSetEntry."Dimension Value Code";
        IF DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 3 Code") THEN
          "Budget Dimension 3 Code" := DimSetEntry."Dimension Value Code";
        IF DimSetEntry.GET("Dimension Set ID",GLBudgetName."Budget Dimension 4 Code") THEN
          "Budget Dimension 4 Code" := DimSetEntry."Dimension Value Code";
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


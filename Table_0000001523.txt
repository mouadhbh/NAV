OBJECT Table 1523 Workflow Step Argument
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1523=rim;
    DataCaptionFields=General Journal Template Name,General Journal Batch Name,Notification User ID;
    OnInsert=BEGIN
               ID := CREATEGUID;
             END;

    OnModify=BEGIN
               CheckEditingIsAllowed;
             END;

    OnDelete=BEGIN
               CheckEditingIsAllowed;
             END;

    OnRename=BEGIN
               CheckEditingIsAllowed;
             END;

    CaptionML=[ENU=Workflow Step Argument;
               FRA=Argument d'Çtape de flux de travail];
    LookupPageID=Page1523;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=[ENU=ID;
                                                              FRA=ID] }
    { 2   ;   ;Type                ;Option        ;TableRelation="Workflow Step".Type WHERE (Argument=FIELD(ID));
                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Event,Response;
                                                                    FRA=êvÇnement,RÇponse];
                                                   OptionString=Event,Response }
    { 3   ;   ;General Journal Template Name;Code10;
                                                   TableRelation="Gen. Journal Template".Name;
                                                   CaptionML=[ENU=General Journal Template Name;
                                                              FRA=Nom du modäle feuille comptabilitÇ] }
    { 4   ;   ;General Journal Batch Name;Code10  ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(General Journal Template Name));
                                                   CaptionML=[ENU=General Journal Batch Name;
                                                              FRA=Nom feuille comptabilitÇ] }
    { 5   ;   ;Notification User ID;Code50        ;TableRelation="User Setup"."User ID";
                                                   CaptionML=[ENU=Notification User ID;
                                                              FRA=Code utilisateur de la notification] }
    { 7   ;   ;Response Function Name;Code128     ;TableRelation="Workflow Response"."Function Name";
                                                   CaptionML=[ENU=Response Function Name;
                                                              FRA=Nom de fonction de la rÇponse] }
    { 9   ;   ;Link Target Page    ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Page));
                                                   CaptionML=[ENU=Link Target Page;
                                                              FRA=Page cible du lien] }
    { 10  ;   ;Custom Link         ;Text250       ;OnValidate=VAR
                                                                WebRequestHelper@1000 : Codeunit 1299;
                                                              BEGIN
                                                                WebRequestHelper.IsValidUri("Custom Link");
                                                              END;

                                                   ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Custom Link;
                                                              FRA=Lien personnalisÇ] }
    { 11  ;   ;Event Conditions    ;BLOB          ;CaptionML=[ENU=Event Conditions;
                                                              FRA=Conditions d'ÇvÇnement] }
    { 12  ;   ;Approver Type       ;Option        ;CaptionML=[ENU=Approver Type;
                                                              FRA=Type approbateur];
                                                   OptionCaptionML=[ENU=Salesperson/Purchaser,Approver,Workflow User Group;
                                                                    FRA=Vendeur/Acheteur,Approbateur,Groupe d'utilisateurs du flux de travail];
                                                   OptionString=Salesperson/Purchaser,Approver,Workflow User Group }
    { 13  ;   ;Approver Limit Type ;Option        ;CaptionML=[ENU=Approver Limit Type;
                                                              FRA=Type limite approbateur];
                                                   OptionCaptionML=[ENU=Approver Chain,Direct Approver,First Qualified Approver;
                                                                    FRA=Chaåne d'approbateurs,Approbateur direct,Premier approbateur qualifiÇ];
                                                   OptionString=Approver Chain,Direct Approver,First Qualified Approver }
    { 14  ;   ;Workflow User Group Code;Code20    ;TableRelation="Workflow User Group".Code;
                                                   CaptionML=[ENU=Workflow User Group Code;
                                                              FRA=Code du groupe d'utilisateurs du flux de travail] }
    { 15  ;   ;Due Date Formula    ;DateFormula   ;OnValidate=BEGIN
                                                                IF COPYSTR(FORMAT("Due Date Formula"),1,1) = '-' THEN
                                                                  ERROR(STRSUBSTNO(NoNegValuesErr,FIELDCAPTION("Due Date Formula")));
                                                              END;

                                                   CaptionML=[ENU=Due Date Formula;
                                                              FRA=Formule ÇchÇance] }
    { 16  ;   ;Message             ;Text250       ;CaptionML=[ENU=Message;
                                                              FRA=Message] }
    { 17  ;   ;Delegate After      ;Option        ;CaptionML=[ENU=Delegate After;
                                                              FRA=DÇlÇguer apräs];
                                                   OptionCaptionML=[ENU=Never,1 day,2 days,5 days;
                                                                    FRA=Jamais,1ˇjour,2ˇjours,5ˇjours];
                                                   OptionString=Never,1 day,2 days,5 days }
    { 18  ;   ;Show Confirmation Message;Boolean  ;CaptionML=[ENU=Show Confirmation Message;
                                                              FRA=Afficher le message de confirmation] }
    { 19  ;   ;Table No.           ;Integer       ;CaptionML=[ENU=Table No.;
                                                              FRA=N¯ table] }
    { 20  ;   ;Field No.           ;Integer       ;CaptionML=[ENU=Field No.;
                                                              FRA=N¯ champ] }
    { 21  ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              FRA=LÇgende champ];
                                                   Editable=No }
    { 100 ;   ;Response Option Group;Code20       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Workflow Response"."Response Option Group" WHERE (Function Name=FIELD(Response Function Name)));
                                                   CaptionML=[ENU=Response Option Group;
                                                              FRA=Groupe d'options de rÇponse];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoNegValuesErr@1000 : TextConst 'ENU=%1 must be a positive value.;FRA=%1 doit àtre une valeur positive.';

    PROCEDURE Clone@1() : GUID;
    VAR
      WorkflowStepArgument@1001 : Record 1523;
    BEGIN
      CALCFIELDS("Event Conditions");
      WorkflowStepArgument.COPY(Rec);
      WorkflowStepArgument.INSERT(TRUE);
      EXIT(WorkflowStepArgument.ID);
    END;

    PROCEDURE Equals@9(WorkflowStepArgument@1000 : Record 1523;SkipBlob@1004 : Boolean) : Boolean;
    VAR
      TypeHelper@1003 : Codeunit 10;
      OtherRecRef@1002 : RecordRef;
      ThisRecRef@1001 : RecordRef;
    BEGIN
      ThisRecRef.GETTABLE(Rec);
      OtherRecRef.GETTABLE(WorkflowStepArgument);

      IF NOT TypeHelper.Equals(ThisRecRef,OtherRecRef,SkipBlob) THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE GetEventFilters@8() Filters : Text;
    VAR
      FiltersInStream@1002 : InStream;
    BEGIN
      IF "Event Conditions".HASVALUE THEN BEGIN
        CALCFIELDS("Event Conditions");
        "Event Conditions".CREATEINSTREAM(FiltersInStream);
        FiltersInStream.READ(Filters);
      END;
    END;

    PROCEDURE SetEventFilters@2(Filters@1000 : Text);
    VAR
      FiltersOutStream@1001 : OutStream;
    BEGIN
      "Event Conditions".CREATEOUTSTREAM(FiltersOutStream);
      FiltersOutStream.WRITE(Filters);
      MODIFY(TRUE);
    END;

    LOCAL PROCEDURE CheckEditingIsAllowed@12();
    VAR
      Workflow@1000 : Record 1501;
      WorkflowStep@1001 : Record 1502;
    BEGIN
      IF ISNULLGUID(ID) THEN
        EXIT;

      WorkflowStep.SETRANGE(Argument,ID);
      IF WorkflowStep.FINDFIRST THEN BEGIN
        Workflow.GET(WorkflowStep."Workflow Code");
        Workflow.CheckEditingIsAllowed;
      END;
    END;

    BEGIN
    END.
  }
}


OBJECT Page 380 Bank Acc. Reconciliation Lines
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               FRA=Lignes];
    LinksAllowed=No;
    SourceTable=Table274;
    DelayedInsert=Yes;
    SourceTableView=WHERE(Statement Type=CONST(Bank Reconciliation));
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             BalanceEnable := TRUE;
             TotalBalanceEnable := TRUE;
             TotalDiffEnable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       SetUserInteractions;
                     END;

    OnNewRecord=BEGIN
                  IF BelowxRec THEN
                    CalcBalance(xRec."Statement Line No.")
                  ELSE
                    CalcBalance(xRec."Statement Line No." - 1);
                END;

    OnDeleteRecord=BEGIN
                     SetUserInteractions;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           IF "Statement Line No." <> 0 THEN
                             CalcBalance("Statement Line No.");
                           SetUserInteractions;
                         END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 9       ;1   ;Action    ;
                      Name=ShowStatementLineDetails;
                      CaptionML=[ENU=Details;
                                 FRA=D‚tails];
                      RunObject=Page 1221;
                      RunPageLink=Data Exch. No.=FIELD(Data Exch. Entry No.),
                                  Line No.=FIELD(Data Exch. Line No.) }
      { 11      ;1   ;Action    ;
                      Name=ApplyEntries;
                      CaptionML=[ENU=&Apply Entries...;
                                 FRA=&Lettrer ‚critures...];
                      Enabled=ApplyEntriesAllowed;
                      Image=ApplyEntries;
                      OnAction=BEGIN
                                 ApplyEntries;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Transaction Date";
                StyleExpr=StyleTxt }

    { 22  ;2   ;Field     ;
                SourceExpr="Value Date";
                Visible=FALSE }

    { 24  ;2   ;Field     ;
                SourceExpr="Document No.";
                Visible=FALSE }

    { 21  ;2   ;Field     ;
                SourceExpr="Check No.";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             SetUserInteractions;
                           END;
                            }

    { 6   ;2   ;Field     ;
                SourceExpr=Description;
                StyleExpr=StyleTxt }

    { 8   ;2   ;Field     ;
                SourceExpr="Statement Amount";
                StyleExpr=StyleTxt }

    { 10  ;2   ;Field     ;
                SourceExpr="Applied Amount" }

    { 12  ;2   ;Field     ;
                SourceExpr=Difference }

    { 14  ;2   ;Field     ;
                SourceExpr="Applied Entries";
                Visible=FALSE }

    { 3   ;2   ;Field     ;
                SourceExpr="Related-Party Name";
                Visible=FALSE }

    { 5   ;2   ;Field     ;
                SourceExpr="Additional Transaction Info";
                Visible=FALSE }

    { 16  ;1   ;Group      }

    { 13  ;2   ;Field      }

    { 26  ;2   ;Field     ;
                Name=Balance;
                CaptionML=[ENU=Balance;
                           FRA=Solde];
                SourceExpr=Balance + "Statement Amount";
                AutoFormatType=1;
                AutoFormatExpr=GetCurrencyCode;
                Enabled=BalanceEnable;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                Name=TotalBalance;
                CaptionML=[ENU=Total Balance;
                           FRA=Solde final];
                SourceExpr=TotalBalance + "Statement Amount";
                AutoFormatType=1;
                AutoFormatExpr=GetCurrencyCode;
                Enabled=TotalBalanceEnable;
                Editable=FALSE }

    { 19  ;2   ;Field     ;
                Name=TotalDiff;
                CaptionML=[ENU=Total Difference;
                           FRA=Total diff‚rence];
                SourceExpr=TotalDiff + Difference;
                AutoFormatType=1;
                AutoFormatExpr=GetCurrencyCode;
                Enabled=TotalDiffEnable;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      BankAccRecon@1000 : Record 273;
      StyleTxt@1005 : Text;
      TotalDiff@1002 : Decimal;
      Balance@1003 : Decimal;
      TotalBalance@1004 : Decimal;
      TotalDiffEnable@19038859 : Boolean INDATASET;
      TotalBalanceEnable@19047480 : Boolean INDATASET;
      BalanceEnable@19079599 : Boolean INDATASET;
      ApplyEntriesAllowed@1001 : Boolean;

    LOCAL PROCEDURE CalcBalance@3(BankAccReconLineNo@1000 : Integer);
    VAR
      TempBankAccReconLine@1001 : Record 274;
    BEGIN
      IF BankAccRecon.GET("Statement Type","Bank Account No.","Statement No.") THEN;

      TempBankAccReconLine.COPY(Rec);

      TotalDiff := -Difference;
      IF TempBankAccReconLine.CALCSUMS(Difference) THEN BEGIN
        TotalDiff := TotalDiff + TempBankAccReconLine.Difference;
        TotalDiffEnable := TRUE;
      END ELSE
        TotalDiffEnable := FALSE;

      TotalBalance := BankAccRecon."Balance Last Statement" - "Statement Amount";
      IF TempBankAccReconLine.CALCSUMS("Statement Amount") THEN BEGIN
        TotalBalance := TotalBalance + TempBankAccReconLine."Statement Amount";
        TotalBalanceEnable := TRUE;
      END ELSE
        TotalBalanceEnable := FALSE;

      Balance := BankAccRecon."Balance Last Statement" - "Statement Amount";
      TempBankAccReconLine.SETRANGE("Statement Line No.",0,BankAccReconLineNo);
      IF TempBankAccReconLine.CALCSUMS("Statement Amount") THEN BEGIN
        Balance := Balance + TempBankAccReconLine."Statement Amount";
        BalanceEnable := TRUE;
      END ELSE
        BalanceEnable := FALSE;
    END;

    LOCAL PROCEDURE ApplyEntries@5();
    VAR
      BankAccReconApplyEntries@1000 : Codeunit 374;
    BEGIN
      "Ready for Application" := TRUE;
      CurrPage.SAVERECORD;
      COMMIT;
      BankAccReconApplyEntries.ApplyEntries(Rec);
    END;

    PROCEDURE GetSelectedRecords@4(VAR TempBankAccReconciliationLine@1000 : TEMPORARY Record 274);
    VAR
      BankAccReconciliationLine@1001 : Record 274;
    BEGIN
      CurrPage.SETSELECTIONFILTER(BankAccReconciliationLine);
      IF BankAccReconciliationLine.FINDSET THEN
        REPEAT
          TempBankAccReconciliationLine := BankAccReconciliationLine;
          TempBankAccReconciliationLine.INSERT;
        UNTIL BankAccReconciliationLine.NEXT = 0;
    END;

    LOCAL PROCEDURE SetUserInteractions@2();
    BEGIN
      StyleTxt := GetStyle;
      ApplyEntriesAllowed := Type = Type::"Check Ledger Entry";
    END;

    PROCEDURE ToggleMatchedFilter@1(SetFilterOn@1000 : Boolean);
    BEGIN
      IF SetFilterOn THEN
        SETFILTER(Difference,'<>%1',0)
      ELSE
        RESET;
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}


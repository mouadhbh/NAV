OBJECT Table 8618 Config. Template Header
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               CALCFIELDS("Used In Hierarchy");
               IF NOT "Used In Hierarchy" THEN BEGIN
                 ConfigTemplateLine.SETRANGE("Data Template Code",Code);
                 ConfigTemplateLine.DELETEALL;
               END;
             END;

    OnRename=BEGIN
               CALCFIELDS("Used In Hierarchy");
               IF NOT "Used In Hierarchy" THEN BEGIN
                 ConfigTemplateLine.SETRANGE("Data Template Code",xRec.Code);
                 ConfigTemplateLine.FIND('-');
                 REPEAT
                   ConfigTemplateLine.RENAME(Code,ConfigTemplateLine."Line No.");
                 UNTIL ConfigTemplateLine.NEXT = 0;
               END;
             END;

    CaptionML=[ENU=Config. Template Header;
               FRA=En-tˆte modŠle config.];
    LookupPageID=Page8620;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 3   ;   ;Table ID            ;Integer       ;OnValidate=BEGIN
                                                                TestXRec;
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ConfigValidateMgt.LookupTable("Table ID");
                                                              IF "Table ID" <> 0 THEN
                                                                VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              FRA=ID table] }
    { 4   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              FRA=Nom table];
                                                   Editable=No }
    { 5   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              FRA=L‚gende table];
                                                   Editable=No }
    { 6   ;   ;Used In Hierarchy   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Config. Template Line" WHERE (Data Template Code=FIELD(Code),
                                                                                                    Type=CONST(Template)));
                                                   CaptionML=[ENU=Used In Hierarchy;
                                                              FRA=Utilis‚ dans la hi‚rarchie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Template lines that relate to %1 exists. Delete the lines to change the Table ID.;FRA=Il existe des lignes modŠle reli‚es … %1. Supprimez-les pour modifier l''ID table.';
      ConfigTemplateLine@1001 : Record 8619;
      Text001@1002 : TextConst '@@@="%2 = Table ID, %3 = Table Caption";ENU=A new instance %1 has been created in table %2 %3.;FRA=Une nouvelle instance %1 a ‚t‚ cr‚‚e dans la table %2 %3.';
      ConfigValidateMgt@1003 : Codeunit 8617;

    LOCAL PROCEDURE TestXRec@1();
    VAR
      ConfigTemplateLine@1000 : Record 8619;
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",Code);
      IF ConfigTemplateLine.FINDFIRST THEN
        IF xRec."Table ID" <> "Table ID" THEN
          ERROR(Text000,xRec."Table ID");
    END;

    PROCEDURE ConfirmNewInstance@2(VAR RecRef@1000 : RecordRef);
    VAR
      KeyRef@1004 : KeyRef;
      FieldRef@1003 : FieldRef;
      KeyFieldCount@1002 : Integer;
      MessageString@1001 : Text[1024];
    BEGIN
      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        MessageString := MessageString + ' ' + FORMAT(FieldRef.VALUE);
        MessageString := DELCHR(MessageString,'<');
        MESSAGE(STRSUBSTNO(Text001,MessageString,RecRef.NUMBER,RecRef.CAPTION));
      END;
    END;

    BEGIN
    END.
  }
}


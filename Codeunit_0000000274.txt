OBJECT Codeunit 274 Res. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=236;
    OnRun=BEGIN
            ResJnlBatch.COPY(Rec);
            Code;
            Rec := ResJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;FRA=Souhaitez-vous valider les feuilles et imprimer le rapport de validation ?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;FRA=Les feuilles ont t valides avec succs.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";FRA="Il n''a pas t possible de valider l''ensemble des feuilles. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;FRA=Les feuilles qui n''ont pas t valides avec succs sont maintenant marques.';
      ResJnlTemplate@1004 : Record 206;
      ResJnlBatch@1005 : Record 236;
      ResJnlLine@1006 : Record 207;
      ResReg@1007 : Record 240;
      ResJnlPostBatch@1008 : Codeunit 213;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ResJnlBatch DO BEGIN
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");

        IF NOT CONFIRM(Text000) THEN
          EXIT;

        FIND('-');
        REPEAT
          ResJnlLine."Journal Template Name" := "Journal Template Name";
          ResJnlLine."Journal Batch Name" := Name;
          ResJnlLine."Line No." := 1;
          CLEAR(ResJnlPostBatch);
          IF ResJnlPostBatch.RUN(ResJnlLine) THEN BEGIN
            MARK(FALSE);
            IF ResReg.GET(ResJnlLine."Line No.") THEN BEGIN
              ResReg.SETRECFILTER;
              REPORT.RUN(ResJnlTemplate."Posting Report ID",FALSE,FALSE,ResReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}


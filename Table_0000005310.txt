OBJECT Table 5310 Outlook Synch. Setup Detail
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckOSynchEntity;
               SetTableNo;
             END;

    OnModify=BEGIN
               CheckOSynchEntity;
               SetTableNo;
             END;

    CaptionML=[ENU=Outlook Synch. Setup Detail;
               FRA=D‚tail paramŠtres synch. Outlook];
    LookupPageID=Page5310;
    DrillDownPageID=Page5310;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation="Outlook Synch. User Setup"."User ID";
                                                   CaptionML=[ENU=User ID;
                                                              FRA=Code utilisateur];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity Element"."Synch. Entity Code";
                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              FRA=Code entit‚ synch.];
                                                   Editable=No }
    { 3   ;   ;Element No.         ;Integer       ;TableRelation="Outlook Synch. Entity Element"."Element No.";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Outlook Collection");
                                                              END;

                                                   CaptionML=[ENU=Element No.;
                                                              FRA=Nø ‚l‚ment];
                                                   Editable=No }
    { 4   ;   ;Outlook Collection  ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity Element"."Outlook Collection" WHERE (Synch. Entity Code=FIELD(Synch. Entity Code),
                                                                                                                                  Element No.=FIELD(Element No.)));
                                                   OnLookup=VAR
                                                              ElementNo@1000 : Integer;
                                                            BEGIN
                                                              ElementNo := OSynchSetupMgt.ShowOEntityCollections("User ID","Synch. Entity Code");

                                                              IF (ElementNo <> 0) AND ("Element No." <> ElementNo) THEN
                                                                VALIDATE("Element No.",ElementNo);
                                                            END;

                                                   CaptionML=[ENU=Outlook Collection;
                                                              FRA=Collection Outlook];
                                                   Editable=No }
    { 5   ;   ;Table No.           ;Integer       ;CaptionML=[ENU=Table No.;
                                                              FRA=Nø table] }
  }
  KEYS
  {
    {    ;User ID,Synch. Entity Code,Element No.  ;Clustered=Yes }
    {    ;Table No.                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchSetupMgt@1000 : Codeunit 5300;
      Text001@1001 : TextConst 'ENU=This collection cannot be synchronized because the relation between this collection and the dependent entity %1 was not defined.;FRA=Impossible de synchroniser cette collection car la relation qui la lie … l''entit‚ d‚pendante %1 n''a pas ‚t‚ d‚finie.';

    PROCEDURE CheckOSynchEntity@3();
    VAR
      OSynchEntityElement@1000 : Record 5301;
      OSynchDependency@1001 : Record 5311;
    BEGIN
      OSynchEntityElement.GET("Synch. Entity Code","Element No.");
      OSynchEntityElement.TESTFIELD("Table No.");
      OSynchEntityElement.TESTFIELD("Outlook Collection");
      OSynchEntityElement.TESTFIELD("Table Relation");

      OSynchEntityElement.CALCFIELDS("No. of Dependencies");
      IF OSynchEntityElement."No. of Dependencies" = 0 THEN
        EXIT;

      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code",OSynchEntityElement."Synch. Entity Code");
      OSynchDependency.SETRANGE("Element No.",OSynchEntityElement."Element No.");
      IF OSynchDependency.FIND('-') THEN
        REPEAT
          IF OSynchDependency."Table Relation" = '' THEN
            ERROR(Text001,OSynchDependency."Depend. Synch. Entity Code");
        UNTIL OSynchDependency.NEXT = 0;
    END;

    PROCEDURE SetTableNo@2();
    VAR
      OSynchEntityElement@1000 : Record 5301;
    BEGIN
      OSynchEntityElement.GET("Synch. Entity Code","Element No.");
      "Table No." := OSynchEntityElement."Table No.";
    END;

    BEGIN
    END.
  }
}


OBJECT Codeunit 1003 Job Task-Indent
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=1001;
    OnRun=BEGIN
            TESTFIELD("Job No.");
            IF NOT
               CONFIRM(
                 Text000 +
                 Text001 +
                 Text002 +
                 Text003,TRUE)
            THEN
              EXIT;
            JT := Rec;
            Indent("Job No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This function updates the indentation of all the Job Tasks.;FRA=Cette fonction met … jour l''indentation de toutes les tƒches projet.';
      Text001@1001 : TextConst 'ENU="All Job Tasks between a Begin-Total and the matching End-Total are indented one level. ";FRA="Toutes les tƒches projet entre un champ D‚but total et le champ Fin total correspondant sont indent‚es d''un niveau. "';
      Text002@1002 : TextConst 'ENU=The Totaling for each End-total is also updated.;FRA=La totalisation pour chaque compte de type Fin total est aussi mise … jour.';
      Text003@1003 : TextConst 'ENU=\\Do you want to indent the Job Tasks?;FRA=\\Voulez-vous indenter les tƒches projet ?';
      Text004@1004 : TextConst 'ENU=Indenting the Job Tasks #1##########.;FRA=Indentation des tƒches projet #1##########.';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;FRA=Il manque un D‚but total pour le Fin total %1.';
      JT@1006 : Record 1001;
      Window@1007 : Dialog;
      JTNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1(JobNo@1000 : Code[20]);
    BEGIN
      Window.OPEN(Text004);
      JT.SETRANGE("Job No.",JobNo);
      WITH JT DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"Job Task No.");

            IF "Job Task Type" = "Job Task Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "Job Task No.");
              Totaling := JTNo[i] + '..' + "Job Task No.";
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Job Task Type" = "Job Task Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              JTNo[i] := "Job Task No.";
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}


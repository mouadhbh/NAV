OBJECT Codeunit 358 DateFilter-Calc
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00,NAVFR9.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Fiscal Year %1;FRA=Exercice comptable %1';
      AccountingPeriod@1001 : Record 50;
      StartDate@1002 : Date;
      Text10800@1120000 : TextConst 'ENU=The selected date is not a starting period.;FRA=La date choisie n''est pas un d‚but de p‚riode.';
      Text10801@1120001 : TextConst 'ENU=The starting date must be the first day of a month.;FRA=La date de d‚but doit ˆtre le premier jour du mois.';
      Text10802@1120002 : TextConst 'ENU=The ending date must be the last day of a month.;FRA=La date de fin doit correspondre au dernier jour du mois.';
      DateInsteadOfPeriodErr@1120003 : TextConst 'ENU=You must enter a date interval, such as 01.01.17..31.01.17.;FRA=Vous devez entrer un intervalle de temps, par exemple 01.01.17..31.01.17.';

    PROCEDURE CreateFiscalYearFilter@3(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];Date@1002 : Date;NextStep@1003 : Integer);
    BEGIN
      CreateAccountingDateFilter(Filter,Name,TRUE,Date,NextStep);
    END;

    PROCEDURE CreateAccountingPeriodFilter@2(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];Date@1002 : Date;NextStep@1003 : Integer);
    BEGIN
      CreateAccountingDateFilter(Filter,Name,FALSE,Date,NextStep);
    END;

    LOCAL PROCEDURE CreateAccountingDateFilter@1(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];FiscalYear@1002 : Boolean;Date@1003 : Date;NextStep@1004 : Integer);
    BEGIN
      AccountingPeriod.RESET;
      IF FiscalYear THEN
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
      AccountingPeriod."Starting Date" := Date;
      AccountingPeriod.FIND('=<>');
      IF AccountingPeriod."Starting Date" > Date THEN
        NextStep := NextStep - 1;
      IF NextStep <> 0 THEN
        IF AccountingPeriod.NEXT(NextStep) <> NextStep THEN BEGIN
          IF NextStep < 0 THEN
            Filter := '..' + FORMAT(AccountingPeriod."Starting Date" - 1)
          ELSE
            Filter := FORMAT(AccountingPeriod."Starting Date") + '..' + FORMAT(31129999D);
          Name := '...';
          EXIT;
        END;
      StartDate := AccountingPeriod."Starting Date";
      IF FiscalYear THEN
        Name := STRSUBSTNO(Text000,FORMAT(DATE2DMY(StartDate,3)))
      ELSE
        Name := AccountingPeriod.Name;
      IF AccountingPeriod.NEXT <> 0 THEN
        Filter := FORMAT(StartDate) + '..' + FORMAT(AccountingPeriod."Starting Date" - 1)
      ELSE BEGIN
        Filter := FORMAT(StartDate) + '..' + FORMAT(31129999D);
        Name := Name + '...';
      END;
    END;

    PROCEDURE VerifiyDateFilter@1120000(Filter@1120000 : Text[30]);
    BEGIN
      IF Filter = ',,,' THEN
        ERROR(Text10800);
    END;

    PROCEDURE VerifMonthPeriod@1120001(Filter@1120000 : Text[30]);
    VAR
      Date@1120001 : Record 2000000007;
      FilterDate@1120002 : Date;
      FilterPos@1120007 : Integer;
    BEGIN
      IF COPYSTR(Filter,STRLEN(Filter) - 1,2) = '..' THEN
        EXIT;
      // Begin Check
      FilterPos := STRPOS(Filter,'..');

      IF FilterPos = 0 THEN
        ERROR(DateInsteadOfPeriodErr);

      EVALUATE(FilterDate,COPYSTR(Filter,1,FilterPos-1));
      Date.SETRANGE("Period Type",Date."Period Type"::Month);
      Date.SETRANGE(Date."Period Start",FilterDate);
      IF NOT Date.FIND('-') THEN
        ERROR(Text10801);

      Date.RESET;
      // Ending check
      IF FilterPos <> 0 THEN BEGIN
        EVALUATE(FilterDate,COPYSTR(Filter,FilterPos + 2,8));
        Date.SETRANGE("Period Type",Date."Period Type"::Month);
        Date.SETRANGE(Date."Period End",CLOSINGDATE(FilterDate));
        IF NOT Date.FIND('-') THEN
          ERROR(Text10802);
      END;
    END;

    PROCEDURE ReturnEndingPeriod@1120002(StartPeriod@1120000 : Date;PeriodType@1120001 : 'Date,Week,Month,Quarter,Year') : Date;
    VAR
      PeriodDate@1120002 : Record 2000000007;
    BEGIN
      PeriodDate.SETRANGE("Period Type",PeriodType);
      PeriodDate.SETRANGE("Period Start",StartPeriod);
      IF PeriodDate.FIND('-') THEN
        EXIT(PeriodDate."Period End")
      ELSE
        EXIT(0D);
    END;

    BEGIN
    END.
  }
}


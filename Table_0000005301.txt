OBJECT Table 5301 Outlook Synch. Entity Element
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Table No.");

               IF ISNULLGUID("Record GUID") THEN
                 "Record GUID" := CREATEGUID;
             END;

    OnDelete=BEGIN
               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchSetupDetail.SETRANGE("Element No.","Element No.");
               IF NOT OSynchSetupDetail.ISEMPTY THEN
                 ERROR(Text001);

               OSynchSetupDetail.DELETEALL(TRUE);

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchField.RESET;
               OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchField.SETRANGE("Element No.","Element No.");
               OSynchField.DELETEALL(TRUE);

               OSynchDependency.RESET;
               OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchDependency.SETRANGE("Element No.","Element No.");
               OSynchDependency.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Outlook Synch. Entity Element;
               FRA=êlÇment entitÇ synch. Outlook];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              FRA=Code entitÇ synch.];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=[ENU=Element No.;
                                                              FRA=N¯ ÇlÇment] }
    { 3   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF "Table No." <> xRec."Table No." THEN BEGIN
                                                                  CheckUserSetup;
                                                                  CheckMasterTableNo;
                                                                  TESTFIELD("Table No.");

                                                                  IF NOT OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") THEN
                                                                    EXIT;

                                                                  IF "Element No." <> 0 THEN BEGIN
                                                                    IF NOT
                                                                       CONFIRM(
                                                                         STRSUBSTNO(
                                                                           Text003,
                                                                           OSynchField.TABLECAPTION,
                                                                           OSynchFilter.TABLECAPTION,
                                                                           OSynchDependency.TABLECAPTION))
                                                                    THEN BEGIN
                                                                      "Table No." := xRec."Table No.";
                                                                      EXIT;
                                                                    END;

                                                                    OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                    OSynchField.SETRANGE("Element No.","Element No.");
                                                                    OSynchField.DELETEALL(TRUE);

                                                                    OSynchFilter.RESET;
                                                                    OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                    OSynchFilter.DELETEALL;

                                                                    OSynchDependency.RESET;
                                                                    OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                    OSynchDependency.SETRANGE("Element No.","Element No.");
                                                                    OSynchDependency.DELETEALL(TRUE);

                                                                    "Table Relation" := '';
                                                                  END;
                                                                END;

                                                                CALCFIELDS("Table Caption","No. of Dependencies");
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              CheckMasterTableNo;
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              IF TableNo <> 0 THEN
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=[ENU=Table No.;
                                                              FRA=N¯ table];
                                                   BlankZero=Yes }
    { 4   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              FRA=LÇgende table];
                                                   Editable=No }
    { 5   ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                              END;

                                                   CaptionML=[ENU=Table Relation;
                                                              FRA=Relation de table];
                                                   Editable=No }
    { 6   ;   ;Outlook Collection  ;Text80        ;OnValidate=BEGIN
                                                                IF "Outlook Collection" <> '' THEN BEGIN
                                                                  OSynchEntity.GET("Synch. Entity Code");
                                                                  IF NOT OSynchSetupMgt.ValidateOutlookCollectionName("Outlook Collection",OSynchEntity."Outlook Item") THEN
                                                                    ERROR(Text002);
                                                                  CheckCollectionName;
                                                                END;

                                                                IF "Outlook Collection" = xRec."Outlook Collection" THEN
                                                                  EXIT;

                                                                CheckUserSetup;
                                                                CheckMasterTableNo;

                                                                IF "Element No." = 0 THEN
                                                                  EXIT;

                                                                IF xRec."Outlook Collection" <> '' THEN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       STRSUBSTNO(
                                                                         Text003,
                                                                         OSynchField.TABLECAPTION,
                                                                         OSynchFilter.TABLECAPTION,
                                                                         OSynchDependency.TABLECAPTION))
                                                                  THEN BEGIN
                                                                    "Outlook Collection" := xRec."Outlook Collection";
                                                                    EXIT;
                                                                  END;

                                                                OSynchField.RESET;
                                                                OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchField.SETRANGE("Element No.","Element No.");
                                                                OSynchField.DELETEALL(TRUE);

                                                                OSynchDependency.RESET;
                                                                OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchDependency.SETRANGE("Element No.","Element No.");
                                                                OSynchDependency.DELETEALL(TRUE);
                                                              END;

                                                   OnLookup=VAR
                                                              CollectionName@1000 : Text[80];
                                                            BEGIN
                                                              CheckMasterTableNo;
                                                              OSynchEntity.GET("Synch. Entity Code");

                                                              OSynchEntity.TESTFIELD("Outlook Item");

                                                              CollectionName := OSynchSetupMgt.ShowOCollectionsList(OSynchEntity."Outlook Item");

                                                              IF CollectionName <> '' THEN
                                                                VALIDATE("Outlook Collection",CollectionName);
                                                            END;

                                                   CaptionML=[ENU=Outlook Collection;
                                                              FRA=Collection Outlook] }
    { 7   ;   ;Master Table No.    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity"."Table No." WHERE (Code=FIELD(Synch. Entity Code)));
                                                   CaptionML=[ENU=Master Table No.;
                                                              FRA=N¯ table principale];
                                                   Editable=No }
    { 8   ;   ;Record GUID         ;GUID          ;CaptionML=[ENU=Record GUID;
                                                              FRA=GUID enregistrement];
                                                   Editable=No }
    { 9   ;   ;No. of Dependencies ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Outlook Synch. Dependency" WHERE (Synch. Entity Code=FIELD(Synch. Entity Code),
                                                                                                        Element No.=FIELD(Element No.)));
                                                   CaptionML=[ENU=No. of Dependencies;
                                                              FRA=Nbre de dÇpendances];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.          ;Clustered=Yes }
    {    ;Table No.                                }
    {    ;Record GUID                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1003 : Record 5300;
      OSynchFilter@1002 : Record 5303;
      OSynchField@1001 : Record 5304;
      OSynchDependency@1012 : Record 5311;
      OSynchSetupDetail@1011 : Record 5310;
      OSynchSetupMgt@1000 : Codeunit 5300;
      Text001@1005 : TextConst 'ENU=You cannot delete this collection because it is used with synchronization.;FRA=Vous ne pouvez pas supprimer cette collection car elle est utilisÇe avec la synchronisation.';
      Text002@1007 : TextConst 'ENU=The Outlook item collection with this name does not exist.\Click the AssistButton to see a list of valid collections for this Outlook item.;FRA=La collection d''ÇlÇments Outlook portant ce nom n''existe pas.\Cliquez sur l''AssistButton pour afficher une liste de collections valides pour cet ÇlÇment Outlook.';
      Text003@1004 : TextConst 'ENU=If you change the value in this field, the %1, %2, and %3 records related to this collection will be deleted.\Do you want to change it anyway?;FRA=Si vous modifiez la valeur de ce champ, les enregistrements %1, %2 et %3 associÇs Ö cette collection seront supprimÇs.\Voulez-vous quand màme la modifier ?';
      Text004@1006 : TextConst 'ENU=You cannot change this collection because it is used with synchronization for user %1.;FRA=Vous ne pouvez pas modifier cette collection car elle est utilisÇe avec la synchronisation pour l''utilisateur %1.';
      Text005@1008 : TextConst 'ENU="An Outlook item collection with this name already exists.\Identification fields and values:\%1=''''%2'''',%3=''''%4''''.";FRA="Une collection d''ÇlÇments Outlook portant ce nom existe dÇjÖ.\Valeurs et champs d''identification :\%1=''''%2'''',%3=''''%4''''."';

    PROCEDURE ShowElementFields@1();
    BEGIN
      TESTFIELD("Synch. Entity Code");
      TESTFIELD("Element No.");
      TESTFIELD("Table No.");
      TESTFIELD("Table Relation");
      TESTFIELD("Outlook Collection");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");

      PAGE.RUNMODAL(PAGE::"Outlook Synch. Fields",OSynchField);
    END;

    PROCEDURE ShowDependencies@3();
    BEGIN
      TESTFIELD("Synch. Entity Code");
      TESTFIELD("Element No.");
      TESTFIELD("Table No.");
      TESTFIELD("Table Relation");
      TESTFIELD("Outlook Collection");

      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchDependency.SETRANGE("Element No.","Element No.");

      PAGE.RUNMODAL(PAGE::"Outlook Synch. Dependencies",OSynchDependency);
      CALCFIELDS("No. of Dependencies");
    END;

    PROCEDURE CheckMasterTableNo@2();
    BEGIN
      CALCFIELDS("Master Table No.");
      IF "Master Table No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        OSynchEntity.TESTFIELD("Table No.");
      END;
    END;

    PROCEDURE CheckUserSetup@4();
    VAR
      OSynchUserSetup@1000 : Record 5305;
    BEGIN
      OSynchUserSetup.RESET;
      OSynchUserSetup.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      IF NOT OSynchUserSetup.FINDSET THEN
        EXIT;

      REPEAT
        OSynchUserSetup.CALCFIELDS("No. of Elements");
        IF OSynchUserSetup."No. of Elements" > 0 THEN
          IF OSynchSetupDetail.GET(OSynchUserSetup."User ID","Synch. Entity Code","Element No.") THEN
            ERROR(Text004,OSynchUserSetup."User ID");
      UNTIL OSynchUserSetup.NEXT = 0;
    END;

    PROCEDURE CheckCollectionName@5();
    VAR
      OSynchEntityElement@1000 : Record 5301;
    BEGIN
      OSynchEntityElement.RESET;
      OSynchEntityElement.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchEntityElement.SETRANGE("Outlook Collection","Outlook Collection");
      IF OSynchEntityElement.FINDFIRST THEN
        ERROR(
          Text005,
          OSynchEntityElement.FIELDCAPTION("Synch. Entity Code"),
          OSynchEntityElement."Synch. Entity Code",
          OSynchEntityElement.FIELDCAPTION("Element No."),
          OSynchEntityElement."Element No.");
    END;

    BEGIN
    END.
  }
}


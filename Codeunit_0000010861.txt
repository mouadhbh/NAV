OBJECT Codeunit 10861 Payment-Apply
{
  OBJECT-PROPERTIES
  {
    Date=23/10/15;
    Time=12:00:00;
    Version List=NAVFR9.00.00.43402;
  }
  PROPERTIES
  {
    TableNo=10866;
    Permissions=TableData 21=rm,
                TableData 25=rm;
    OnRun=VAR
            Header@1120000 : Record 10865;
            PaymentToleranceMgt@1120001 : Codeunit 426;
          BEGIN
            Header.GET("No.");

            GenJnlLine."Account Type" := "Account Type";
            GenJnlLine."Account No." := "Account No.";
            GenJnlLine.Amount := Amount;
            GenJnlLine."Amount (LCY)" := "Amount (LCY)";
            GenJnlLine."Currency Code" := "Currency Code";
            GenJnlLine."Posting Date" := Header."Posting Date";
            GenJnlLine."Document Type" := GenJnlLine."Document Type"::Payment;
            GenJnlLine."Applies-to ID" := "Applies-to ID";

            WITH GenJnlLine DO BEGIN
              GetCurrency;
              AccType := "Account Type";
              AccNo := "Account No.";

              CASE AccType OF
                AccType::Customer:
                  BEGIN
                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                    CustLedgEntry.SETRANGE(Open,TRUE);
                    "Applies-to ID" := GetAppliesToID("Applies-to ID",Rec);
                    ApplyCustEntries.SetGenJnlLine(GenJnlLine,FIELDNO("Applies-to ID"));
                    ApplyCustEntries.SETRECORD(CustLedgEntry);
                    ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
                    ApplyCustEntries.LOOKUPMODE(TRUE);
                    OK := ApplyCustEntries.RUNMODAL = ACTION::LookupOK;
                    CLEAR(ApplyCustEntries);
                    IF NOT OK THEN
                      EXIT;
                    CustLedgEntry.RESET;
                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                    CustLedgEntry.SETRANGE(Open,TRUE);
                    CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                    IF CustLedgEntry.FIND('-') THEN BEGIN
                      CurrencyCode2 := CustLedgEntry."Currency Code";
                      IF Amount = 0 THEN BEGIN
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                          CustLedgEntry.CALCFIELDS("Remaining Amount");
                          CustLedgEntry."Remaining Amount" :=
                            CurrExchRate.ExchangeAmount(
                              CustLedgEntry."Remaining Amount",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Remaining Amount" :=
                            ROUND(CustLedgEntry."Remaining Amount",Currency."Amount Rounding Precision");
                          CustLedgEntry."Remaining Pmt. Disc. Possible" :=
                            CurrExchRate.ExchangeAmount(
                              CustLedgEntry."Remaining Pmt. Disc. Possible",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Remaining Pmt. Disc. Possible" :=
                            ROUND(CustLedgEntry."Remaining Pmt. Disc. Possible",Currency."Amount Rounding Precision");
                          CustLedgEntry."Amount to Apply" :=
                            CurrExchRate.ExchangeAmount(
                              CustLedgEntry."Amount to Apply",
                              CustLedgEntry."Currency Code","Currency Code","Posting Date");
                          CustLedgEntry."Amount to Apply" :=
                            ROUND(CustLedgEntry."Amount to Apply",Currency."Amount Rounding Precision");
                          IF ((CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::"Credit Memo") AND
                              (CustLedgEntry."Remaining Pmt. Disc. Possible" <> 0) OR
                              (CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Invoice)) AND
                             ("Posting Date" <= CustLedgEntry."Pmt. Discount Date")
                          THEN
                            Amount := Amount - (CustLedgEntry."Amount to Apply" - CustLedgEntry."Remaining Pmt. Disc. Possible")
                          ELSE
                            Amount := Amount - CustLedgEntry."Amount to Apply";
                        UNTIL CustLedgEntry.NEXT = 0;
                        "Amount (LCY)" := Amount;
                        "Currency Factor" := 1;
                        IF ("Bal. Account Type" = "Bal. Account Type"::Customer) OR
                           ("Bal. Account Type" = "Bal. Account Type"::Vendor)
                        THEN BEGIN
                          Amount := -Amount;
                          "Amount (LCY)" := -"Amount (LCY)";
                        END;
                        VALIDATE(Amount);
                        VALIDATE("Amount (LCY)");
                      END ELSE
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,CustLedgEntry."Currency Code",AccType::Customer,TRUE);
                        UNTIL CustLedgEntry.NEXT = 0;
                      ConfirmAndCheckApplnCurrency(GenJnlLine,Text001 + Text002,CustLedgEntry."Currency Code",AccType::Customer);
                      "Applies-to Doc. Type" := 0;
                      "Applies-to Doc. No." := '';
                    END ELSE
                      "Applies-to ID" := '';
                    "Due Date" := CustLedgEntry."Due Date";
                    "Direct Debit Mandate ID" := CustLedgEntry."Direct Debit Mandate ID";
                    // Check Payment Tolerance
                    IF  Rec.Amount <> 0 THEN
                      IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                        EXIT;
                  END;
                AccType::Vendor:
                  BEGIN
                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                    VendLedgEntry.SETRANGE(Open,TRUE);
                    "Applies-to ID" := GetAppliesToID("Applies-to ID",Rec);
                    ApplyVendEntries.SetGenJnlLine(GenJnlLine,FIELDNO("Applies-to ID"));
                    ApplyVendEntries.SETRECORD(VendLedgEntry);
                    ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
                    ApplyVendEntries.LOOKUPMODE(TRUE);
                    OK := ApplyVendEntries.RUNMODAL = ACTION::LookupOK;
                    CLEAR(ApplyVendEntries);
                    IF NOT OK THEN
                      EXIT;
                    VendLedgEntry.RESET;
                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive);
                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                    VendLedgEntry.SETRANGE(Open,TRUE);
                    VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                    IF VendLedgEntry.FIND('+') THEN BEGIN
                      CurrencyCode2 := VendLedgEntry."Currency Code";
                      IF Amount = 0 THEN BEGIN
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                          VendLedgEntry.CALCFIELDS("Remaining Amount");
                          VendLedgEntry."Remaining Amount" :=
                            CurrExchRate.ExchangeAmount(
                              VendLedgEntry."Remaining Amount",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Remaining Amount" :=
                            ROUND(VendLedgEntry."Remaining Amount",Currency."Amount Rounding Precision");
                          VendLedgEntry."Remaining Pmt. Disc. Possible" :=
                            CurrExchRate.ExchangeAmount(
                              VendLedgEntry."Remaining Pmt. Disc. Possible",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Remaining Pmt. Disc. Possible" :=
                            ROUND(VendLedgEntry."Remaining Pmt. Disc. Possible",Currency."Amount Rounding Precision");
                          VendLedgEntry."Amount to Apply" :=
                            CurrExchRate.ExchangeAmount(
                              VendLedgEntry."Amount to Apply",
                              VendLedgEntry."Currency Code","Currency Code","Posting Date");
                          VendLedgEntry."Amount to Apply" :=
                            ROUND(VendLedgEntry."Amount to Apply",Currency."Amount Rounding Precision");
                          IF ((VendLedgEntry."Document Type" = VendLedgEntry."Document Type"::"Credit Memo") AND
                              (VendLedgEntry."Remaining Pmt. Disc. Possible" <> 0) OR
                              (VendLedgEntry."Document Type" = VendLedgEntry."Document Type"::Invoice)) AND
                             ("Posting Date" <= VendLedgEntry."Pmt. Discount Date")
                          THEN
                            Amount := Amount - (VendLedgEntry."Amount to Apply" - VendLedgEntry."Remaining Pmt. Disc. Possible")
                          ELSE
                            Amount := Amount - VendLedgEntry."Amount to Apply";
                        UNTIL VendLedgEntry.NEXT(-1) = 0;
                        "Amount (LCY)" := Amount;
                        "Currency Factor" := 1;
                        IF ("Bal. Account Type" = "Bal. Account Type"::Customer) OR
                           ("Bal. Account Type" = "Bal. Account Type"::Vendor)
                        THEN BEGIN
                          Amount := -Amount;
                          "Amount (LCY)" := -"Amount (LCY)";
                        END;
                        VALIDATE(Amount);
                        VALIDATE("Amount (LCY)");
                      END ELSE
                        REPEAT
                          CheckAgainstApplnCurrency(CurrencyCode2,VendLedgEntry."Currency Code",AccType::Vendor,TRUE);
                        UNTIL VendLedgEntry.NEXT(-1) = 0;
                      ConfirmAndCheckApplnCurrency(GenJnlLine,Text001 + Text002,VendLedgEntry."Currency Code",AccType::Vendor);
                      "Applies-to Doc. Type" := 0;
                      "Applies-to Doc. No." := '';
                    END ELSE
                      "Applies-to ID" := '';
                    "Due Date" := VendLedgEntry."Due Date";
                    // Check Payment Tolerance
                    IF  Rec.Amount <> 0 THEN
                      IF NOT PaymentToleranceMgt.PmtTolGenJnl(GenJnlLine) THEN
                        EXIT;
                  END;
                ELSE
                  ERROR(
                    Text005,
                    FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"));
              END;
            END;
            "Applies-to Doc. Type" := GenJnlLine."Applies-to Doc. Type";
            "Applies-to Doc. No." := GenJnlLine."Applies-to Doc. No.";
            "Applies-to ID" := GenJnlLine."Applies-to ID";
            "Due Date" := GenJnlLine."Due Date";
            Amount := GenJnlLine.Amount;
            "Amount (LCY)" := GenJnlLine."Amount (LCY)";
            VALIDATE(Amount);
            VALIDATE("Amount (LCY)");
            IF ("Direct Debit Mandate ID" = '') AND (GenJnlLine."Direct Debit Mandate ID" <> '') THEN
              VALIDATE("Direct Debit Mandate ID",GenJnlLine."Direct Debit Mandate ID");
          END;

  }
  CODE
  {
    VAR
      Text001@1120001 : TextConst 'ENU=The %1 in the %2 will be changed from %3 to %4.\;FRA=Remplacement de %1 %3 par %4 dans %2.\';
      Text002@1120002 : TextConst 'ENU=Do you wish to continue?;FRA=Souhaitez-vous continuer ?';
      Text003@1120003 : TextConst 'ENU=The update has been interrupted to respect the warning.;FRA=La mise … jour a ‚t‚ interrompue pour respecter l''alerte.';
      Text005@1120004 : TextConst 'ENU=The %1 or %2 must be Customer or Vendor.;FRA=%1 ou %2 doit ˆtre un client ou un fournisseur.';
      Text006@1120005 : TextConst 'ENU=All entries in one application must be in the same currency.;FRA=La mˆme devise doit ˆtre utilis‚e pour toutes les ‚critures lettrage.';
      Text007@1120006 : TextConst 'ENU=All entries in one application must be in the same currency or one or more of the EMU currencies.;FRA="Toutes les ‚critures d''une mˆme application doivent ˆtre indiqu‚es dans la mˆme devise ou dans une ou plusieurs devises UME. "';
      GenJnlLine@1120007 : Record 81;
      CustLedgEntry@1120008 : Record 21;
      VendLedgEntry@1120009 : Record 25;
      GLSetup@1120010 : Record 98;
      Currency@1120011 : Record 4;
      CurrExchRate@1120019 : Record 330;
      ApplyCustEntries@1120012 : Page 232;
      ApplyVendEntries@1120013 : Page 233;
      AccNo@1120015 : Code[20];
      CurrencyCode2@1120016 : Code[10];
      OK@1120017 : Boolean;
      AccType@1120018 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';

    PROCEDURE CheckAgainstApplnCurrency@1120001(ApplnCurrencyCode@1120000 : Code[10];CompareCurrencyCode@1120001 : Code[10];AccType@1120002 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';Message@1120003 : Boolean) : Boolean;
    VAR
      Currency@1120004 : Record 4;
      Currency2@1120005 : Record 4;
      SalesSetup@1120006 : Record 311;
      PurchSetup@1120007 : Record 312;
      CurrencyAppln@1120008 : 'No,EMU,All';
    BEGIN
      IF ApplnCurrencyCode = CompareCurrencyCode THEN
        EXIT(TRUE);

      CASE AccType OF
        AccType::Customer:
          BEGIN
            SalesSetup.GET;
            CurrencyAppln := SalesSetup."Appln. between Currencies";
            CASE CurrencyAppln OF
              CurrencyAppln::No:
                BEGIN
                  IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                    IF Message THEN
                      ERROR(Text006);

                  EXIT(FALSE);
                END;
              CurrencyAppln::EMU:
                BEGIN
                  GLSetup.GET;
                  IF NOT Currency.GET(ApplnCurrencyCode) THEN
                    Currency."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency2.GET(CompareCurrencyCode) THEN
                    Currency2."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency."EMU Currency" OR NOT Currency2."EMU Currency" THEN
                    IF Message THEN
                      ERROR(Text007);

                  EXIT(FALSE);
                END;
            END;
          END;
        AccType::Vendor:
          BEGIN
            PurchSetup.GET;
            CurrencyAppln := PurchSetup."Appln. between Currencies";
            CASE CurrencyAppln OF
              CurrencyAppln::No:
                BEGIN
                  IF ApplnCurrencyCode <> CompareCurrencyCode THEN
                    IF Message THEN
                      ERROR(Text006);

                  EXIT(FALSE);
                END;
              CurrencyAppln::EMU:
                BEGIN
                  GLSetup.GET;
                  IF NOT Currency.GET(ApplnCurrencyCode) THEN
                    Currency."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency2.GET(CompareCurrencyCode) THEN
                    Currency2."EMU Currency" := GLSetup."EMU Currency";
                  IF NOT Currency."EMU Currency" OR NOT Currency2."EMU Currency" THEN
                    IF Message THEN
                      ERROR(Text007);

                  EXIT(FALSE);
                END;
            END;
          END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE GetCurrency@1120000();
    BEGIN
      WITH GenJnlLine DO
        IF "Currency Code" = '' THEN
          Currency.InitRoundingPrecision
        ELSE BEGIN
          Currency.GET("Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
        END;
    END;

    PROCEDURE DeleteApply@1120002(Rec@1120000 : Record 10866);
    BEGIN
      CASE Rec."Account Type" OF
        Rec."Account Type"::Customer:
          BEGIN
            CustLedgEntry.INIT;
            CustLedgEntry.SETCURRENTKEY("Applies-to ID");
            CustLedgEntry.SETRANGE("Applies-to ID",Rec."Applies-to ID");
            CustLedgEntry.MODIFYALL("Applies-to ID",'');
          END;
        Rec."Account Type"::Vendor:
          BEGIN
            VendLedgEntry.INIT;
            VendLedgEntry.SETCURRENTKEY("Applies-to ID");
            VendLedgEntry.SETRANGE("Applies-to ID",Rec."Applies-to ID");
            VendLedgEntry.MODIFYALL("Applies-to ID",'');
          END;
      END;
    END;

    LOCAL PROCEDURE ConfirmAndCheckApplnCurrency@1120006(VAR GenJnlLine@1120000 : Record 81;Question@1120002 : Text;CurrencyCode@1120003 : Code[10];AccountType@1120004 : Option);
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF "Currency Code" <> CurrencyCode2 THEN
          IF Amount = 0 THEN BEGIN
            IF NOT CONFIRM(Question,TRUE,FIELDCAPTION("Currency Code"),TABLECAPTION,"Currency Code",CurrencyCode) THEN
              ERROR(Text003);
            "Currency Code" := CurrencyCode
          END ELSE
            CheckAgainstApplnCurrency("Currency Code",CurrencyCode,AccountType,TRUE);
      END;
    END;

    LOCAL PROCEDURE GetAppliesToID@1120004(AppliesToID@1120000 : Code[50];PaymentLine@1120001 : Record 10866) : Code[50];
    BEGIN
      IF AppliesToID = '' THEN
        IF PaymentLine."Document No." <> '' THEN
          AppliesToID := PaymentLine."Document No."
        ELSE
          AppliesToID := PaymentLine."No." + '/' + FORMAT(PaymentLine."Line No.");
      EXIT(AppliesToID);
    END;

    BEGIN
    END.
  }
}


OBJECT Table 99000830 Planning Routing Line
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Next Operation No." = '' THEN
                 SetNextOperations;
             END;

    OnDelete=BEGIN
               ProdOrderCapNeed.SETCURRENTKEY("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
               ProdOrderCapNeed.SETRANGE("Worksheet Template Name","Worksheet Template Name");
               ProdOrderCapNeed.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
               ProdOrderCapNeed.SETRANGE("Worksheet Line No.","Worksheet Line No.");
               ProdOrderCapNeed.SETRANGE("Operation No.","Operation No.");
               ProdOrderCapNeed.DELETEALL;
             END;

    OnRename=BEGIN
               SetRecalcStatus;
             END;

    CaptionML=[ENU=Planning Routing Line;
               FRA=Ligne gamme planning];
    LookupPageID=Page99000863;
    DrillDownPageID=Page99000863;
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Req. Wksh. Template";
                                                   CaptionML=[ENU=Worksheet Template Name;
                                                              FRA=Nom modŠle feuille];
                                                   Editable=No }
    { 2   ;   ;Worksheet Batch Name;Code10        ;TableRelation="Requisition Wksh. Name".Name WHERE (Worksheet Template Name=FIELD(Worksheet Template Name));
                                                   CaptionML=[ENU=Worksheet Batch Name;
                                                              FRA=Feuille] }
    { 3   ;   ;Worksheet Line No.  ;Integer       ;TableRelation="Requisition Line"."Line No." WHERE (Worksheet Template Name=FIELD(Worksheet Template Name),
                                                                                                      Journal Batch Name=FIELD(Worksheet Batch Name));
                                                   CaptionML=[ENU=Worksheet Line No.;
                                                              FRA=Nø ligne planning] }
    { 4   ;   ;Operation No.       ;Code10        ;OnValidate=BEGIN
                                                                SetRecalcStatus;

                                                                GetLine;
                                                                "Starting Time" := ReqLine."Starting Time";
                                                                "Ending Time" := ReqLine."Ending Time";
                                                                "Starting Date" := ReqLine."Starting Date";
                                                                "Ending Date" := ReqLine."Ending Date";
                                                              END;

                                                   CaptionML=[ENU=Operation No.;
                                                              FRA=Nø op‚ration];
                                                   NotBlank=Yes }
    { 5   ;   ;Next Operation No.  ;Code30        ;OnValidate=BEGIN
                                                                SetRecalcStatus;

                                                                GetLine;
                                                                ReqLine.TESTFIELD("Routing Type",ReqLine."Routing Type"::Serial);
                                                              END;

                                                   CaptionML=[ENU=Next Operation No.;
                                                              FRA=Nø op‚ration suivante] }
    { 6   ;   ;Previous Operation No.;Code30      ;OnValidate=BEGIN
                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Previous Operation No.;
                                                              FRA=Nø op‚ration pr‚c‚dente] }
    { 7   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "No." := '';
                                                                "Work Center No." := '';
                                                                "Work Center Group Code" := '';

                                                                ModifyCapNeedEntries;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Work Center,Machine Center;
                                                                    FRA=Centre de charge,Poste de charge];
                                                   OptionString=Work Center,Machine Center }
    { 8   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Work Center)) "Work Center"
                                                                 ELSE IF (Type=CONST(Machine Center)) "Machine Center";
                                                   OnValidate=BEGIN
                                                                SetRecalcStatus;

                                                                IF "No." = '' THEN
                                                                  EXIT;

                                                                CASE Type OF
                                                                  Type::"Work Center":
                                                                    BEGIN
                                                                      WorkCenter.GET("No.");
                                                                      WorkCenter.TESTFIELD(Blocked,FALSE);
                                                                      WorkCenterTransferfields;
                                                                    END;
                                                                  Type::"Machine Center":
                                                                    BEGIN
                                                                      MachineCenter.GET("No.");
                                                                      MachineCenter.TESTFIELD(Blocked,FALSE);
                                                                      MachineCtrTransferfields;
                                                                    END;
                                                                END;
                                                                GetLine;
                                                                IF ReqLine."Routing Type" = ReqLine."Routing Type"::Serial THEN
                                                                  CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              FRA=Nø] }
    { 9   ;   ;Work Center No.     ;Code20        ;TableRelation="Work Center";
                                                   CaptionML=[ENU=Work Center No.;
                                                              FRA=Nø centre de charge];
                                                   Editable=No }
    { 10  ;   ;Work Center Group Code;Code10      ;TableRelation="Work Center Group";
                                                   CaptionML=[ENU=Work Center Group Code;
                                                              FRA=Code groupe centres de charge];
                                                   Editable=No }
    { 11  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 12  ;   ;Setup Time          ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Setup Time;
                                                              FRA=Temps de pr‚paration];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 13  ;   ;Run Time            ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Run Time;
                                                              FRA=Temps d'ex‚cution];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 14  ;   ;Wait Time           ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Wait Time;
                                                              FRA=Temps d'attente];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 15  ;   ;Move Time           ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Move Time;
                                                              FRA=Temps de transfert];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 16  ;   ;Fixed Scrap Quantity;Decimal       ;OnValidate=BEGIN
                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Fixed Scrap Quantity;
                                                              FRA=Quantit‚ perte fixe];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 17  ;   ;Lot Size            ;Decimal       ;CaptionML=[ENU=Lot Size;
                                                              FRA=Taille lot];
                                                   DecimalPlaces=0:5 }
    { 18  ;   ;Scrap Factor %      ;Decimal       ;OnValidate=BEGIN
                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Scrap Factor %;
                                                              FRA=% perte];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 19  ;   ;Setup Time Unit of Meas. Code;Code10;
                                                   TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Setup Time Unit of Meas. Code;
                                                              FRA=Unit‚ temps de pr‚paration] }
    { 20  ;   ;Run Time Unit of Meas. Code;Code10 ;TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Run Time Unit of Meas. Code;
                                                              FRA=Unit‚ temps d'ex‚cution] }
    { 21  ;   ;Wait Time Unit of Meas. Code;Code10;TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Wait Time Unit of Meas. Code;
                                                              FRA=Unit‚ temps d'attente] }
    { 22  ;   ;Move Time Unit of Meas. Code;Code10;TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Move Time Unit of Meas. Code;
                                                              FRA=Unit‚ temps de transfert] }
    { 27  ;   ;Minimum Process Time;Decimal       ;CaptionML=[ENU=Minimum Process Time;
                                                              FRA=Temps op‚ratoire min.];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 28  ;   ;Maximum Process Time;Decimal       ;CaptionML=[ENU=Maximum Process Time;
                                                              FRA=Temps op‚ratoire max.];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 30  ;   ;Concurrent Capacities;Decimal      ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Concurrent Capacities;
                                                              FRA=Capacit‚s simultan‚es];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 31  ;   ;Send-Ahead Quantity ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates;
                                                              END;

                                                   CaptionML=[ENU=Send-Ahead Quantity;
                                                              FRA=Quantit‚ de transfert];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 34  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   CaptionML=[ENU=Routing Link Code;
                                                              FRA=Code lien gamme] }
    { 35  ;   ;Standard Task Code  ;Code10        ;TableRelation="Standard Task";
                                                   OnValidate=VAR
                                                                StandardTask@1000 : Record 99000778;
                                                              BEGIN
                                                                IF "Standard Task Code" = '' THEN
                                                                  EXIT;

                                                                StandardTask.GET("Standard Task Code");
                                                                Description := StandardTask.Description;
                                                              END;

                                                   CaptionML=[ENU=Standard Task Code;
                                                              FRA=Code tƒche standard] }
    { 40  ;   ;Unit Cost per       ;Decimal       ;CaptionML=[ENU=Unit Cost per;
                                                              FRA=Co–t unitaire par];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 41  ;   ;Recalculate         ;Boolean       ;CaptionML=[ENU=Recalculate;
                                                              FRA=Recalculer];
                                                   Editable=No }
    { 50  ;   ;Sequence No.(Forward);Integer      ;CaptionML=[ENU=Sequence No.(Forward);
                                                              FRA=Nø s‚quence (aval)];
                                                   Editable=No }
    { 51  ;   ;Sequence No.(Backward);Integer     ;CaptionML=[ENU=Sequence No.(Backward);
                                                              FRA=Nø s‚quence (amont)];
                                                   Editable=No }
    { 52  ;   ;Fixed Scrap Qty. (Accum.);Decimal  ;CaptionML=[ENU=Fixed Scrap Qty. (Accum.);
                                                              FRA=Quantit‚ perte fixe (cumul‚e)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Scrap Factor % (Accumulated);Decimal;
                                                   CaptionML=[ENU=Scrap Factor % (Accumulated);
                                                              FRA=% perte (cumul‚e)];
                                                   DecimalPlaces=1:1;
                                                   Editable=No }
    { 55  ;   ;Sequence No. (Actual);Integer      ;CaptionML=[ENU=Sequence No. (Actual);
                                                              FRA=Nø s‚quence (r‚el)];
                                                   Editable=No }
    { 56  ;   ;Direct Unit Cost    ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Direct Unit Cost;
                                                              FRA=Co–t unitaire direct];
                                                   DecimalPlaces=2:5 }
    { 57  ;   ;Indirect Cost %     ;Decimal       ;OnValidate=BEGIN
                                                                GetGLSetup;
                                                                "Unit Cost per" :=
                                                                  ROUND("Direct Unit Cost" * (1 + "Indirect Cost %" / 100) + "Overhead Rate",GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Indirect Cost %;
                                                              FRA=% co–t indirect];
                                                   DecimalPlaces=0:5 }
    { 58  ;   ;Overhead Rate       ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Overhead Rate;
                                                              FRA=Frais g‚n‚raux];
                                                   DecimalPlaces=0:5 }
    { 61  ;   ;Output Quantity     ;Decimal       ;CaptionML=[ENU=Output Quantity;
                                                              FRA=Quantit‚ produite];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 70  ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                MODIFY;

                                                                PlanningRtngLine.GET(
                                                                  "Worksheet Template Name",
                                                                  "Worksheet Batch Name","Worksheet Line No.","Operation No.");

                                                                PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,0,ReqLine);
                                                                PlanningRtngLine.SETCURRENTKEY(
                                                                  "Worksheet Template Name",
                                                                  "Worksheet Batch Name",
                                                                  "Worksheet Line No.","Sequence No. (Actual)");

                                                                PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,0,FALSE);

                                                                CalculateRoutingBack;
                                                                CalculateRoutingForward;

                                                                GET(
                                                                  "Worksheet Template Name",
                                                                  "Worksheet Batch Name","Worksheet Line No.","Operation No.");

                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Starting Time;
                                                              FRA=Heure d‚but] }
    { 71  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              FRA=Date d‚but] }
    { 72  ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                MODIFY;

                                                                PlanningRtngLine.GET(
                                                                  "Worksheet Template Name",
                                                                  "Worksheet Batch Name","Worksheet Line No.","Operation No.");

                                                                PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,1,ReqLine);
                                                                PlanningRtngLine.SETCURRENTKEY(
                                                                  "Worksheet Template Name",
                                                                  "Worksheet Batch Name",
                                                                  "Worksheet Line No.","Sequence No. (Actual)");
                                                                PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,1,FALSE);

                                                                CalculateRoutingBack;
                                                                CalculateRoutingForward;

                                                                GET(
                                                                  "Worksheet Template Name",
                                                                  "Worksheet Batch Name","Worksheet Line No.","Operation No.");
                                                              END;

                                                   CaptionML=[ENU=Ending Time;
                                                              FRA=Heure fin] }
    { 73  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              FRA=Date fin] }
    { 76  ;   ;Unit Cost Calculation;Option       ;CaptionML=[ENU=Unit Cost Calculation;
                                                              FRA=Unit‚ de co–t];
                                                   OptionCaptionML=[ENU=Time,Units;
                                                                    FRA=Temps,Quantit‚];
                                                   OptionString=Time,Units }
    { 77  ;   ;Input Quantity      ;Decimal       ;CaptionML=[ENU=Input Quantity;
                                                              FRA=Quantit‚ d'entr‚e];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 78  ;   ;Critical Path       ;Boolean       ;CaptionML=[ENU=Critical Path;
                                                              FRA=Chemin critique];
                                                   Editable=No }
    { 98  ;   ;Starting Date-Time  ;DateTime      ;OnValidate=BEGIN
                                                                "Starting Date" := DT2DATE("Starting Date-Time");
                                                                "Starting Time" := DT2TIME("Starting Date-Time");
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date-Time;
                                                              FRA=Date/Heure d‚but] }
    { 99  ;   ;Ending Date-Time    ;DateTime      ;OnValidate=BEGIN
                                                                "Ending Date" := DT2DATE("Ending Date-Time");
                                                                "Ending Time" := DT2TIME("Ending Date-Time");
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date-Time;
                                                              FRA=Date/Heure fin] }
    { 99000909;;Expected Operation Cost Amt.;Decimal;
                                                   CaptionML=[ENU=Expected Operation Cost Amt.;
                                                              FRA=Co–t op‚ratoire total pr‚vu];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 99000910;;Expected Capacity Ovhd. Cost;Decimal;
                                                   CaptionML=[ENU=Expected Capacity Ovhd. Cost;
                                                              FRA=Frais g‚n. op‚ratoires pr‚vus];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 99000911;;Expected Capacity Need;Decimal    ;CaptionML=[ENU=Expected Capacity Need;
                                                              FRA=Charge pr‚vue];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Worksheet Template Name,Worksheet Batch Name,Worksheet Line No.,Operation No.;
                                                   SumIndexFields=Expected Operation Cost Amt.;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Worksheet Template Name,Worksheet Batch Name,Worksheet Line No.,Sequence No.(Forward);
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Worksheet Batch Name,Worksheet Line No.,Sequence No.(Backward);
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Worksheet Batch Name,Worksheet Line No.,Sequence No. (Actual);
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Worksheet Batch Name,Type,No.,Starting Date;
                                                   MaintainSQLIndex=No }
    {    ;Work Center No.                          }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This routing line cannot be moved because of critical work centers in previous operations;FRA=Cette ligne gamme ne peut pas ˆtre d‚plac‚e car il existe des centres de charge critiques dans les op‚rations pr‚c‚dentes.';
      Text001@1001 : TextConst 'ENU=This routing line cannot be moved because of critical work centers in next operations;FRA=Cette ligne gamme ne peut pas ˆtre d‚plac‚e car il existe des centres de charge critiques dans les op‚rations suivantes';
      WorkCenter@1004 : Record 99000754;
      MachineCenter@1005 : Record 99000758;
      ReqLine@1006 : Record 246;
      PlanningRtngLine@1007 : Record 99000830;
      ProdOrderCapNeed@1008 : Record 5410;
      GLSetup@1002 : Record 98;
      PlngLnMgt@1009 : Codeunit 99000809;
      PlanningRoutingMgt@1010 : Codeunit 99000808;
      HasGLSetup@1020 : Boolean;

    PROCEDURE Caption@5() : Text[100];
    VAR
      ReqWkshName@1000 : Record 245;
      ReqLine@1001 : Record 246;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF NOT ReqWkshName.GET("Worksheet Template Name","Worksheet Batch Name") THEN
        EXIT('');

      IF NOT ReqLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.") THEN
        CLEAR(ReqLine);

      EXIT(
        STRSUBSTNO('%1 %2 %3 %4 %5',
          "Worksheet Batch Name",ReqWkshName.Description,ReqLine.Type,ReqLine."No.",ReqLine.Description));
    END;

    LOCAL PROCEDURE GetLine@14();
    BEGIN
      IF (ReqLine."Worksheet Template Name" <> "Worksheet Template Name") OR
         (ReqLine."Journal Batch Name" <> "Worksheet Batch Name") OR
         (ReqLine."Line No." <> "Worksheet Line No.")
      THEN
        ReqLine.GET("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
    END;

    LOCAL PROCEDURE WorkCenterTransferfields@2();
    BEGIN
      "Work Center No." := WorkCenter."No.";
      "Work Center Group Code" := WorkCenter."Work Center Group Code";
      "Setup Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      "Run Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      "Wait Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      "Move Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      Description := WorkCenter.Name;
      "Unit Cost per" := WorkCenter."Unit Cost";
      "Direct Unit Cost" := WorkCenter."Direct Unit Cost";
      "Indirect Cost %" := WorkCenter."Indirect Cost %";
      "Overhead Rate" := WorkCenter."Overhead Rate";
    END;

    LOCAL PROCEDURE MachineCtrTransferfields@1();
    BEGIN
      WorkCenter.GET(MachineCenter."Work Center No.");
      WorkCenterTransferfields;

      Description := MachineCenter.Name;
      "Setup Time" := MachineCenter."Setup Time";
      "Wait Time" := MachineCenter."Wait Time";
      "Move Time" := MachineCenter."Move Time";
      "Fixed Scrap Quantity" := MachineCenter."Fixed Scrap Quantity";
      "Scrap Factor %" := MachineCenter."Scrap %";
      "Minimum Process Time" := MachineCenter."Minimum Process Time";
      "Maximum Process Time" := MachineCenter."Maximum Process Time";
      "Concurrent Capacities" := MachineCenter."Concurrent Capacities";
      "Send-Ahead Quantity" := MachineCenter."Send-Ahead Quantity";
      "Setup Time Unit of Meas. Code" := MachineCenter."Setup Time Unit of Meas. Code";
      "Wait Time Unit of Meas. Code" := MachineCenter."Wait Time Unit of Meas. Code";
      "Move Time Unit of Meas. Code" := MachineCenter."Move Time Unit of Meas. Code";
      "Unit Cost per" := MachineCenter."Unit Cost";
      "Direct Unit Cost" := MachineCenter."Direct Unit Cost";
      "Indirect Cost %" := MachineCenter."Indirect Cost %";
      "Overhead Rate" := MachineCenter."Overhead Rate";
    END;

    PROCEDURE SetRecalcStatus@4();
    BEGIN
      Recalculate := TRUE;
    END;

    PROCEDURE RunTimePer@7() : Decimal;
    BEGIN
      IF "Lot Size" = 0 THEN
        "Lot Size" := 1;

      EXIT(ROUND("Run Time" / "Lot Size",0.00001));
    END;

    LOCAL PROCEDURE CalcStartingEndingDates@17();
    BEGIN
      MODIFY;

      PlanningRtngLine.GET(
        "Worksheet Template Name",
        "Worksheet Batch Name","Worksheet Line No.","Operation No.");

      PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,0,ReqLine);
      PlanningRtngLine.SETCURRENTKEY(
        "Worksheet Template Name",
        "Worksheet Batch Name",
        "Worksheet Line No.","Sequence No. (Actual)");

      PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,0,FALSE);

      CalculateRoutingBack;
      CalculateRoutingForward;

      GET(
        "Worksheet Template Name",
        "Worksheet Batch Name","Worksheet Line No.","Operation No.");
    END;

    LOCAL PROCEDURE CalculateRoutingBack@10();
    BEGIN
      IF "Previous Operation No." = '' THEN
        EXIT;

      GetLine;

      PlanningRtngLine.RESET;
      PlanningRtngLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.","Worksheet Line No.");
      PlanningRtngLine.SETFILTER("Operation No.","Previous Operation No.");

      IF PlanningRtngLine.FIND('-') THEN
        REPEAT
          PlanningRtngLine.SETCURRENTKEY(
            "Worksheet Template Name",
            "Worksheet Batch Name",
            "Worksheet Line No.","Sequence No. (Actual)");
          WorkCenter.GET(PlanningRtngLine."Work Center No.");

          CASE WorkCenter."Simulation Type" OF
            WorkCenter."Simulation Type"::Moves:
              BEGIN
                PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,1,ReqLine);
                PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,1,TRUE);
              END;
            WorkCenter."Simulation Type"::"Moves When Necessary":
              BEGIN
                IF (PlanningRtngLine."Ending Date" > "Starting Date") OR
                   ((PlanningRtngLine."Ending Date" = "Starting Date") AND
                    (PlanningRtngLine."Ending Time" > "Starting Time"))
                THEN BEGIN
                  PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,1,ReqLine);
                  PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,1,TRUE);
                END;
              END;
            WorkCenter."Simulation Type"::Critical:
              BEGIN
                IF (PlanningRtngLine."Ending Date" > "Starting Date") OR
                   ((PlanningRtngLine."Ending Date" = "Starting Date") AND
                    (PlanningRtngLine."Ending Time" > "Starting Time"))
                THEN
                  ERROR(Text000);
              END;
          END;
          PlanningRtngLine.SETCURRENTKEY(
            "Worksheet Template Name",
            "Worksheet Batch Name","Worksheet Line No.","Operation No.");
        UNTIL PlanningRtngLine.NEXT = 0;

      PlngLnMgt.CalculatePlanningLineDates(ReqLine);
      AdjustComponents(ReqLine);
    END;

    LOCAL PROCEDURE CalculateRoutingForward@9();
    BEGIN
      IF "Next Operation No." = '' THEN
        EXIT;

      GetLine;

      PlanningRtngLine.RESET;
      PlanningRtngLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.","Worksheet Line No.");
      PlanningRtngLine.SETFILTER("Operation No.","Next Operation No.");

      IF PlanningRtngLine.FIND('-') THEN
        REPEAT
          PlanningRtngLine.SETCURRENTKEY(
            "Worksheet Template Name",
            "Worksheet Batch Name",
            "Worksheet Line No.","Sequence No. (Actual)");
          WorkCenter.GET(PlanningRtngLine."Work Center No.");
          CASE WorkCenter."Simulation Type" OF
            WorkCenter."Simulation Type"::Moves:
              BEGIN
                PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,0,ReqLine);
                PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,0,TRUE);
              END;
            WorkCenter."Simulation Type"::"Moves When Necessary":
              BEGIN
                IF (PlanningRtngLine."Starting Date" < "Ending Date") OR
                   ((PlanningRtngLine."Starting Date" = "Ending Date") AND
                    (PlanningRtngLine."Starting Time" < "Ending Time"))
                THEN BEGIN
                  PlanningRoutingMgt."Calc. Sequence From Actual"(PlanningRtngLine,0,ReqLine);
                  PlngLnMgt.CalculateRoutingFromActual(PlanningRtngLine,0,TRUE);
                END;
              END;
            WorkCenter."Simulation Type"::Critical:
              BEGIN
                IF (PlanningRtngLine."Starting Date" < "Ending Date") OR
                   ((PlanningRtngLine."Starting Date" = "Ending Date") AND
                    (PlanningRtngLine."Starting Time" < "Ending Time"))
                THEN
                  ERROR(Text001);
              END;
          END;
          PlanningRtngLine.SETCURRENTKEY(
            "Worksheet Template Name",
            "Worksheet Batch Name","Worksheet Line No.","Operation No.");
        UNTIL PlanningRtngLine.NEXT = 0;

      PlngLnMgt.CalculatePlanningLineDates(ReqLine);
      AdjustComponents(ReqLine);
    END;

    LOCAL PROCEDURE ModifyCapNeedEntries@12();
    BEGIN
      ProdOrderCapNeed.SETCURRENTKEY("Worksheet Template Name","Worksheet Batch Name","Worksheet Line No.");
      ProdOrderCapNeed.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      ProdOrderCapNeed.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      ProdOrderCapNeed.SETRANGE("Worksheet Line No.","Worksheet Line No.");
      ProdOrderCapNeed.SETRANGE("Operation No.","Operation No.");
      IF ProdOrderCapNeed.FIND('-') THEN
        REPEAT
          ProdOrderCapNeed."No." := "No.";
          ProdOrderCapNeed."Work Center No." := "Work Center No.";
          ProdOrderCapNeed."Work Center Group Code" := "Work Center Group Code";
          ProdOrderCapNeed.MODIFY;
        UNTIL ProdOrderCapNeed.NEXT = 0;
    END;

    LOCAL PROCEDURE AdjustComponents@13(VAR ReqLine@1000 : Record 246);
    VAR
      PlanningComponent@1001 : Record 99000829;
    BEGIN
      PlanningComponent.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningComponent.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningComponent.SETRANGE("Worksheet Line No.",ReqLine."Line No.");

      IF PlanningComponent.FIND('-') THEN
        REPEAT
          PlanningComponent.VALIDATE("Routing Link Code");
          PlanningComponent.MODIFY;
        UNTIL PlanningComponent.NEXT = 0;
    END;

    PROCEDURE TransferFromProdOrderRouting@3(VAR ProdOrderRoutingLine@1000 : Record 5409);
    BEGIN
      ProdOrderRoutingLine.TESTFIELD(Recalculate,FALSE);
      "Operation No." := ProdOrderRoutingLine."Operation No.";
      "Next Operation No." := ProdOrderRoutingLine."Next Operation No.";
      "Previous Operation No." := ProdOrderRoutingLine."Previous Operation No.";
      Type := ProdOrderRoutingLine.Type;
      "No." := ProdOrderRoutingLine."No.";
      Description := ProdOrderRoutingLine.Description;
      "Work Center No." := ProdOrderRoutingLine."Work Center No.";
      "Work Center Group Code" := ProdOrderRoutingLine."Work Center Group Code";
      "Setup Time" := ProdOrderRoutingLine."Setup Time";
      "Run Time" := ProdOrderRoutingLine."Run Time";
      "Wait Time" := ProdOrderRoutingLine."Wait Time";
      "Move Time" := ProdOrderRoutingLine."Move Time";
      "Fixed Scrap Quantity" := ProdOrderRoutingLine."Fixed Scrap Quantity";
      "Lot Size" := ProdOrderRoutingLine."Lot Size";
      "Scrap Factor %" := ProdOrderRoutingLine."Scrap Factor %";
      "Setup Time Unit of Meas. Code" := ProdOrderRoutingLine."Setup Time Unit of Meas. Code";
      "Run Time Unit of Meas. Code" := ProdOrderRoutingLine."Run Time Unit of Meas. Code";
      "Wait Time Unit of Meas. Code" := ProdOrderRoutingLine."Wait Time Unit of Meas. Code";
      "Move Time Unit of Meas. Code" := ProdOrderRoutingLine."Move Time Unit of Meas. Code";
      "Minimum Process Time" := ProdOrderRoutingLine."Minimum Process Time";
      "Maximum Process Time" := ProdOrderRoutingLine."Maximum Process Time";
      "Concurrent Capacities" := ProdOrderRoutingLine."Concurrent Capacities";
      "Send-Ahead Quantity" := ProdOrderRoutingLine."Send-Ahead Quantity";
      "Direct Unit Cost" := ProdOrderRoutingLine."Direct Unit Cost";
      "Unit Cost per" := ProdOrderRoutingLine."Unit Cost per";
      "Unit Cost Calculation" := ProdOrderRoutingLine."Unit Cost Calculation";
      "Indirect Cost %" := ProdOrderRoutingLine."Indirect Cost %";
      "Overhead Rate" := ProdOrderRoutingLine."Overhead Rate";
      VALIDATE("Routing Link Code",ProdOrderRoutingLine."Routing Link Code");
      "Standard Task Code" := ProdOrderRoutingLine."Standard Task Code";
      "Sequence No.(Forward)" := ProdOrderRoutingLine."Sequence No. (Forward)";
      "Sequence No.(Backward)" := ProdOrderRoutingLine."Sequence No. (Backward)";
      "Fixed Scrap Qty. (Accum.)" := ProdOrderRoutingLine."Fixed Scrap Qty. (Accum.)";
      "Scrap Factor % (Accumulated)" := ProdOrderRoutingLine."Scrap Factor % (Accumulated)";
      "Starting Time" := ProdOrderRoutingLine."Starting Time";
      "Starting Date" := ProdOrderRoutingLine."Starting Date";
      "Ending Time" := ProdOrderRoutingLine."Ending Time";
      "Ending Date" := ProdOrderRoutingLine."Ending Date";
      UpdateDatetime;
      VALIDATE("Unit Cost per");
    END;

    PROCEDURE UpdateDatetime@11();
    BEGIN
      "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
      "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
    END;

    PROCEDURE SetNextOperations@6();
    VAR
      PlanningRtngLine2@1003 : Record 99000830;
    BEGIN
      PlanningRtngLine2.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningRtngLine2.SETRANGE("Worksheet Batch Name","Worksheet Batch Name");
      PlanningRtngLine2.SETRANGE("Worksheet Line No.","Worksheet Line No.");
      PlanningRtngLine2.SETFILTER("Operation No.",'>%1',"Operation No.");

      IF PlanningRtngLine2.FINDFIRST THEN
        "Next Operation No." := PlanningRtngLine2."Operation No."
      ELSE BEGIN
        PlanningRtngLine2.SETFILTER("Operation No.",'');
        PlanningRtngLine2.SETRANGE("Next Operation No.",'');
        IF PlanningRtngLine2.FINDFIRST THEN BEGIN
          PlanningRtngLine2."Next Operation No." := "Operation No.";
          PlanningRtngLine2.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE GetGLSetup@1022();
    BEGIN
      IF NOT HasGLSetup THEN BEGIN
        HasGLSetup := TRUE;
        GLSetup.GET;
      END;
    END;

    BEGIN
    END.
  }
}


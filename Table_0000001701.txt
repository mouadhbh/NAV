OBJECT Table 1701 Deferral Header
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Schedule Description;
    OnDelete=VAR
               DeferralLine@1000 : Record 1702;
             BEGIN
               // If the user deletes the header, all associated lines should also be deleted
               DeferralLine.SETRANGE("Deferral Doc. Type","Deferral Doc. Type");
               DeferralLine.SETRANGE("Gen. Jnl. Template Name","Gen. Jnl. Template Name");
               DeferralLine.SETRANGE("Gen. Jnl. Batch Name","Gen. Jnl. Batch Name");
               DeferralLine.SETRANGE("Document Type","Document Type");
               DeferralLine.SETRANGE("Document No.","Document No.");
               DeferralLine.SETRANGE("Line No.","Line No.");
               DeferralLine.DELETEALL;
             END;

    CaptionML=[ENU=Deferral Header;
               FRA=En-tˆte ‚chelonnement];
  }
  FIELDS
  {
    { 1   ;   ;Deferral Doc. Type  ;Option        ;CaptionML=[ENU=Deferral Doc. Type;
                                                              FRA=Type doc. ‚chelonnement];
                                                   OptionCaptionML=[ENU=Purchase,Sales,G/L;
                                                                    FRA=Achats,Ventes,Comptabilit‚];
                                                   OptionString=Purchase,Sales,G/L }
    { 2   ;   ;Gen. Jnl. Template Name;Code10     ;CaptionML=[ENU=Gen. Jnl. Template Name;
                                                              FRA=Nom modŠle feuille cpta. immo.] }
    { 3   ;   ;Gen. Jnl. Batch Name;Code10        ;CaptionML=[ENU=Gen. Jnl. Batch Name;
                                                              FRA=Nom feuille cpta. immo.] }
    { 4   ;   ;Document Type       ;Integer       ;CaptionML=[ENU=Document Type;
                                                              FRA=Type document] }
    { 5   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              FRA=Nø document] }
    { 6   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne] }
    { 7   ;   ;Deferral Code       ;Code10        ;CaptionML=[ENU=Deferral Code;
                                                              FRA=Code ‚chelonnement];
                                                   NotBlank=Yes }
    { 8   ;   ;Amount to Defer     ;Decimal       ;OnValidate=BEGIN
                                                                IF "Initial Amount to Defer" < 0 THEN BEGIN// Negative amount
                                                                  IF "Amount to Defer" < "Initial Amount to Defer" THEN
                                                                    ERROR(AmountToDeferErr);
                                                                  IF "Amount to Defer" > 0 THEN
                                                                    ERROR(AmountToDeferErr)
                                                                END;

                                                                IF "Initial Amount to Defer" >= 0 THEN BEGIN// Positive amount
                                                                  IF "Amount to Defer" > "Initial Amount to Defer" THEN
                                                                    ERROR(AmountToDeferErr);
                                                                  IF "Amount to Defer" < 0 THEN
                                                                    ERROR(AmountToDeferErr);
                                                                END;

                                                                IF "Amount to Defer" = 0 THEN
                                                                  ERROR(ZeroAmountToDeferErr);
                                                              END;

                                                   CaptionML=[ENU=Amount to Defer;
                                                              FRA=Montant … ‚chelonner];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 9   ;   ;Amount to Defer (LCY);Decimal      ;CaptionML=[ENU=Amount to Defer (LCY);
                                                              FRA=Montant … ‚chelonner DS];
                                                   AutoFormatType=1 }
    { 10  ;   ;Calc. Method        ;Option        ;CaptionML=[ENU=Calc. Method;
                                                              FRA=M‚thode de calcul];
                                                   OptionCaptionML=[ENU=Straight-Line,Equal per Period,Days per Period,User-Defined;
                                                                    FRA=Lin‚aire,gal par p‚riode,Jours par p‚riode,D‚finie par l'utilisateur];
                                                   OptionString=Straight-Line,Equal per Period,Days per Period,User-Defined }
    { 11  ;   ;Start Date          ;Date          ;OnValidate=BEGIN
                                                                IF GenJnlCheckLine.DateNotAllowed("Start Date") THEN
                                                                  ERROR(InvalidPostingDateErr,"Start Date");

                                                                AccountingPeriod.SETFILTER("Starting Date",'>=%1',"Start Date");
                                                                IF NOT AccountingPeriod.FINDFIRST THEN
                                                                  ERROR(DeferSchedOutOfBoundsErr);
                                                              END;

                                                   CaptionML=[ENU=Start Date;
                                                              FRA=Date d‚but] }
    { 12  ;   ;No. of Periods      ;Integer       ;OnValidate=BEGIN
                                                                IF "No. of Periods" < 1 THEN
                                                                  ERROR(NumberofPeriodsErr);
                                                              END;

                                                   CaptionML=[ENU=No. of Periods;
                                                              FRA=Nombre de p‚riodes];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 13  ;   ;Schedule Description;Text50        ;CaptionML=[ENU=Schedule Description;
                                                              FRA=Description tableau] }
    { 14  ;   ;Initial Amount to Defer;Decimal    ;CaptionML=[ENU=Initial Amount to Defer;
                                                              FRA=Montant initial … ‚chelonner] }
    { 15  ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              FRA=Code devise] }
    { 20  ;   ;Schedule Line Total ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Deferral Line".Amount WHERE (Deferral Doc. Type=FIELD(Deferral Doc. Type),
                                                                                                 Gen. Jnl. Template Name=FIELD(Gen. Jnl. Template Name),
                                                                                                 Gen. Jnl. Batch Name=FIELD(Gen. Jnl. Batch Name),
                                                                                                 Document Type=FIELD(Document Type),
                                                                                                 Document No.=FIELD(Document No.),
                                                                                                 Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Schedule Line Total;
                                                              FRA=Total ligne Planifi‚] }
  }
  KEYS
  {
    {    ;Deferral Doc. Type,Gen. Jnl. Template Name,Gen. Jnl. Batch Name,Document Type,Document No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AmountToDeferErr@1000 : TextConst 'ENU=The deferred amount cannot be greater than the document line amount.;FRA=Le montant ‚chelonn‚ ne peut pas ˆtre sup‚rieur au montant de la ligne document.';
      AccountingPeriod@1002 : Record 50;
      GenJnlCheckLine@1001 : Codeunit 11;
      InvalidPostingDateErr@1004 : TextConst '@@@="%1=The date passed in for the posting date.";ENU=%1 is not within the range of posting dates for your company.;FRA=%1 n''est pas dans la plage de dates de comptabilisation de votre soci‚t‚.';
      DeferSchedOutOfBoundsErr@1003 : TextConst 'ENU=The deferral schedule falls outside the accounting periods that have been set up for the company.;FRA=Le tableau ‚chelonnement tombe en dehors des p‚riodes comptables qui ont ‚t‚ param‚tr‚es pour la soci‚t‚.';
      SelectionMsg@1005 : TextConst 'ENU=You must specify a deferral code for this line before you can view the deferral schedule.;FRA=Vous devez sp‚cifier un code ‚chelonnement pour cette ligne avant de pouvoir afficher le tableau ‚chelonnement.';
      DeferralUtilities@1006 : Codeunit 1720;
      NumberofPeriodsErr@1007 : TextConst 'ENU=The number of periods must be greater than 0.;FRA=Le nombre de p‚riodes doit ˆtre sup‚rieur … 0.';
      ZeroAmountToDeferErr@1008 : TextConst 'ENU=The Amount to Defer cannot be 0.;FRA=Le montant … ‚chelonner ne peut pas ˆtre ‚gal … 0.';

    PROCEDURE CalculateSchedule@1() : Boolean;
    VAR
      DeferralDescription@1000 : Text[50];
    BEGIN
      IF "Deferral Code" = '' THEN BEGIN
        MESSAGE(SelectionMsg);
        EXIT(FALSE);
      END;
      DeferralDescription := "Schedule Description";
      DeferralUtilities.CreateDeferralSchedule("Deferral Code","Deferral Doc. Type","Gen. Jnl. Template Name",
        "Gen. Jnl. Batch Name","Document Type","Document No.","Line No.","Amount to Defer",
        "Calc. Method","Start Date","No. of Periods",FALSE,DeferralDescription,FALSE,"Currency Code");
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}


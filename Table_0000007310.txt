OBJECT Table 7310 Warehouse Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               WhseJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               WhseJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               WhseJnlLine.SETRANGE("Journal Batch Name",Name);
               WhseJnlLine.SETRANGE("Location Code","Location Code");
               WhseJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               WhseJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               WhseJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WhseJnlLine.SETRANGE("Location Code",xRec."Location Code");
               WHILE WhseJnlLine.FINDFIRST DO
                 WhseJnlLine.RENAME("Journal Template Name",Name,"Location Code",WhseJnlLine."Line No.");
             END;

    CaptionML=[ENU=Warehouse Journal Batch;
               FRA=Feuille entrep“t];
    LookupPageID=Page7323;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Warehouse Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              FRA=Nom modŠle feuille];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              FRA=Nom];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  WhseJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  WhseJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  WhseJnlLine.SETRANGE("Location Code","Location Code");
                                                                  WhseJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              FRA=Code motif] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN
                                                                  IF "No. Series" = "Registering No. Series" THEN
                                                                    VALIDATE("Registering No. Series",'');
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 6   ;   ;Registering No. Series;Code10      ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Registering No. Series" = "No. Series") AND ("Registering No. Series" <> '') THEN
                                                                  FIELDERROR("Registering No. Series",STRSUBSTNO(Text000,"Registering No. Series"));
                                                                WhseJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                WhseJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                WhseJnlLine.SETRANGE("Location Code","Location Code");
                                                                WhseJnlLine.MODIFYALL("Registering No. Series","Registering No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Registering No. Series;
                                                              FRA=Enregistrement des nø de s‚rie] }
    { 7   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   OnValidate=VAR
                                                                Location@1000 : Record 14;
                                                              BEGIN
                                                                Location.GET("Location Code");
                                                                Location.TESTFIELD("Directed Put-away and Pick",TRUE);
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              FRA=Code magasin];
                                                   NotBlank=Yes }
    { 21  ;   ;Template Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Warehouse Journal Template".Type WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Template Type;
                                                              FRA=Type modŠle];
                                                   OptionCaptionML=[ENU=Item,Physical Inventory,Reclassification;
                                                                    FRA=Article,Inventaire,Reclassement];
                                                   OptionString=Item,Physical Inventory,Reclassification;
                                                   Editable=No }
    { 7700;   ;Assigned User ID    ;Code50        ;TableRelation="Warehouse Employee" WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=[ENU=Assigned User ID;
                                                              FRA=Code utilisateur affect‚] }
  }
  KEYS
  {
    {    ;Journal Template Name,Name,Location Code;Clustered=Yes }
    {    ;Location Code,Assigned User ID           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=must not be %1;FRA=ne doit pas ˆtre %1';
      WhseJnlTemplate@1002 : Record 7309;
      WhseJnlLine@1003 : Record 7311;
      Text001@1000 : TextConst 'ENU=The locations for warehouse employee %1 are not enabled for WMS.;FRA=Les magasins du magasinier %1 ne sont pas activ‚s dans le module WMS.';

    PROCEDURE SetupNewBatch@3();
    VAR
      Location@1001 : Record 14;
      WhseEmployee@1002 : Record 7301;
      WMSMgt@1000 : Codeunit 7302;
      FoundLocation@1003 : Boolean;
    BEGIN
      WhseJnlTemplate.GET("Journal Template Name");
      "No. Series" := WhseJnlTemplate."No. Series";
      "Registering No. Series" := WhseJnlTemplate."Registering No. Series";
      "Reason Code" := WhseJnlTemplate."Reason Code";
      IF USERID <> '' THEN BEGIN
        Location.GET(WMSMgt.GetDefaultLocation);
        IF NOT Location."Directed Put-away and Pick" THEN BEGIN
          WhseEmployee.SETRANGE("User ID",USERID);
          WhseEmployee.FIND('-');
          REPEAT
            Location.GET(WhseEmployee."Location Code");
            IF Location."Directed Put-away and Pick" THEN
              FoundLocation := TRUE;
          UNTIL (WhseEmployee.NEXT = 0) OR FoundLocation;
        END;
        IF NOT Location."Directed Put-away and Pick" THEN
          ERROR(Text001,USERID);
      END;
      "Location Code" := Location.Code;
    END;

    BEGIN
    END.
  }
}


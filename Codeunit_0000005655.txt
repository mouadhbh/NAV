OBJECT Codeunit 5655 Insurance Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=5634;
    OnRun=BEGIN
            InsuranceJnlBatch.COPY(Rec);
            Code;
            COPY(InsuranceJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?;FRA=Souhaitez-vous valider les feuilles ?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;FRA=Les feuilles ont t valides avec succs.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";FRA="Il n''a pas t possible de valider l''ensemble des feuilles. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;FRA=Les feuilles qui n''ont pas t valides avec succs sont maintenant marques.';
      InsuranceJnlTempl@1004 : Record 5633;
      InsuranceJnlBatch@1005 : Record 5634;
      InsuranceJnlLine@1006 : Record 5635;
      InsuranceJnlPostBatch@1007 : Codeunit 5653;
      JournalWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH InsuranceJnlBatch DO BEGIN
        InsuranceJnlTempl.GET("Journal Template Name");
        InsuranceJnlTempl.TESTFIELD("Force Posting Report",FALSE);

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          InsuranceJnlLine."Journal Template Name" := "Journal Template Name";
          InsuranceJnlLine."Journal Batch Name" := Name;
          InsuranceJnlLine."Line No." := 1;

          CLEAR(InsuranceJnlPostBatch);
          IF InsuranceJnlPostBatch.RUN(InsuranceJnlLine) THEN
            MARK(FALSE)
          ELSE BEGIN
            MARK(TRUE);
            JournalWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JournalWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}


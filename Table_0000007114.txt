OBJECT Table 7114 Analysis Line
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               AnalysisLineTemplate@1000 : Record 7112;
             BEGIN
               LOCKTABLE;
               AnalysisLineTemplate.GET("Analysis Area","Analysis Line Template Name");
             END;

    CaptionML=[ENU=Analysis Line;
               FRA=Ligne d'analyse];
    LookupPageID=Page7115;
    DrillDownPageID=Page7115;
  }
  FIELDS
  {
    { 1   ;   ;Analysis Area       ;Option        ;CaptionML=[ENU=Analysis Area;
                                                              FRA=Zone d'analyse];
                                                   OptionCaptionML=[ENU=Sales,Purchase,Inventory;
                                                                    FRA=Ventes,Achats,Stocks];
                                                   OptionString=Sales,Purchase,Inventory }
    { 2   ;   ;Analysis Line Template Name;Code10 ;TableRelation="Analysis Line Template".Name WHERE (Analysis Area=FIELD(Analysis Area));
                                                   CaptionML=[ENU=Analysis Line Template Name;
                                                              FRA=Nom modŠle de ligne d'analyse] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne] }
    { 4   ;   ;Row Ref. No.        ;Code20        ;OnValidate=BEGIN
                                                                IF "Row Ref. No." <> xRec."Row Ref. No." THEN
                                                                  SubstRowRefNo(xRec."Row Ref. No.","Row Ref. No.");
                                                              END;

                                                   CaptionML=[ENU=Row Ref. No.;
                                                              FRA=Nø r‚f. ligne] }
    { 5   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 6   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                InventorySetup@1000 : Record 313;
                                                                SalesSetup@1001 : Record 311;
                                                              BEGIN
                                                                IF Type <> xRec.Type THEN
                                                                  Range := '';
                                                                VALIDATE(Range);

                                                                CASE Type OF
                                                                  Type::"Item Group":
                                                                    BEGIN
                                                                      InventorySetup.GET;
                                                                      InventorySetup.TESTFIELD("Item Group Dimension Code");
                                                                      "Group Dimension Code" := InventorySetup."Item Group Dimension Code";
                                                                    END;
                                                                  Type::"Customer Group":
                                                                    BEGIN
                                                                      SalesSetup.GET;
                                                                      SalesSetup.TESTFIELD("Customer Group Dimension Code");
                                                                      "Group Dimension Code" := SalesSetup."Customer Group Dimension Code";
                                                                    END;
                                                                  Type::"Sales/Purchase person":
                                                                    BEGIN
                                                                      SalesSetup.GET;
                                                                      SalesSetup.TESTFIELD("Salesperson Dimension Code");
                                                                      "Group Dimension Code" := SalesSetup."Salesperson Dimension Code";
                                                                    END;
                                                                  ELSE
                                                                    "Group Dimension Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Item,Item Group,Customer,Customer Group,Vendor,Sales/Purchase person,Formula;
                                                                    FRA=Article,Groupe articles,Client,Groupe clients,Fournisseur,Vendeur/Acheteur,Formule];
                                                   OptionString=Item,Item Group,Customer,Customer Group,Vendor,Sales/Purchase person,Formula }
    { 7   ;   ;Range               ;Text250       ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Customer)) Customer
                                                                 ELSE IF (Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Type=CONST(Item Group)) "Dimension Value".Code WHERE (Dimension Code=FIELD(Group Dimension Code))
                                                                 ELSE IF (Type=CONST(Customer Group)) "Dimension Value".Code WHERE (Dimension Code=FIELD(Group Dimension Code))
                                                                 ELSE IF (Type=CONST(Sales/Purchase person)) "Dimension Value".Code WHERE (Dimension Code=FIELD(Group Dimension Code));
                                                   OnValidate=VAR
                                                                Item@1003 : Record 27;
                                                                Cust@1002 : Record 18;
                                                                Vend@1001 : Record 23;
                                                                ItemStatisticBuf@1000 : Record 5821;
                                                              BEGIN
                                                                CASE Type OF
                                                                  Type::Item:
                                                                    Item.SETFILTER("No.",Range);
                                                                  Type::Customer:
                                                                    Cust.SETFILTER("No.",Range);
                                                                  Type::Vendor:
                                                                    Vend.SETFILTER("No.",Range);
                                                                  Type::"Item Group",Type::"Customer Group",Type::"Sales/Purchase person":
                                                                    ItemStatisticBuf.SETFILTER("Dimension 1 Filter",Range);
                                                                  Type::Formula:
                                                                    BEGIN
                                                                      Range := UPPERCASE(Range);
                                                                      CheckFormula(Range);
                                                                    END;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Range;
                                                              FRA=Plage] }
    { 8   ;   ;New Page            ;Boolean       ;CaptionML=[ENU=New Page;
                                                              FRA=Nouvelle page] }
    { 9   ;   ;Show                ;Option        ;CaptionML=[ENU=Show;
                                                              FRA=Afficher];
                                                   OptionCaptionML=[ENU=Yes,No,If Any Column Not Zero;
                                                                    FRA=Oui,Non,Si diff‚rent de 0];
                                                   OptionString=Yes,No,If Any Column Not Zero }
    { 10  ;   ;Bold                ;Boolean       ;CaptionML=[ENU=Bold;
                                                              FRA=Gras] }
    { 11  ;   ;Italic              ;Boolean       ;CaptionML=[ENU=Italic;
                                                              FRA=Italique] }
    { 12  ;   ;Underline           ;Boolean       ;CaptionML=[ENU=Underline;
                                                              FRA=Soulign‚] }
    { 13  ;   ;Show Opposite Sign  ;Boolean       ;CaptionML=[ENU=Show Opposite Sign;
                                                              FRA=Afficher signe oppos‚] }
    { 14  ;   ;Source Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Source Type Filter;
                                                              FRA=Filtre type origine];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Item";
                                                                    FRA=" ,Client,Fournisseur,Article"];
                                                   OptionString=[ ,Customer,Vendor,Item] }
    { 15  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              FRA=Filtre date] }
    { 16  ;   ;Item Budget Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Item Budget Name".Name WHERE (Analysis Area=FIELD(Analysis Area));
                                                   CaptionML=[ENU=Item Budget Filter;
                                                              FRA=Filtre budget article] }
    { 17  ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=[ENU=Location Filter;
                                                              FRA=Filtre magasin] }
    { 18  ;   ;Dimension 1 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Dimension 1 Filter;
                                                              FRA=Filtre axe 1];
                                                   CaptionClass=GetCaptionClass(1) }
    { 19  ;   ;Dimension 2 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Dimension 2 Filter;
                                                              FRA=Filtre axe 2];
                                                   CaptionClass=GetCaptionClass(2) }
    { 20  ;   ;Dimension 3 Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Dimension 3 Filter;
                                                              FRA=Filtre axe 3];
                                                   CaptionClass=GetCaptionClass(3) }
    { 21  ;   ;Dimension 1 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 348=R;
                                                   CaptionML=[ENU=Dimension 1 Totaling;
                                                              FRA=Totalisation axe 1];
                                                   CaptionClass=GetCaptionClass(4) }
    { 22  ;   ;Dimension 2 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 348=R;
                                                   CaptionML=[ENU=Dimension 2 Totaling;
                                                              FRA=Totalisation axe 2];
                                                   CaptionClass=GetCaptionClass(5) }
    { 23  ;   ;Dimension 3 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   AccessByPermission=TableData 350=R;
                                                   CaptionML=[ENU=Dimension 3 Totaling;
                                                              FRA=Totalisation axe 3];
                                                   CaptionClass=GetCaptionClass(6) }
    { 24  ;   ;Source No. Filter   ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=IF (Source Type Filter=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type Filter=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type Filter=CONST(Item)) Item;
                                                   CaptionML=[ENU=Source No. Filter;
                                                              FRA=Filtre nø origine] }
    { 25  ;   ;Group Dimension Code;Code20        ;TableRelation=Dimension;
                                                   CaptionML=[ENU=Group Dimension Code;
                                                              FRA=Code axe groupe] }
    { 26  ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              FRA=Indentation];
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Analysis Area,Analysis Line Template Name,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=The parenthesis at position %1 is misplaced.;FRA=Position %1. La parenthŠse est mal plac‚e.';
      Text002@1002 : TextConst 'ENU=You cannot have two consecutive operators. The error occurred at position %1.;FRA=Position %1. Vous ne pouvez pas avoir deux op‚rateurs cons‚cutifs.';
      Text003@1003 : TextConst 'ENU=There is an operand missing after position %1.;FRA=Il manque un op‚rateur aprŠs la position %1.';
      Text004@1004 : TextConst 'ENU=There are more left parentheses than right parentheses.;FRA=Il y a plus de parenthŠses ouvrantes que de parenthŠses fermantes.';
      Text005@1005 : TextConst 'ENU=There are more right parentheses than left parentheses.;FRA=Il y a plus de parenthŠses fermantes que de parenthŠses ouvrantes.';
      Text009@1009 : TextConst 'ENU=1,6,,Dimension %1 Filter;FRA=1,6,,Filtre axe %1';
      Text010@1010 : TextConst 'ENU=,, Totaling;FRA=,, Totalisation';
      Text011@1011 : TextConst 'ENU=1,5,,Dimension %1 Totaling;FRA=1,5,,Totalisation axe %1';
      GLSetup@1018 : Record 98;
      ItemAnalysisView@1015 : Record 7152;
      HasGLSetup@1022 : Boolean;

    PROCEDURE CheckFormula@1(Formula@1000 : Code[250]);
    VAR
      i@1001 : Integer;
      ParenthesesLevel@1002 : Integer;
      HasOperator@1003 : Boolean;
    BEGIN
      ParenthesesLevel := 0;
      FOR i := 1 TO STRLEN(Formula) DO BEGIN
        IF Formula[i] = '(' THEN
          ParenthesesLevel := ParenthesesLevel + 1
        ELSE
          IF Formula[i] = ')' THEN
            ParenthesesLevel := ParenthesesLevel - 1;
        IF ParenthesesLevel < 0 THEN
          ERROR(Text001,i);
        IF Formula[i] IN ['+','-','*','/','^'] THEN BEGIN
          IF HasOperator THEN
            ERROR(Text002,i);

          HasOperator := TRUE;
          IF i = STRLEN(Formula) THEN
            ERROR(Text003,i);

          IF Formula[i + 1] = ')' THEN
            ERROR(Text003,i);
        END ELSE
          HasOperator := FALSE;
      END;
      IF ParenthesesLevel > 0 THEN
        ERROR(Text004);

      IF ParenthesesLevel < 0 THEN
        ERROR(Text005);
    END;

    LOCAL PROCEDURE SubstRowRefNo@5(OldRowRefNo@1002 : Code[20];NewRowRefNo@1000 : Code[20]);
    VAR
      FormulaAnalysisLine@1001 : Record 7114;
      OriginalRange@1005 : Text[250];
      NewRange@1004 : Text[250];
      Token@1008 : Text[250];
      TheChar@1009 : Text[1];
      i@1006 : Integer;
      TheLength@1003 : Integer;
    BEGIN
      IF (OldRowRefNo = '') OR (NewRowRefNo = '') THEN
        EXIT;
      FormulaAnalysisLine.SETRANGE("Analysis Area","Analysis Area");
      FormulaAnalysisLine.SETRANGE("Analysis Line Template Name","Analysis Line Template Name");
      FormulaAnalysisLine.SETRANGE(Type,Type::Formula);
      IF NOT FormulaAnalysisLine.FIND('-') THEN
        EXIT;
      REPEAT
        OriginalRange := FormulaAnalysisLine.Range;
        TheLength := STRLEN(FormulaAnalysisLine.Range);
        NewRange := '';

        FOR i := 1 TO TheLength DO BEGIN
          TheChar := COPYSTR(FormulaAnalysisLine.Range,i,1);
          IF TheChar IN ['0'..'9','A'..'Z'] THEN BEGIN
            Token := Token + TheChar;
            IF i = TheLength THEN BEGIN
              IF Token = OldRowRefNo THEN
                NewRange := NewRange + NewRowRefNo
              ELSE
                NewRange := NewRange + Token;
              Token := '';
            END;
          END ELSE BEGIN
            IF Token = OldRowRefNo THEN
              NewRange := NewRange + NewRowRefNo
            ELSE
              NewRange := NewRange + Token;
            NewRange := NewRange + TheChar;
            Token := '';
          END;
        END;
        IF NewRange <> OriginalRange THEN
          IF "Line No." = FormulaAnalysisLine."Line No." THEN
            Range := NewRange
          ELSE BEGIN
            FormulaAnalysisLine.Range := NewRange;
            FormulaAnalysisLine.MODIFY;
          END;
      UNTIL FormulaAnalysisLine.NEXT = 0;
    END;

    PROCEDURE LookupTotalingRange@3(VAR Text@1000 : Text[1024]) : Boolean;
    VAR
      InventorySetup@1007 : Record 313;
      SalesSetup@1006 : Record 311;
      ItemList@1005 : Page 31;
      CustList@1004 : Page 22;
      VendList@1003 : Page 27;
    BEGIN
      CASE Type OF
        Type::Item:
          BEGIN
            ItemList.LOOKUPMODE(TRUE);
            IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
              Text := ItemList.GetSelectionFilter;
              EXIT(TRUE);
            END;
          END;
        Type::Customer:
          BEGIN
            CustList.LOOKUPMODE(TRUE);
            IF CustList.RUNMODAL = ACTION::LookupOK THEN BEGIN
              Text := CustList.GetSelectionFilter;
              EXIT(TRUE);
            END;
          END;
        Type::Vendor:
          BEGIN
            VendList.LOOKUPMODE(TRUE);
            IF VendList.RUNMODAL = ACTION::LookupOK THEN BEGIN
              Text := VendList.GetSelectionFilter;
              EXIT(TRUE);
            END;
          END;
        Type::"Item Group":
          BEGIN
            InventorySetup.GET;
            InventorySetup.TESTFIELD("Item Group Dimension Code");
            EXIT(LookupDimTotalingRange(Text,InventorySetup."Item Group Dimension Code"));
          END;
        Type::"Customer Group":
          BEGIN
            SalesSetup.GET;
            SalesSetup.TESTFIELD("Customer Group Dimension Code");
            EXIT(LookupDimTotalingRange(Text,SalesSetup."Customer Group Dimension Code"));
          END;
        Type::"Sales/Purchase person":
          BEGIN
            SalesSetup.GET;
            SalesSetup.TESTFIELD("Salesperson Dimension Code");
            EXIT(LookupDimTotalingRange(Text,SalesSetup."Salesperson Dimension Code"));
          END;
      END;
    END;

    PROCEDURE LookupDimTotalingRange@4(VAR Text@1000 : Text[1024];GroupDimCode@1003 : Code[20]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
      DimValList@1001 : Page 560;
    BEGIN
      DimVal.FILTERGROUP := 2;
      DimVal.SETRANGE("Dimension Code",GroupDimCode);
      DimVal.FILTERGROUP := 0;
      DimValList.SETTABLEVIEW(DimVal);
      DimValList.LOOKUPMODE(TRUE);
      IF DimValList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := DimValList.GetSelectionFilter;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetCaptionClass@12(DimNo@1000 : Integer) : Text[250];
    BEGIN
      GetItemAnalysisView;

      CASE DimNo OF
        1:
          IF ItemAnalysisView."Dimension 1 Code" <> '' THEN
            EXIT('1,6,' + ItemAnalysisView."Dimension 1 Code");
        2:
          IF ItemAnalysisView."Dimension 2 Code" <> '' THEN
            EXIT('1,6,' + ItemAnalysisView."Dimension 2 Code");
        3:
          IF ItemAnalysisView."Dimension 3 Code" <> '' THEN
            EXIT('1,6,' + ItemAnalysisView."Dimension 3 Code");
        4:
          IF ItemAnalysisView."Dimension 1 Code" <> '' THEN
            EXIT('1,5,' + ItemAnalysisView."Dimension 1 Code" + Text010);
        5:
          IF ItemAnalysisView."Dimension 2 Code" <> '' THEN
            EXIT('1,5,' + ItemAnalysisView."Dimension 2 Code" + Text010);
        6:
          IF ItemAnalysisView."Dimension 3 Code" <> '' THEN
            EXIT('1,5,' + ItemAnalysisView."Dimension 3 Code" + Text010);
      END;
      IF DimNo <= 3 THEN
        EXIT(STRSUBSTNO(Text009,DimNo));
      EXIT(STRSUBSTNO(Text011,DimNo - 3));
    END;

    LOCAL PROCEDURE GetItemAnalysisView@2();
    VAR
      AnalysisLineTemplate@1000 : Record 7112;
    BEGIN
      IF (AnalysisLineTemplate."Analysis Area" = "Analysis Area") AND
         (AnalysisLineTemplate.Name = "Analysis Line Template Name")
      THEN
        EXIT;

      IF AnalysisLineTemplate.GET("Analysis Area","Analysis Line Template Name") THEN
        IF AnalysisLineTemplate."Item Analysis View Code" <> '' THEN
          ItemAnalysisView.GET("Analysis Area",AnalysisLineTemplate."Item Analysis View Code")
        ELSE BEGIN
          CLEAR(ItemAnalysisView);
          IF NOT HasGLSetup THEN
            GLSetup.GET;
          HasGLSetup := TRUE;
          ItemAnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
          ItemAnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
        END;
    END;

    BEGIN
    END.
  }
}


OBJECT Table 1227 Data Exch. Line Def
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               DataExchMapping@1001 : Record 1224;
               DataExchColumnDef@1000 : Record 1223;
             BEGIN
               DataExchMapping.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
               DataExchMapping.SETRANGE("Data Exch. Line Def Code",Code);
               DataExchMapping.DELETEALL(TRUE);

               DataExchColumnDef.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
               DataExchColumnDef.SETRANGE("Data Exch. Line Def Code",Code);
               DataExchColumnDef.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Data Exch. Line Def;
               FRA=DÇf. ligne Çchge donnÇes];
  }
  FIELDS
  {
    { 1   ;   ;Data Exch. Def Code ;Code20        ;TableRelation="Data Exch. Def".Code;
                                                   CaptionML=[ENU=Data Exch. Def Code;
                                                              FRA=Code dÇf. Çchge donnÇes];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Name                ;Text100       ;CaptionML=[ENU=Name;
                                                              FRA=Nom] }
    { 4   ;   ;Column Count        ;Integer       ;CaptionML=[ENU=Column Count;
                                                              FRA=Nombre colonnes] }
    { 5   ;   ;Data Line Tag       ;Text250       ;CaptionML=[ENU=Data Line Tag;
                                                              FRA=êtiquette ligne donnÇes] }
    { 6   ;   ;Namespace           ;Text250       ;CaptionML=[ENU=Namespace;
                                                              FRA=Espace de noms] }
    { 10  ;   ;Parent Code         ;Code20        ;TableRelation="Data Exch. Line Def".Code WHERE (Data Exch. Def Code=FIELD(Data Exch. Def Code));
                                                   OnValidate=BEGIN
                                                                IF "Parent Code" = '' THEN
                                                                  EXIT;
                                                                IF "Parent Code" = Code THEN
                                                                  ERROR(STRSUBSTNO(DontPointToTheSameLineErr,FIELDCAPTION("Parent Code"),FIELDCAPTION(Code)));
                                                              END;

                                                   CaptionML=[ENU=Parent Code;
                                                              FRA=Code parent] }
    { 11  ;   ;Line Type           ;Option        ;CaptionML=[ENU=Line Type;
                                                              FRA=Type ligne];
                                                   OptionCaptionML=[ENU=Detail,Header,Footer;
                                                                    FRA=DÇtail,En-tàte,Pied de page];
                                                   OptionString=Detail,Header,Footer }
  }
  KEYS
  {
    {    ;Data Exch. Def Code,Code                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      IncorrectNamespaceErr@1000 : TextConst '@@@="%1=file namespace,%2=supported namespace";ENU=The imported file contains unsupported namespace "%1". The supported namespace is ''%2''.;FRA=Le fichier importÇ contient l''espace de noms non pris en chargeˇÆˇ%1ˇØ. L''espace de noms pris en charge est Æˇ%2ˇØ.';
      DontPointToTheSameLineErr@1001 : TextConst '@@@="%1 =Parent Code and %2 = Code";ENU=%1 cannot be the same as %2.;FRA=%1 ne peut pas àtre identique Ö %2.';

    PROCEDURE InsertRec@7(DataExchDefCode@1000 : Code[20];Code@1001 : Code[20];Name@1002 : Text[100];ColumnCount@1003 : Integer);
    BEGIN
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE(Code,Code);
      VALIDATE(Name,Name);
      VALIDATE("Column Count",ColumnCount);
      INSERT;
    END;

    PROCEDURE ValidateNamespace@3(XMLNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      NamespaceURI@1001 : Text;
    BEGIN
      IF Namespace <> '' THEN BEGIN
        NamespaceURI := XMLNode.NamespaceURI;
        IF NamespaceURI <> Namespace THEN
          ERROR(IncorrectNamespaceErr,NamespaceURI,Namespace);
      END;
    END;

    PROCEDURE GetPath@1(TableId@1000 : Integer;FieldId@1001 : Integer) : Text;
    VAR
      DataExchFieldMapping@1002 : Record 1225;
      DataExchDef@1003 : Record 1222;
    BEGIN
      DataExchDef.GET("Data Exch. Def Code");
      DataExchFieldMapping.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
      DataExchFieldMapping.SETRANGE("Data Exch. Line Def Code",Code);
      IF DataExchDef.Type = DataExchDef.Type::"Generic Import" THEN BEGIN
        DataExchFieldMapping.SETRANGE("Target Table ID",TableId);
        DataExchFieldMapping.SETRANGE("Target Field ID",FieldId);
      END ELSE BEGIN
        DataExchFieldMapping.SETRANGE("Table ID",TableId);
        DataExchFieldMapping.SETRANGE("Field ID",FieldId);
      END;
      IF DataExchFieldMapping.FINDFIRST THEN
        EXIT(DataExchFieldMapping.GetPath);
      EXIT('');
    END;

    BEGIN
    END.
  }
}


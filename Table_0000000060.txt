OBJECT Table 60 Document Sending Profile
{
  OBJECT-PROPERTIES
  {
    Date=23/10/15;
    Time=12:00:00;
    Version List=NAVW19.00.00.43402;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DocumentSendingProfile@1000 : Record 60;
             BEGIN
               DocumentSendingProfile.SETRANGE(Default,TRUE);
               IF NOT DocumentSendingProfile.FINDFIRST THEN
                 Default := TRUE;
             END;

    OnDelete=VAR
               Customer@1000 : Record 18;
             BEGIN
               IF Default THEN
                 ERROR(CannotDeleteDefaultRuleErr);

               Customer.SETRANGE("Document Sending Profile",Code);
               IF Customer.FINDFIRST THEN BEGIN
                 IF CONFIRM(UpdateAssCustomerQst,FALSE,Code) THEN
                   Customer.MODIFYALL("Document Sending Profile",'')
                 ELSE
                   ERROR(CannotDeleteErr);
               END;
             END;

    CaptionML=[ENU=Document Sending Profile;
               FRA=Profil d'envoi de documents];
    LookupPageID=Page359;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=DÇsignation] }
    { 10  ;   ;Printer             ;Option        ;CaptionML=[ENU=Printer;
                                                              FRA=Imprimante];
                                                   OptionCaptionML=[ENU=No,Yes (Prompt for Settings),Yes (Use Default Settings);
                                                                    FRA=Non,Oui (Afficher une invite pour le rÇglage des paramätres),Oui (Utiliser les paramätres par dÇfaut)];
                                                   OptionString=No,Yes (Prompt for Settings),Yes (Use Default Settings) }
    { 11  ;   ;E-Mail              ;Option        ;CaptionML=[ENU=E-Mail;
                                                              FRA=E-mail];
                                                   OptionCaptionML=[ENU=No,Yes (Prompt for Settings),Yes (Use Default Settings);
                                                                    FRA=Non,Oui (Afficher une invite pour le rÇglage des paramätres),Oui (Utiliser les paramätres par dÇfaut)];
                                                   OptionString=No,Yes (Prompt for Settings),Yes (Use Default Settings) }
    { 12  ;   ;E-Mail Attachment   ;Option        ;CaptionML=[ENU=E-Mail Attachment;
                                                              FRA=Piäce jointe Ö l'e-mail];
                                                   OptionCaptionML=[ENU=PDF,Electronic Document,PDF & Electronic Document;
                                                                    FRA=PDF,Document Çlectronique,PDF et document Çlectronique];
                                                   OptionString=PDF,Electronic Document,PDF & Electronic Document }
    { 13  ;   ;E-Mail Format       ;Code20        ;TableRelation="Electronic Document Format".Code;
                                                   CaptionML=[ENU=E-Mail Format;
                                                              FRA=Format E-mail] }
    { 15  ;   ;Disk                ;Option        ;CaptionML=[ENU=Disk;
                                                              FRA=Disque];
                                                   OptionCaptionML=[ENU=No,PDF,Electronic Document,PDF & Electronic Document;
                                                                    FRA=Aucun,PDF,Document Çlectronique,PDF et document Çlectronique];
                                                   OptionString=No,PDF,Electronic Document,PDF & Electronic Document }
    { 16  ;   ;Disk Format         ;Code20        ;TableRelation="Electronic Document Format".Code;
                                                   CaptionML=[ENU=Disk Format;
                                                              FRA=Format disque] }
    { 20  ;   ;Electronic Document ;Option        ;CaptionML=[ENU=Electronic Document;
                                                              FRA=Document Çlectronique];
                                                   OptionCaptionML=[ENU=No,Through Document Exchange Service;
                                                                    FRA=Aucun,Via le service d'Çchange de documents];
                                                   OptionString=No,Through Document Exchange Service }
    { 21  ;   ;Electronic Format   ;Code20        ;TableRelation="Electronic Document Format".Code;
                                                   CaptionML=[ENU=Electronic Format;
                                                              FRA=Format Çlectronique] }
    { 30  ;   ;Default             ;Boolean       ;OnValidate=VAR
                                                                DocumentSendingProfile@1001 : Record 60;
                                                              BEGIN
                                                                IF (xRec.Default = TRUE) AND (Default = FALSE) THEN
                                                                  ERROR(CannotRemoveDefaultRuleErr);

                                                                DocumentSendingProfile.SETRANGE(Default,TRUE);
                                                                DocumentSendingProfile.MODIFYALL(Default,FALSE,FALSE);
                                                              END;

                                                   CaptionML=[ENU=Default;
                                                              FRA=Par dÇfaut] }
    { 50  ;   ;Send To             ;Option        ;CaptionML=[ENU=Send To;
                                                              FRA=Envoyer Ö];
                                                   OptionCaptionML=[ENU=Disk,E-Mail,Print,Electronic Document;
                                                                    FRA=Disque,E-Mail,Impression,Document Çlectronique];
                                                   OptionString=Disk,E-Mail,Print,Electronic Document }
    { 51  ;   ;Usage               ;Option        ;CaptionML=[ENU=Usage;
                                                              FRA=Utilisation];
                                                   OptionCaptionML=[ENU=Sales Invoice,Sales Credit Memo,Service Invoice,Service Credit Memo;
                                                                    FRA=Facture vente,Avoir vente,Facture service,Avoir service];
                                                   OptionString=Sales Invoice,Sales Credit Memo,Service Invoice,Service Credit Memo }
    { 52  ;   ;One Related Party Selected;Boolean ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "One Related Party Selected" THEN BEGIN
                                                                  "Electronic Document" := "Electronic Document"::No;
                                                                  "Electronic Format" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=One Related Party Selected;
                                                              FRA=Une partie associÇe est sÇlectionnÇe] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DefaultCodeTxt@1000 : TextConst '@@@=Translate as we translate default term in local languages;ENU=DEFAULT;FRA=PAR DêFAUT';
      DefaultDescriptionTxt@1001 : TextConst 'ENU=Default rule used if no other provided;FRA=Rägle par dÇf. utilisÇe si aucune rägle dÇfinie';
      RecordAsTextFormatterTxt@1002 : TextConst 'ENU="%1 ; %2";FRA="%1ˇ; %2"';
      FieldCaptionContentFormatterTxt@1007 : TextConst '@@@="%1=Field Caption (e.g. E-mail), %2=Field Content (e.g. PDF) so for example ''E-mail (PDF)''";ENU=%1 (%2);FRA=%1 (%2)';
      CannotDeleteDefaultRuleErr@1003 : TextConst 'ENU=You cannot delete the default rule. Assign other rule to be default first.;FRA=Vous ne pouvez pas supprimer la rägle par dÇfaut. DÇfinissez d''abord une autre rägle par dÇfaut.';
      CannotRemoveDefaultRuleErr@1004 : TextConst 'ENU=There must be one default rule in the system. To remove the default property from this rule, assign default to another rule.;FRA=Le systäme doit contenir une rägle par dÇfaut. Pour que cette rägle ne soit plus la rägle par dÇfaut, dÇfinissez une autre rägle comme rägle par dÇfaut.';
      UpdateAssCustomerQst@1005 : TextConst 'ENU=If you delete document sending profile %1, it will also be deleted on customer cards that use the profile.\\Do you want to continue?;FRA=Si vous supprimez le profil d''envoi de documents %1, ce dernier sera Çgalement supprimÇ des fiches clients qui utilisent le profil.\\Souhaitez-vous continuerˇ?';
      CannotDeleteErr@1006 : TextConst 'ENU=Cannot delete the document sending profile.;FRA=Impossible de supprimer le profil d''envoi de documents.';
      CannotSendMultipleSalesDocsErr@1008 : TextConst 'ENU=You can only send one electronic sales document at a time.;FRA=Vous ne pouvez envoyer qu''un seul document vente Çlectronique Ö la fois.';

    PROCEDURE GetDefaultForCustomer@4(CustomerNo@1000 : Code[20];VAR DocumentSendingProfile@1002 : Record 60);
    VAR
      Customer@1001 : Record 18;
    BEGIN
      IF Customer.GET(CustomerNo) THEN
        IF DocumentSendingProfile.GET(Customer."Document Sending Profile") THEN
          EXIT;

      GetDefault(DocumentSendingProfile);
    END;

    PROCEDURE GetDefault@1(VAR DefaultDocumentSendingProfile@1000 : Record 60);
    VAR
      DocumentSendingProfile@1001 : Record 60;
    BEGIN
      DocumentSendingProfile.SETRANGE(Default,TRUE);
      IF NOT DocumentSendingProfile.FINDFIRST THEN BEGIN
        DocumentSendingProfile.INIT;
        DocumentSendingProfile.VALIDATE(Code,DefaultCodeTxt);
        DocumentSendingProfile.VALIDATE(Description,DefaultDescriptionTxt);
        DocumentSendingProfile.VALIDATE("E-Mail","E-Mail"::"Yes (Prompt for Settings)");
        DocumentSendingProfile.VALIDATE("E-Mail Attachment","E-Mail Attachment"::PDF);
        DocumentSendingProfile.VALIDATE(Default,TRUE);
        DocumentSendingProfile.INSERT(TRUE);
      END;

      DefaultDocumentSendingProfile := DocumentSendingProfile;
    END;

    PROCEDURE GetRecordAsText@2() : Text;
    VAR
      RecordAsText@1000 : Text;
    BEGIN
      RecordAsText := '';

      IF ("Electronic Document" <> "Electronic Document"::No) AND ("Electronic Format" <> '') THEN
        RecordAsText := STRSUBSTNO(
            RecordAsTextFormatterTxt,
            STRSUBSTNO(FieldCaptionContentFormatterTxt,FIELDCAPTION("Electronic Document"),"Electronic Document"),RecordAsText);

      IF "E-Mail" <> "E-Mail"::No THEN
        RecordAsText := STRSUBSTNO(
            RecordAsTextFormatterTxt,
            STRSUBSTNO(FieldCaptionContentFormatterTxt,FIELDCAPTION("E-Mail"),"E-Mail Attachment"),RecordAsText);

      IF Printer <> Printer::No THEN
        RecordAsText := STRSUBSTNO(RecordAsTextFormatterTxt,FIELDCAPTION(Printer),RecordAsText);

      IF Disk <> Disk::No THEN
        RecordAsText := STRSUBSTNO(
            RecordAsTextFormatterTxt,STRSUBSTNO(FieldCaptionContentFormatterTxt,FIELDCAPTION(Disk),Disk),RecordAsText);

      EXIT(RecordAsText);
    END;

    PROCEDURE WillUserBePrompted@5() : Boolean;
    BEGIN
      EXIT(
        (Printer = Printer::"Yes (Prompt for Settings)") OR
        ("E-Mail" = "E-Mail"::"Yes (Prompt for Settings)"));
    END;

    PROCEDURE SetDocumentUsage@3(DocumentVariant@1000 : Variant);
    VAR
      ElectronicDocumentFormat@1001 : Record 61;
      DocumentUsage@1002 : Option;
    BEGIN
      ElectronicDocumentFormat.GetDocumentUsage(DocumentUsage,DocumentVariant);
      VALIDATE(Usage,DocumentUsage);
    END;

    PROCEDURE VerifySelectedOptionsValid@6();
    BEGIN
      IF "One Related Party Selected" THEN
        EXIT;

      IF "E-Mail Attachment" > "E-Mail Attachment"::PDF THEN
        ERROR(CannotSendMultipleSalesDocsErr);

      IF "Electronic Document" > "Electronic Document"::No THEN
        ERROR(CannotSendMultipleSalesDocsErr);
    END;

    BEGIN
    END.
  }
}


OBJECT Codeunit 447 Record Link Management
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            IF CONFIRM(Text001,FALSE) THEN BEGIN
              RemoveOrphanedLink;
              MESSAGE(Text004,NoOfRemoved);
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Do you want to remove links with no record reference?;FRA=Voulez-vous supprimer des liens sans r‚f‚rence d''enregistrement ?';
      Text002@1001 : TextConst 'ENU=Removing Record Links without record reference.\;FRA=Retrait des liens d''enregistrement sans r‚f‚rence d''enregistrement.\';
      Text003@1002 : TextConst 'ENU=@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;FRA=@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text004@1003 : TextConst 'ENU=%1 orphaned links were removed.;FRA=Les liens orphelins %1 ont ‚t‚ supprim‚s.';
      NoOfRemoved@1004 : Integer;

    LOCAL PROCEDURE RemoveOrphanedLink@5();
    VAR
      RecordLink@1000 : Record 2000000068;
      RecordRef@1001 : RecordRef;
      Window@1002 : Dialog;
      i@1003 : Integer;
      Total@1004 : Integer;
      TimeLocked@1005 : Time;
      InTransaction@1011 : Boolean;
      PrevRecID@1007 : RecordID;
      RecordExists@1010 : Boolean;
    BEGIN
      Window.OPEN(Text002 + Text003);
      TimeLocked := TIME;
      WITH RecordLink DO BEGIN
        SETFILTER(Company,'%1|%2','',COMPANYNAME);
        SETCURRENTKEY("Record ID");
        Total := COUNT;
        IF Total = 0 THEN
          EXIT;
        IF FIND('-') THEN
          REPEAT
            i := i + 1;
            IF (i MOD 1000) = 0 THEN
              Window.UPDATE(1,ROUND(i / Total * 10000,1));
            IF FORMAT("Record ID") <> FORMAT(PrevRecID) THEN BEGIN  // Direct comparison doesn't work.
              PrevRecID := "Record ID";
              RecordExists := RecordRef.GET("Record ID");
            END;
            IF NOT RecordExists THEN BEGIN
              DELETE;
              NoOfRemoved := NoOfRemoved + 1;
              IF NOT InTransaction THEN
                TimeLocked := TIME;
              InTransaction := TRUE;
            END;
            IF InTransaction AND (TIME > (TimeLocked + 1000)) THEN BEGIN
              COMMIT;
              TimeLocked := TIME;
              InTransaction := FALSE;
            END;
          UNTIL NEXT = 0;
      END;
      Window.CLOSE;
    END;

    LOCAL PROCEDURE ResetNotifyOnLinks@11(RecVar@1000 : Variant);
    VAR
      RecordLink@1002 : Record 2000000068;
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(RecVar);
      RecordLink.SETRANGE("Record ID",RecRef.RECORDID);
      RecordLink.SETRANGE(Notify,TRUE);
      IF NOT RecordLink.ISEMPTY THEN
        RecordLink.MODIFYALL(Notify,FALSE);
    END;

    PROCEDURE CopyLinks@20(FromRecord@1000 : Variant;ToRecord@1001 : Variant);
    VAR
      RecRefTo@1002 : RecordRef;
    BEGIN
      RecRefTo.GETTABLE(ToRecord);
      RecRefTo.COPYLINKS(FromRecord);
      ResetNotifyOnLinks(RecRefTo);
    END;

    BEGIN
    END.
  }
}


OBJECT Report 5692 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00;
    Version List=NAVW17.10,NAVFR7.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Calculate Depreciation;
               FRA=Calculer amortissement];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CLEAR(DeprBook2);
                  DeprBook.GET(DeprBookCode);

                  IF DeprBook."Derogatory Calculation" <> '' THEN
                    ERROR(Text10800,DeprBook.Code);
                  DeprBook2.SETRANGE("Derogatory Calculation",DeprBookCode);
                  IF DeprBook2.FIND('-') THEN ;

                  IF DeprUntilDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF PostingDate = 0D THEN
                    PostingDate := DeprUntilDate;
                  IF UseForceNoOfDays AND (DaysInPeriod = 0) THEN
                    ERROR(Text001);

                  IF DeprBook."Use Same FA+G/L Posting Dates" AND (DeprUntilDate <> PostingDate) THEN
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      FALSE,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  Window.OPEN(
                    Text003 +
                    Text004 +
                    Text005);
                END;

  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               OnAfterGetRecord=BEGIN
                                  IF Inactive OR Blocked THEN
                                    CurrReport.SKIP;

                                  HasDerogatorySetup := FALSE;
                                  FADeprBook.SETRANGE("FA No.","No.");
                                  FADeprBook.SETRANGE("Depreciation Book Code",DeprBook2.Code);
                                  IF FADeprBook.FIND('-') THEN
                                    HasDerogatorySetup := TRUE;

                                  CalculateDepr.Calculate(
                                    DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                                    "No.",DeprBookCode,DeprUntilDate,EntryAmounts,0D,DaysInPeriod);
                                  IF (DeprAmount <> 0) OR (Custom1Amount <> 0) THEN
                                    Window.UPDATE(1,"No.")
                                  ELSE
                                    Window.UPDATE(2,"No.");

                                  IF Custom1Amount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Custom 1" OR "Budgeted Asset" THEN BEGIN
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::"Custom 1";
                                      FAJnlLineTmp.Amount := Custom1Amount;
                                      FAJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::"Custom 1";
                                      GenJnlLineTmp.Amount := Custom1Amount;
                                      GenJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    END;

                                  IF DeprAmount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Depreciation" OR "Budgeted Asset" THEN BEGIN
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Depreciation;
                                      FAJnlLineTmp.Amount := DeprAmount;
                                      FAJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Depreciation;
                                      GenJnlLineTmp.Amount := DeprAmount;
                                      GenJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    END;

                                  IF HasDerogatorySetup THEN
                                    DeprAmount2 := DeprAmount;
                                END;

               OnPostDataItem=BEGIN
                                WITH FAJnlLine DO BEGIN
                                  IF FAJnlLineTmp.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetFANoSeries(FAJnlLine);
                                    IF DocumentNo = '' THEN
                                      DocumentNo2 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,DeprUntilDate,TRUE)
                                    ELSE
                                      DocumentNo2 := DocumentNo;
                                  END;
                                  IF FAJnlLineTmp.FIND('-') THEN
                                    REPEAT
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "Posting Date" := 0D;
                                      "FA Posting Type" := FAJnlLineTmp."FA Posting Type";
                                      VALIDATE("FA No.",FAJnlLineTmp."FA No.");
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      Description := PostingDescription;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,FAJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := FAJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := FAJnlLineTmp."FA Error Entry No.";
                                      FAJnlNextLineNo := FAJnlNextLineNo + 10000;
                                      "Line No." := FAJnlNextLineNo;
                                      INSERT(TRUE);
                                    UNTIL FAJnlLineTmp.NEXT = 0;
                                END;

                                WITH GenJnlLine DO BEGIN
                                  IF GenJnlLineTmp.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetGenNoSeries(GenJnlLine);
                                    IF DocumentNo = '' THEN
                                      DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,DeprUntilDate,TRUE)
                                    ELSE
                                      DocumentNo2 := DocumentNo;
                                  END;
                                  IF GenJnlLineTmp.FIND('-') THEN
                                    REPEAT
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "FA Posting Date" := 0D;
                                      "FA Posting Type" := GenJnlLineTmp."FA Posting Type";
                                      "Account Type" := "Account Type"::"Fixed Asset";
                                      VALIDATE("Account No.",GenJnlLineTmp."Account No.");
                                      IF GenJnlLineTmp.Description <> '' THEN
                                        Description := GenJnlLineTmp.Description
                                      ELSE
                                        Description := PostingDescription;
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,GenJnlLineTmp.Amount);
                                      "No. of Depreciation Days" := GenJnlLineTmp."No. of Depreciation Days";
                                      "FA Error Entry No." := GenJnlLineTmp."FA Error Entry No.";
                                      GenJnlNextLineNo := GenJnlNextLineNo + 1000;
                                      "Line No." := GenJnlNextLineNo;
                                      INSERT(TRUE);
                                      IF BalAccount THEN
                                        FAInsertGLAcc.GetBalAcc2(GenJnlLine,GenJnlNextLineNo);
                                    UNTIL GenJnlLineTmp.NEXT = 0;
                                END;
                              END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code,Budgeted Asset }

    { 1170000001;1;DataItem;CalcDerogDepr    ;
               DataItemTable=Table5600;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=BEGIN
                                  IF NOT HasDerogatorySetup THEN
                                    CurrReport.SKIP;

                                  CalculateDepr.Calculate(
                                    DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                                    "No.",DeprBook2.Code,DeprUntilDate,EntryAmounts,0D,DaysInPeriod);

                                  IF DeprAmount2 <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Derogatory" OR "Budgeted Asset" THEN BEGIN
                                      FAJnlLineTmp."FA No." := "No.";
                                      FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Derogatory;
                                      FAJnlLineTmp.Amount := CalcDerogDeprAmount(DeprAmount,DeprAmount2);
                                      FAJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      FAJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                      FAJnlLineTmp.INSERT;
                                    END ELSE BEGIN
                                      GenJnlLineTmp."Account No." := "No.";
                                      GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Derogatory;
                                      GenJnlLineTmp.Amount := CalcDerogDeprAmount(DeprAmount,DeprAmount2);
                                      GenJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                                      GenJnlLineTmp."FA Error Entry No." := ErrorNo;
                                      GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                      GenJnlLineTmp.INSERT;
                                    END;
                                  FADeprBook.GET("No.",DeprBookCode);
                                  IF (DeprUntilDate >= FADeprBook."Depreciation Ending Date") THEN BEGIN
                                    FADeprBook.CALCFIELDS(Derogatory);
                                    // include last derogatory depreciation amount when calculating final balance
                                    IF NOT DeprBook."G/L Integration - Derogatory" OR "Budgeted Asset" THEN
                                      IF (FADeprBook.Derogatory + FAJnlLineTmp.Amount <> 0) THEN BEGIN
                                        FAJnlLineTmp."FA No." := "No.";
                                        FAJnlLineTmp.Description := STRSUBSTNO(Text10801,DeprUntilDate);
                                        FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Derogatory;
                                        FAJnlLineTmp.Amount := -FADeprBook.Derogatory - FAJnlLineTmp.Amount;
                                        FAJnlLineTmp."No. of Depreciation Days" := 0;
                                        FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                                        FAJnlLineTmp.INSERT;
                                      END
                                    ELSE
                                      IF (FADeprBook.Derogatory + GenJnlLineTmp.Amount <> 0) THEN BEGIN
                                        GenJnlLineTmp."Account No." := "No.";
                                        GenJnlLineTmp.Description := STRSUBSTNO(Text10801,DeprUntilDate);
                                        GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Derogatory;
                                        GenJnlLineTmp.Amount := -FADeprBook.Derogatory - GenJnlLineTmp.Amount;
                                        GenJnlLineTmp."No. of Depreciation Days" := 0;
                                        GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                                        GenJnlLineTmp.INSERT;
                                      END;
                                  END;
                                END;

               DataItemLink=No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 15  ;2   ;Field     ;
                  Name=DepreciationBook;
                  CaptionML=[ENU=Depreciation Book;
                             FRA=Lois d'amortissement];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 9   ;2   ;Field     ;
                  Name=FAPostingDate;
                  CaptionML=[ENU=FA Posting Date;
                             FRA=Date compta. immo.];
                  SourceExpr=DeprUntilDate }

      { 8   ;2   ;Field     ;
                  Name=UseForceNoOfDays;
                  CaptionML=[ENU=Use Force No. of Days;
                             FRA=Utiliser forcer nombre de jours];
                  SourceExpr=UseForceNoOfDays;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays THEN
                                 DaysInPeriod := 0;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ForceNoOfDays;
                  CaptionML=[ENU=Force No. of Days;
                             FRA=Forcer nombre de jours];
                  BlankZero=Yes;
                  SourceExpr=DaysInPeriod;
                  MinValue=0;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays AND (DaysInPeriod <> 0) THEN
                                 ERROR(Text006);
                             END;
                              }

      { 11  ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             FRA=Date comptabilisation];
                  SourceExpr=PostingDate }

      { 1   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             FRA=Nø document];
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             FRA=Libell‚ ‚criture];
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  Name=InsertBalAccount;
                  CaptionML=[ENU=Insert Bal. Account;
                             FRA=Ins‚rer compte contrepartie];
                  SourceExpr=BalAccount }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;FRA=Vous devez sp‚cifier une valeur %1.';
      Text001@1001 : TextConst 'ENU=Force No. of Days must be activated.;FRA=Le champ Forcer nombre de jours doit ˆtre activ‚.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";FRA="%1 et %2 doivent ˆtre identiques. %3 doit ˆtre %4 dans %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Depreciating fixed asset      #1##########\;FRA=Amortissement des immobilisations  #1##########\';
      Text004@1004 : TextConst 'ENU=Not depreciating fixed asset  #2##########\;FRA=Immobilisation non amort.          #2##########\';
      Text005@1005 : TextConst 'ENU=Inserting journal lines       #3##########;FRA=Insertion dans feuille             #3##########';
      Text006@1006 : TextConst 'ENU=Use Force No. of Days must be activated.;FRA=Le champ Utiliser forcer nombre de jours doit ˆtre activ‚.';
      GenJnlLine@1007 : Record 81;
      GenJnlLineTmp@1008 : TEMPORARY Record 81;
      FASetup@1009 : Record 5603;
      FAJnlLine@1010 : Record 5621;
      FAJnlLineTmp@1011 : TEMPORARY Record 5621;
      DeprBook@1012 : Record 5611;
      FAJnlSetup@1013 : Record 5605;
      CalculateDepr@1014 : Codeunit 5610;
      FAInsertGLAcc@1015 : Codeunit 5601;
      Window@1016 : Dialog;
      DeprAmount@1017 : Decimal;
      Custom1Amount@1018 : Decimal;
      NumberOfDays@1019 : Integer;
      Custom1NumberOfDays@1020 : Integer;
      DeprUntilDate@1021 : Date;
      UseForceNoOfDays@1022 : Boolean;
      DaysInPeriod@1023 : Integer;
      PostingDate@1024 : Date;
      DocumentNo@1025 : Code[20];
      DocumentNo2@1026 : Code[20];
      NoSeries@1027 : Code[10];
      PostingDescription@1028 : Text[50];
      DeprBookCode@1029 : Code[10];
      BalAccount@1030 : Boolean;
      ErrorNo@1031 : Integer;
      Custom1ErrorNo@1032 : Integer;
      FAJnlNextLineNo@1033 : Integer;
      GenJnlNextLineNo@1034 : Integer;
      EntryAmounts@1035 : ARRAY [4] OF Decimal;
      LineNo@1036 : Integer;
      DeprBook2@1120000 : Record 5611;
      FADeprBook@1120001 : Record 5612;
      DeprAmount2@1120002 : Decimal;
      HasDerogatorySetup@1120003 : Boolean;
      Text10800@1120004 : TextConst 'ENU=Depreciation cannot be posted on depreciation book %1 because it is set up as derogatory.;FRA=L''amortissement ne peut pas ˆtre enregistr‚ dans la loi d''amortissement %1 car il est d‚fini comme ‚tant d‚rogatoire.';
      Text10801@1120005 : TextConst 'ENU=Reversal of Derogatory Balance at %1;FRA=Inversion du solde d‚rogatoire au %1';

    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1007 : Code[10];DeprUntilDateFrom@1000 : Date;UseForceNoOfDaysFrom@1001 : Boolean;DaysInPeriodFrom@1002 : Integer;PostingDateFrom@1003 : Date;DocumentNoFrom@1004 : Code[20];PostingDescriptionFrom@1005 : Text[50];BalAccountFrom@1006 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      DeprUntilDate := DeprUntilDateFrom;
      UseForceNoOfDays := UseForceNoOfDaysFrom;
      DaysInPeriod := DaysInPeriodFrom;
      PostingDate := PostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    PROCEDURE CalcDerogDeprAmount@1120000(Amount1@1120000 : Decimal;Amount2@1120001 : Decimal) : Decimal;
    BEGIN
      EXIT(Amount1 - Amount2);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


OBJECT Page 1042 Copy Job Planning Lines
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Job Planning Lines;
               FRA=Copier les lignes planning projet];
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       IF CloseAction IN [ACTION::OK,ACTION::LookupOK] THEN BEGIN
                         ValidateUserInput;
                         CopyJob.SetCopyQuantity(CopyQuantity);
                         CopyJob.SetCopyJobPlanningLineType(PlanningLineType);
                         CopyJob.SetJobTaskDateRange(FromDate,ToDate);
                         CopyJob.CopyJobPlanningLines(SourceJobTask,TargetJobTask);
                         MESSAGE(Text001);
                       END
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 15  ;1   ;Group     ;
                CaptionML=[ENU=Copy from;
                           FRA=Copier de] }

    { 11  ;2   ;Field     ;
                CaptionML=[ENU=Job No.;
                           FRA=Nø projet];
                SourceExpr=SourceJobNo;
                TableRelation=Job;
                OnValidate=VAR
                             SourceJob@1001 : Record 167;
                           BEGIN
                             IF (SourceJobNo <> '') AND NOT SourceJob.GET(SourceJobNo) THEN
                               ERROR(Text003,SourceJob.TABLECAPTION,SourceJobNo);

                             SourceJobTaskNo := '';
                           END;
                            }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Job Task No.;
                           FRA=Nø tƒche projet];
                SourceExpr=SourceJobTaskNo;
                OnValidate=VAR
                             JobTask@1000 : Record 1001;
                           BEGIN
                             IF (SourceJobTaskNo <> '') AND NOT JobTask.GET(SourceJobNo,SourceJobTaskNo) THEN
                               ERROR(Text003,JobTask.TABLECAPTION,SourceJobTaskNo);
                           END;

                OnLookup=VAR
                           JobTask@1000 : Record 1001;
                         BEGIN
                           IF SourceJobNo <> '' THEN BEGIN
                             JobTask.SETRANGE("Job No.",SourceJobNo);
                             IF PAGE.RUNMODAL(PAGE::"Job Task List",JobTask) = ACTION::LookupOK THEN
                               SourceJobTaskNo := JobTask."Job Task No.";
                           END;
                         END;
                          }

    { 2   ;2   ;Field     ;
                Name=Planning Line Type;
                CaptionML=[ENU=Incl. Planning Line Type;
                           FRA=Type lignes planning … inclure];
                OptionCaptionML=[ENU=Schedule+Contract,Schedule,Contract;
                                 FRA=Planifi‚+Contrat,Planifi‚,Contrat];
                SourceExpr=PlanningLineType }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Starting Date;
                           FRA=Date d‚but];
                SourceExpr=FromDate }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Ending Date;
                           FRA=Date fin];
                SourceExpr=ToDate }

    { 5   ;1   ;Group     ;
                CaptionML=[ENU=Copy to;
                           FRA=Copier vers] }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Job No.;
                           FRA=Nø projet];
                SourceExpr=TargetJobNo;
                TableRelation=Job;
                OnValidate=VAR
                             TargetJob@1001 : Record 167;
                           BEGIN
                             IF (TargetJobNo <> '') AND NOT TargetJob.GET(TargetJobNo) THEN
                               ERROR(Text003,TargetJob.TABLECAPTION,TargetJobNo);

                             TargetJobTaskNo := '';
                           END;
                            }

    { 1   ;2   ;Field     ;
                CaptionML=[ENU=Job Task No.;
                           FRA=Nø tƒche projet];
                SourceExpr=TargetJobTaskNo;
                OnValidate=VAR
                             JobTask@1000 : Record 1001;
                           BEGIN
                             IF (TargetJobTaskNo <> '') AND NOT JobTask.GET(TargetJobNo,TargetJobTaskNo) THEN
                               ERROR(Text003,JobTask.TABLECAPTION,TargetJobTaskNo);
                           END;

                OnLookup=VAR
                           JobTask@1000 : Record 1001;
                         BEGIN
                           IF TargetJobNo <> '' THEN BEGIN
                             JobTask.SETRANGE("Job No.",TargetJobNo);
                             IF PAGE.RUNMODAL(PAGE::"Job Task List",JobTask) = ACTION::LookupOK THEN
                               TargetJobTaskNo := JobTask."Job Task No.";
                           END;
                         END;
                          }

    { 6   ;1   ;Group     ;
                CaptionML=[ENU=Apply;
                           FRA=Appliquer] }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Copy Quantity;
                           FRA=Copier quantit‚];
                SourceExpr=CopyQuantity }

  }
  CODE
  {
    VAR
      SourceJobTask@1003 : Record 1001;
      TargetJobTask@1000 : Record 1001;
      CopyJob@1012 : Codeunit 1006;
      SourceJobNo@1009 : Code[20];
      SourceJobTaskNo@1004 : Code[20];
      TargetJobNo@1008 : Code[20];
      TargetJobTaskNo@1001 : Code[20];
      Text001@1016 : TextConst 'ENU=The job was successfully copied.;FRA=Le projet a ‚t‚ copi‚ avec succŠs.';
      Text003@1018 : TextConst '@@@=Job Task 1000 does not exist.;ENU=%1 %2 does not exist.;FRA=%1 %2 n''existe pas.';
      PlanningLineType@1005 : 'Schedule+Contract,Schedule,Contract';
      FromDate@1007 : Date;
      ToDate@1006 : Date;
      CopyQuantity@1011 : Boolean;
      Text004@1015 : TextConst 'ENU=Provide a valid source %1.;FRA=Fournissez une source %1 valide.';
      Text005@1002 : TextConst 'ENU=Provide a valid target %1.;FRA=Fournissez un %1 cible valide.';

    LOCAL PROCEDURE ValidateUserInput@1();
    VAR
      Job@1000 : Record 167;
    BEGIN
      IF SourceJobNo = '' THEN
        ERROR(Text004,Job.TABLECAPTION);
      IF (SourceJobTaskNo = '') OR NOT SourceJobTask.GET(SourceJobNo,SourceJobTaskNo) THEN
        ERROR(Text004,SourceJobTask.TABLECAPTION);

      IF TargetJobNo = '' THEN
        ERROR(Text005,Job.TABLECAPTION);
      IF (TargetJobTaskNo = '') OR NOT TargetJobTask.GET(TargetJobNo,TargetJobTaskNo) THEN
        ERROR(Text005,TargetJobTask.TABLECAPTION);
    END;

    PROCEDURE SetFromJobTask@3(SourceJobTask2@1000 : Record 1001);
    BEGIN
      SourceJobNo := SourceJobTask2."Job No.";
      SourceJobTask := SourceJobTask2;
      SourceJobTaskNo := SourceJobTask2."Job Task No.";
    END;

    PROCEDURE SetToJobTask@4(TargetJobTask2@1000 : Record 1001);
    BEGIN
      TargetJobNo := TargetJobTask2."Job No.";
      TargetJobTask := TargetJobTask2;
      TargetJobTaskNo := TargetJobTask2."Job Task No.";
    END;

    BEGIN
    END.
  }
}


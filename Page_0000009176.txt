OBJECT Page 9176 My Settings
{
  OBJECT-PROPERTIES
  {
    Date=23/10/15;
    Time=12:00:00;
    Version List=NAVW19.00.00.43402;
  }
  PROPERTIES
  {
    CaptionML=[ENU=My Settings;
               FRA=Mes ParamŠtres];
    PageType=StandardDialog;
    OnOpenPage=VAR
                 UserPersonalization@1000 : Record 2000000073;
               BEGIN
                 WITH UserPersonalization DO BEGIN
                   GET(USERSECURITYID);
                   LanguageID := "Language ID";
                   LocaleID := "Locale ID";
                   TimeZoneID := "Time Zone";
                   VarCompany := Company;
                   NewWorkdate := WORKDATE;
                 END;
               END;

    OnQueryClosePage=VAR
                       UserPersonalization@1002 : Record 2000000073;
                     BEGIN
                       IF CloseAction = ACTION::OK THEN
                         WITH UserPersonalization DO BEGIN
                           GET(USERSECURITYID);

                           IF ("Language ID" <> LanguageID) OR
                              ("Locale ID" <> LocaleID) OR
                              ("Time Zone" <> TimeZoneID) OR
                              (Company <> VarCompany)
                           THEN BEGIN
                             VALIDATE("Language ID",LanguageID);
                             VALIDATE("Locale ID",LocaleID);
                             VALIDATE("Time Zone",TimeZoneID);
                             VALIDATE(Company,VarCompany);
                             MODIFY;
                             MESSAGE(ReSignInMsg);
                           END;
                         END;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           FRA=G‚n‚ral];
                GroupType=Group }

    { 3   ;2   ;Field     ;
                Name=Language;
                CaptionML=[ENU=Language;
                           FRA=Langue];
                ToolTipML=[ENU=Specifies the display language on all devices. Changing the language will take effect once you sign out and sign in again.;
                           FRA=Sp‚cifie la langue d'affichage sur tous les p‚riph‚riques. Pour qu'un changement de langue prenne effet, vous devez vous d‚connecter et vous reconnecter.];
                SourceExpr=GetLanguage;
                Importance=Promoted;
                Editable=FALSE;
                OnAssistEdit=VAR
                               LanguageManagement@1000 : Codeunit 43;
                             BEGIN
                               LanguageManagement.LookupApplicationLanguage(LanguageID);
                             END;
                              }

    { 4   ;2   ;Field     ;
                Name=Locale;
                CaptionML=[ENU=Region;
                           FRA=R‚gion];
                ToolTipML=[ENU=Specifies the regional settings, such as date and numeric format, on all devices. Changing the regional settings will take effect once you sign out and sign in again.;
                           FRA=Sp‚cifie les paramŠtres r‚gionaux, comme la date et le format num‚rique, sur tous les p‚riph‚riques. Pour qu'un changement de paramŠtres r‚gionaux prenne effet, vous devez vous d‚connecter et vous reconnecter.];
                SourceExpr=GetLocale;
                OnAssistEdit=VAR
                               LanguageManagement@1000 : Codeunit 43;
                             BEGIN
                               LanguageManagement.LookupWindowsLocale(LocaleID);
                             END;
                              }

    { 5   ;2   ;Field     ;
                Name=TimeZone;
                CaptionML=[ENU=Time Zone;
                           FRA=Fuseau horaire];
                ToolTipML=[ENU=Select the time zone setting that controls time and date on all devices. Changing the time zone will take effect once you sign out and sign in again.;
                           FRA=S‚lectionnez le paramŠtre Fuseau horaire qui contr“le la date et l'heure sur tous les p‚riph‚riques. Pour qu'un changement de fuseau horaire prenne effet, vous devez vous d‚connecter et vous reconnecter.];
                SourceExpr=GetTimeZone;
                OnAssistEdit=VAR
                               TimeZone@1000 : Record 2000000164;
                             BEGIN
                               TimeZone.SETCURRENTKEY("Display Name");
                               TimeZone.ASCENDING(TRUE);

                               IF PAGE.RUNMODAL(PAGE::"Time Zones",TimeZone) = ACTION::LookupOK THEN
                                 TimeZoneID := TimeZone.ID;
                             END;
                              }

    { 6   ;2   ;Field     ;
                Name=Company;
                CaptionML=[ENU=Company;
                           FRA=Soci‚t‚];
                ToolTipML=[ENU=Select the company to work in on all devices. Changing the company will take effect once you sign out and sign in again.;
                           FRA=S‚lectionnez la soci‚t‚ dans laquelle travailler sur tous les p‚riph‚riques. Pour qu'un changement de soci‚t‚ prenne effet, vous devez vous d‚connecter et vous reconnecter.];
                SourceExpr=VarCompany;
                Editable=false;
                OnAssistEdit=VAR
                               SelectedCompany@1001 : Record 2000000006;
                               AllowedCompanies@1000 : Page 9177;
                             BEGIN
                               AllowedCompanies.Initialize;

                               IF SelectedCompany.GET(COMPANYNAME) THEN
                                 AllowedCompanies.SETRECORD(SelectedCompany);

                               AllowedCompanies.LOOKUPMODE(TRUE);

                               IF AllowedCompanies.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 AllowedCompanies.GETRECORD(SelectedCompany);
                                 VarCompany := SelectedCompany.Name;
                               END;
                             END;
                              }

    { 7   ;2   ;Field     ;
                Name=NewWorkdate;
                CaptionML=[ENU=Work Date;
                           FRA=Date de travail];
                ToolTipML=[ENU=Select the work date for transactions on all devices. This change only affects the date on new transactions you create.;
                           FRA=S‚lectionnez la date de travail des transactions sur tous les p‚riph‚riques. Ce changement n'affecte que la date des nouvelles transactions que vous cr‚ez.];
                SourceExpr=NewWorkdate;
                OnValidate=BEGIN
                             WORKDATE := NewWorkdate;
                           END;
                            }

  }
  CODE
  {
    VAR
      LanguageID@1001 : Integer;
      ReSignInMsg@1004 : TextConst '@@@="""sign out"" and ""sign in"" are the same terms as shown in the Dynamics NAV client.";ENU=You must sign out and then sign in again to have the changes take effect.;FRA=Vous devez vous d‚connecter et vous reconnecter pour que les changements prennent effet.';
      LocaleID@1000 : Integer;
      TimeZoneID@1002 : Text[180];
      VarCompany@1003 : Text;
      NewWorkdate@1005 : Date;

    LOCAL PROCEDURE GetLanguage@20() : Text;
    BEGIN
      EXIT(GetWindowsLanguageNameFromID(LanguageID));
    END;

    LOCAL PROCEDURE GetWindowsLanguageNameFromID@12(ID@1000 : Integer) : Text;
    VAR
      WindowsLanguage@1001 : Record 2000000045;
    BEGIN
      IF WindowsLanguage.GET(ID) THEN
        EXIT(WindowsLanguage.Name);
    END;

    LOCAL PROCEDURE GetLocale@1() : Text;
    BEGIN
      EXIT(GetWindowsLanguageNameFromID(LocaleID));
    END;

    LOCAL PROCEDURE GetTimeZone@2() : Text;
    VAR
      TimeZone@1000 : Record 2000000164;
    BEGIN
      IF TimeZone.GET(TimeZoneID) THEN
        EXIT(TimeZone."Display Name");
    END;

    BEGIN
    {
      Contains various system-wide settings which are personal to an individual user.
      Styled as a StandardDialog which is ideal for presenting a single field. Once more fields are added,
      this page should be converted to a Card page.
    }
    END.
  }
}


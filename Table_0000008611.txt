OBJECT Table 8611 Config. Question Area
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ConfigQuestion@1000 : Record 8612;
             BEGIN
               ConfigQuestion.RESET;
               ConfigQuestion.SETRANGE("Questionnaire Code","Questionnaire Code");
               ConfigQuestion.SETRANGE("Question Area Code",Code);
               ConfigQuestion.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text003);
             END;

    CaptionML=[ENU=Config. Question Area;
               FRA=Zone question config.];
    LookupPageID=Page8613;
  }
  FIELDS
  {
    { 1   ;   ;Questionnaire Code  ;Code10        ;TableRelation="Config. Questionnaire";
                                                   CaptionML=[ENU=Questionnaire Code;
                                                              FRA=Code questionnaire];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Code                ;Code10        ;OnValidate=VAR
                                                                TestValue@1000 : Integer;
                                                              BEGIN
                                                                IF EVALUATE(TestValue,COPYSTR(Code,1,1)) THEN
                                                                  ERROR(Text002);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=FILTER(Table));
                                                   OnValidate=VAR
                                                                ConfigQuestion@1000 : Record 8612;
                                                                ConfigQuestionArea@1001 : Record 8611;
                                                              BEGIN
                                                                IF (xRec."Table ID" <> "Table ID") AND (xRec."Table ID" > 0) THEN BEGIN
                                                                  ConfigQuestion.SETRANGE("Questionnaire Code","Questionnaire Code");
                                                                  ConfigQuestion.SETRANGE("Question Area Code",Code);
                                                                  IF NOT ConfigQuestion.ISEMPTY THEN
                                                                    ERROR(STRSUBSTNO(Text000,Code));
                                                                  ConfigQuestionArea.SETRANGE("Questionnaire Code","Questionnaire Code");
                                                                  ConfigQuestionArea.SETRANGE("Table ID","Table ID");
                                                                  IF NOT ConfigQuestionArea.ISEMPTY THEN
                                                                    ERROR(STRSUBSTNO(Text001,"Table ID"));
                                                                END;
                                                                CALCFIELDS("Table Name","Table Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigValidateMgt@1000 : Codeunit 8617;
                                                            BEGIN
                                                              ConfigValidateMgt.LookupTable("Table ID");
                                                              VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              FRA=ID table] }
    { 5   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              FRA=Nom table];
                                                   Editable=No }
    { 6   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              FRA=L‚gende table] }
    { 7   ;   ;No. of Questions    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Question" WHERE (Questionnaire Code=FIELD(Questionnaire Code),
                                                                                               Question Area Code=FIELD(Code)));
                                                   CaptionML=[ENU=No. of Questions;
                                                              FRA=Nombre questions];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Questionnaire Code,Code                 ;Clustered=Yes }
    {    ;Table ID                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1003 : TextConst 'ENU=Delete questions for question area %1 to change the table relationship.;FRA=Supprimez les questions pour la zone question %1 afin de modifier la relation de table.';
      Text001@1000 : TextConst 'ENU=A question area already exists for table %1.;FRA=Il existe d‚j… une zone question pour la table %1.';
      Text002@1001 : TextConst 'ENU=The first character cannot be a numeric value.;FRA=Le premier caractŠre ne peut pas ˆtre un nombre.';
      Text003@1002 : TextConst 'ENU=You cannot rename a question area.;FRA=Vous ne pouvez pas renommer une zone de question.';

    BEGIN
    END.
  }
}


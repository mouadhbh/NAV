OBJECT Codeunit 1222 SEPA CT-Prepare Source
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00,NAVFR8.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=VAR
            GenJnlLine@1000 : Record 81;
          BEGIN
            GenJnlLine.COPYFILTERS(Rec);
            CopyJnlLines(GenJnlLine,Rec);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE CopyJnlLines@4(VAR FromGenJnlLine@1000 : Record 81;VAR TempGenJnlLine@1001 : TEMPORARY Record 81);
    VAR
      GenJnlBatch@1003 : Record 232;
    BEGIN
      IF FromGenJnlLine.FINDSET THEN BEGIN
        GenJnlBatch.GET(FromGenJnlLine."Journal Template Name",FromGenJnlLine."Journal Batch Name");

        REPEAT
          TempGenJnlLine := FromGenJnlLine;
          TempGenJnlLine.INSERT;
        UNTIL FromGenJnlLine.NEXT = 0
      END ELSE
        CreateTempJnlLines(FromGenJnlLine,TempGenJnlLine);
    END;

    LOCAL PROCEDURE CreateTempJnlLines@5(VAR FromGenJnlLine@1000 : Record 81;VAR TempGenJnlLine@1001 : TEMPORARY Record 81);
    VAR
      PaymentHeader@1120003 : Record 10865;
      PaymentLine@1120004 : Record 10866;
      PaymentDocNo@1120002 : Code[20];
      AppliedDocNoList@1120005 : Text;
      DescriptionLen@1120006 : Integer;
    BEGIN
      PaymentDocNo := FromGenJnlLine.GETFILTER("Document No.");
      PaymentHeader.GET(PaymentDocNo);
      PaymentLine.RESET;
      PaymentLine.SETRANGE("No.",PaymentHeader."No.");
      IF PaymentLine.FINDSET THEN
        REPEAT
          TempGenJnlLine.INIT;
          TempGenJnlLine."Journal Template Name" := '';
          TempGenJnlLine."Journal Batch Name" := FORMAT(DATABASE::"Payment Header");
          TempGenJnlLine."Document No." := PaymentHeader."No.";
          TempGenJnlLine."Line No." := PaymentLine."Line No.";
          TempGenJnlLine."Account No." := PaymentLine."Account No.";
          TempGenJnlLine."Account Type" := PaymentLine."Account Type";
          CASE PaymentLine."Account Type" OF
            PaymentLine."Account Type"::Vendor:
              TempGenJnlLine."Document Type" := TempGenJnlLine."Document Type"::Payment;
            PaymentLine."Account Type"::Customer:
              TempGenJnlLine."Document Type" := TempGenJnlLine."Document Type"::Refund;
          END;
          TempGenJnlLine.Amount := PaymentLine.Amount;
          TempGenJnlLine."Applies-to Doc. Type" := PaymentLine."Applies-to Doc. Type";
          TempGenJnlLine."Applies-to Doc. No." := PaymentLine."Applies-to Doc. No.";
          TempGenJnlLine."Applies-to ID" := PaymentLine."Applies-to ID";
          TempGenJnlLine."Bal. Account Type" := PaymentHeader."Account Type";
          TempGenJnlLine."Bal. Account No." := PaymentHeader."Account No.";
          TempGenJnlLine."Currency Code" := PaymentLine."Currency Code";
          TempGenJnlLine."Posting Date" := PaymentLine."Posting Date";
          TempGenJnlLine."Recipient Bank Account" := PaymentLine."Bank Account Code";

          DescriptionLen := MAXSTRLEN(TempGenJnlLine.Description);
          AppliedDocNoList := PaymentLine.GetAppliedDocNoList(DescriptionLen);
          TempGenJnlLine.Description := COPYSTR(AppliedDocNoList,1,DescriptionLen);
          IF STRLEN(AppliedDocNoList) > DescriptionLen THEN
            TempGenJnlLine."Message to Recipient" :=
              COPYSTR(AppliedDocNoList,DescriptionLen + 1,DescriptionLen + MAXSTRLEN(TempGenJnlLine."Message to Recipient"));
          TempGenJnlLine.INSERT;
        UNTIL PaymentLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}


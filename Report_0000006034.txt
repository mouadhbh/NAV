OBJECT Report 6034 Remove Lines from Contract
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Remove Lines from Contract;
               FRA=Supprimer lignes contrat];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   DelToDate := WORKDATE;
                   ServMgtSetup.GET;
                 END;

    OnPreReport=BEGIN
                  CreateCreditfromContractLines.InitVariables;
                END;

    OnPostReport=BEGIN
                   IF DeleteLines = DeleteLines::"Delete Lines" THEN
                     IF LinesRemoved > 1 THEN
                       MESSAGE(Text000,LinesRemoved)
                     ELSE
                       MESSAGE(Text001,LinesRemoved);

                   IF CreditMemoCreated = 1 THEN
                     MESSAGE(Text006);

                   IF CreditMemoCreated > 1 THEN
                     MESSAGE(Text007);
                   CreateCreditfromContractLines.InitVariables;
                 END;

  }
  DATASET
  {
    { 6062;    ;DataItem;                    ;
               DataItemTable=Table5964;
               DataItemTableView=SORTING(Contract Type,Contract No.,Credited,New Line)
                                 WHERE(Contract Type=CONST(Contract),
                                       Contract Status=CONST(Signed),
                                       New Line=CONST(No));
               OnPreDataItem=BEGIN
                               IF DeleteLines = DeleteLines::"Print Only" THEN BEGIN
                                 CLEAR(ExpiredContractLinesTest);
                                 ExpiredContractLinesTest.InitVariables(DelToDate,ReasonCode);
                                 ExpiredContractLinesTest.SETTABLEVIEW("Service Contract Line");
                                 ExpiredContractLinesTest.RUNMODAL;
                                 CurrReport.BREAK;
                               END;

                               IF DelToDate = 0D THEN
                                 ERROR(Text002);
                               ServMgtSetup.GET;
                               IF ServMgtSetup."Use Contract Cancel Reason" THEN
                                 IF ReasonCode = '' THEN
                                   ERROR(Text003);
                               SETFILTER("Contract Expiration Date",'<>%1&<=%2',0D,DelToDate);

                               Window.OPEN(
                                 Text004 +
                                 '@1@@@@@@@@@@@@@@@@@@@@@@@@@@@');
                               i := COUNT;
                               j := 0;
                               LinesRemoved := 0;
                               LastContractNo := '';
                               CreditMemoCreated := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  j := j + 1;
                                  Window.UPDATE(1,ROUND(j / i * 10000,1));

                                  IF LastContractNo <> "Contract No." THEN BEGIN
                                    LastContractNo := "Contract No.";
                                    ServContract.GET("Contract Type","Contract No.");
                                    FiledServContract.FileContract(ServContract);
                                    IF ServContract."Automatic Credit Memos" AND
                                       ("Credit Memo Date" > 0D) AND
                                       CreditMemoBaseExists
                                    THEN
                                      CreditMemoCreated := CreditMemoCreated + 1;
                                  END;
                                  SuspendStatusCheck(TRUE);
                                  DELETE(TRUE);

                                  LinesRemoved := LinesRemoved + 1;
                                END;

               ReqFilterFields=Contract No.,Service Item No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Remove Lines to;
                             FRA=Retrait des lignes dat‚es du];
                  SourceExpr=DelToDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Reason Code;
                             FRA=Code motif];
                  SourceExpr=ReasonCode;
                  OnValidate=BEGIN
                               ReasonCode2.GET(ReasonCode);
                             END;

                  OnLookup=BEGIN
                             ReasonCode2.RESET;
                             ReasonCode2.Code := ReasonCode;
                             IF PAGE.RUNMODAL(0,ReasonCode2) = ACTION::LookupOK THEN BEGIN
                               ReasonCode2.GET(ReasonCode2.Code);
                               ReasonCode := ReasonCode2.Code;
                             END;
                           END;
                            }

      { 7   ;2   ;Field     ;
                  Name=Reason Code;
                  CaptionML=[ENU=Reason Code Description;
                             FRA=Description code motif];
                  SourceExpr=ReasonCode2.Description;
                  Editable=FALSE }

      { 10  ;2   ;Field     ;
                  CaptionML=[ENU=Action;
                             FRA=Action];
                  OptionCaptionML=[ENU=Delete Lines,Print Only;
                                   FRA=Supprimer lignes,Imprimer seulement];
                  SourceExpr=DeleteLines }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 contract lines were removed.;FRA=%1 lignes contrat supprim‚es.';
      Text001@1001 : TextConst 'ENU=%1 contract line was removed.;FRA=%1 ligne contrat supprim‚e.';
      Text002@1002 : TextConst 'ENU=You must fill in the Remove Lines to field.;FRA=Vous devez renseigner le champ Retrait des lignes dat‚es du.';
      Text003@1003 : TextConst 'ENU=You must fill in the Reason Code field.;FRA=Vous devez renseigner le champ Code motif.';
      Text004@1004 : TextConst 'ENU=Removing contract lines... \\;FRA=Retrait des lignes contrat...\\';
      ServMgtSetup@1006 : Record 5911;
      ServContract@1007 : Record 5965;
      FiledServContract@1008 : Record 5970;
      ReasonCode2@1009 : Record 231;
      ExpiredContractLinesTest@1022 : Report 5987;
      CreateCreditfromContractLines@1010 : Codeunit 5945;
      Window@1012 : Dialog;
      i@1013 : Integer;
      j@1014 : Integer;
      LinesRemoved@1015 : Integer;
      DelToDate@1017 : Date;
      DeleteLines@1018 : 'Delete Lines,Print Only';
      ReasonCode@1019 : Code[10];
      LastContractNo@1020 : Code[20];
      Text006@1023 : TextConst 'ENU=A credit memo was created/updated.;FRA=L''avoir a ‚t‚ cr‚‚/mis … jour.';
      CreditMemoCreated@1011 : Integer;
      Text007@1016 : TextConst 'ENU=Credit memos were created/updated.;FRA=Les avoirs ont ‚t‚ cr‚‚s/mis … jour.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}


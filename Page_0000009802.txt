OBJECT Page 9802 Permission Sets
{
  OBJECT-PROPERTIES
  {
    Date=23/10/15;
    Time=12:00:00;
    Version List=NAVW19.00.00.43402;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Permission Sets;
               FRA=Ensembles d'autorisations];
    SourceTable=Table2000000167;
    PageType=List;
    OnAfterGetRecord=BEGIN
                       PermissionEditable := ISNULLGUID("App ID");
                       RoleIDEditable := FALSE;
                     END;

    OnNewRecord=BEGIN
                  // Allow edits only on a new record - underlying tables do not allow renames.
                  RoleIDEditable := TRUE;
                END;

    OnInsertRecord=VAR
                     PermissionSet@1000 : Record 2000000004;
                   BEGIN
                     PermissionSet.INIT;
                     PermissionSet."Role ID" := "Role ID";
                     PermissionSet.Name := Name;
                     PermissionSet.INSERT;
                     PermissionEditable := ISNULLGUID("App ID");
                     RoleIDEditable := FALSE;
                     EXIT(FALSE); // Causes UI to stop processing the action - we handled it manually
                   END;

    OnModifyRecord=VAR
                     PermissionSet@1000 : Record 2000000004;
                   BEGIN
                     IF NOT ISNULLGUID("App ID") THEN
                       ERROR(AlterAppPermSetErr);

                     PermissionSet.GET(xRec."Role ID");
                     IF xRec."Role ID" <> "Role ID" THEN BEGIN
                       PermissionSet.RENAME(xRec."Role ID","Role ID");
                       PermissionSet.GET("Role ID");
                     END;
                     PermissionSet.Name := Name;
                     PermissionSet.MODIFY;
                     PermissionEditable := ISNULLGUID("App ID");
                     EXIT(FALSE); // Causes UI to stop processing the action - we handled it manually
                   END;

    OnDeleteRecord=VAR
                     PermissionSet@1000 : Record 2000000004;
                   BEGIN
                     IF NOT ISNULLGUID("App ID") THEN
                       ERROR(AlterAppPermSetErr);

                     PermissionSet.GET("Role ID");
                     PermissionSet.DELETE;
                     PermissionEditable := ISNULLGUID("App ID");
                     EXIT(FALSE); // Causes UI to stop processing the action - we handled it manually
                   END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      Name=PermissionActions;
                      CaptionML=[ENU=Permissions;
                                 FRA=Autorisations];
                      ActionContainerType=RelatedInformation }
      { 7       ;1   ;ActionGroup;
                      Name=ShowPermissions;
                      CaptionML=[ENU=Permissions;
                                 FRA=Autorisations];
                      Image=Permission }
      { 6       ;2   ;Action    ;
                      Name=Permissions;
                      ShortCutKey=Shift+Ctrl+p;
                      CaptionML=[ENU=Permissions;
                                 FRA=Autorisations];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Permission;
                      PromotedCategory=Process;
                      Scope=Repeater;
                      OnAction=VAR
                                 Permission@1001 : Record 2000000005;
                                 Permissions@1000 : Page 9803;
                               BEGIN
                                 Permission.SETRANGE("Role ID","Role ID");
                                 Permissions.SETRECORD(Permission);
                                 Permissions.SETTABLEVIEW(Permission);
                                 Permissions.EDITABLE := PermissionEditable;
                                 Permissions.RUN;
                               END;
                                }
      { 11      ;2   ;Action    ;
                      CaptionML=[ENU=Permission Set by User;
                                 FRA=Ensemble d'autorisations par utilisateur];
                      RunObject=Page 9816;
                      Promoted=Yes;
                      Image=Permission;
                      PromotedCategory=Process }
      { 12      ;2   ;Action    ;
                      CaptionML=[ENU=Permission Set by User Group;
                                 FRA=Ensemble d'autorisations par groupe d'utilisateurs];
                      RunObject=Page 9837;
                      Promoted=Yes;
                      Image=Permission;
                      PromotedCategory=Process }
      { 15      ;1   ;ActionGroup;
                      CaptionML=[ENU=User Groups;
                                 FRA=Groupes d'utilisateurs] }
      { 14      ;2   ;Action    ;
                      CaptionML=[ENU=User by User Group;
                                 FRA=Utilisateur par groupe d'utilisateurs];
                      RunObject=Page 9838;
                      Promoted=Yes;
                      Image=User;
                      PromotedCategory=Process }
      { 13      ;2   ;Action    ;
                      Name=UserGroups;
                      CaptionML=[ENU=User Groups;
                                 FRA=Groupes d'utilisateurs];
                      RunObject=Page 9830;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Users;
                      PromotedCategory=Process }
      { 9       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 5       ;1   ;ActionGroup;
                      Name=<Functions>;
                      CaptionML=[ENU=F&unctions;
                                 FRA=Fonction&s];
                      Image=Action }
      { 8       ;2   ;Action    ;
                      Name=<CopyPermissionSet>;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Copy Permission Set;
                                 FRA=Copier ensemble d'autorisations];
                      Enabled=PermissionEditable;
                      Image=Copy;
                      OnAction=VAR
                                 PermissionSet@1035 : Record 2000000004;
                                 CopyPermissionSet@1034 : Report 9802;
                                 NullGUID@1000 : GUID;
                               BEGIN
                                 PermissionSet.SETRANGE("Role ID","Role ID");
                                 CopyPermissionSet.SETTABLEVIEW(PermissionSet);
                                 CopyPermissionSet.RUNMODAL;

                                 IF GET(Scope::System,NullGUID,CopyPermissionSet.GetRoleId) THEN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                CaptionML=[ENU=Permission Set;
                           FRA=Ensemble d'autorisations];
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=PermissionSet;
                CaptionML=[ENU=Permission Set;
                           FRA=Ensemble d'autorisations];
                SourceExpr="Role ID";
                Editable=RoleIDEditable }

    { 4   ;2   ;Field     ;
                Name=<Name>;
                CaptionML=[ENU=Name;
                           FRA=Nom];
                SourceExpr=Name }

    { 21  ;2   ;Field     ;
                CaptionML=[ENU=NAV App Name;
                           FRA=Nom appli NAV];
                SourceExpr="App Name";
                Editable=FALSE }

    { 16  ;    ;Container ;
                ContainerType=FactBoxArea }

    { 17  ;1   ;Part      ;
                CaptionML=[ENU=Permissions;
                           FRA=Autorisations];
                SubPageLink=Role ID=FIELD(Role ID);
                PagePartID=Page9804;
                PartType=Page }

    { 19  ;1   ;Part      ;
                CaptionML=[ENU=Tenant Permissions;
                           FRA=Autorisations abonn‚];
                SubPageLink=Role ID=FIELD(Role ID),
                            App ID=FIELD(App ID);
                PagePartID=Page9840;
                Editable=FALSE;
                PartType=Page }

  }
  CODE
  {
    VAR
      AlterAppPermSetErr@1001 : TextConst 'ENU=You cannot modify application-level permission sets.;FRA=Vous ne pouvez pas modifier les ensembles d''autorisations de niveau application.';
      PermissionEditable@1000 : Boolean;
      RoleIDEditable@1004 : Boolean;

    BEGIN
    END.
  }
}


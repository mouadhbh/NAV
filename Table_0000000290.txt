OBJECT Table 290 VAT Amount Line
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Amount Line;
               FRA=Ligne montant TVA];
  }
  FIELDS
  {
    { 1   ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              FRA=% TVA];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 2   ;   ;VAT Base            ;Decimal       ;CaptionML=[ENU=VAT Base;
                                                              FRA=Base TVA];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 3   ;   ;VAT Amount          ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("VAT %");
                                                                TESTFIELD("VAT Base");
                                                                IF "VAT Amount" / "VAT Base" < 0 THEN
                                                                  ERROR(Text002,FIELDCAPTION("VAT Amount"));
                                                                "VAT Difference" := "VAT Amount" - "Calculated VAT Amount";
                                                              END;

                                                   CaptionML=[ENU=VAT Amount;
                                                              FRA=Montant TVA];
                                                   AutoFormatType=1 }
    { 4   ;   ;Amount Including VAT;Decimal       ;CaptionML=[ENU=Amount Including VAT;
                                                              FRA=Montant TTC];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5   ;   ;VAT Identifier      ;Code10        ;CaptionML=[ENU=VAT Identifier;
                                                              FRA=Identifiant TVA];
                                                   Editable=No }
    { 6   ;   ;Line Amount         ;Decimal       ;CaptionML=[ENU=Line Amount;
                                                              FRA=Montant ligne];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 7   ;   ;Inv. Disc. Base Amount;Decimal     ;CaptionML=[ENU=Inv. Disc. Base Amount;
                                                              FRA=Montant base remise facture];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 8   ;   ;Invoice Discount Amount;Decimal    ;OnValidate=BEGIN
                                                                TESTFIELD("Inv. Disc. Base Amount");
                                                                IF "Invoice Discount Amount" / "Inv. Disc. Base Amount" > 1 THEN
                                                                  ERROR(
                                                                    InvoiceDiscAmtIsGreaterThanBaseAmtErr,
                                                                    FIELDCAPTION("Invoice Discount Amount"),"Inv. Disc. Base Amount");
                                                                "VAT Base" := "Line Amount" - "Invoice Discount Amount";
                                                              END;

                                                   CaptionML=[ENU=Invoice Discount Amount;
                                                              FRA=Montant remise facture];
                                                   AutoFormatType=1 }
    { 9   ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              FRA=Mode calcul TVA];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    FRA=Normal,Intracomm.,Correctif,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 10  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              FRA=Code groupe taxes];
                                                   Editable=No }
    { 11  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              FRA=Quantit‚];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 12  ;   ;Modified            ;Boolean       ;CaptionML=[ENU=Modified;
                                                              FRA=Modifi‚] }
    { 13  ;   ;Use Tax             ;Boolean       ;CaptionML=[ENU=Use Tax;
                                                              FRA=Use Tax] }
    { 14  ;   ;Calculated VAT Amount;Decimal      ;CaptionML=[ENU=Calculated VAT Amount;
                                                              FRA=Montant TVA calcul‚e];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 15  ;   ;VAT Difference      ;Decimal       ;CaptionML=[ENU=VAT Difference;
                                                              FRA=Diff‚rence TVA];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 16  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              FRA=Positif] }
    { 17  ;   ;Includes Prepayment ;Boolean       ;CaptionML=[ENU=Includes Prepayment;
                                                              FRA=Acompte inclus] }
    { 18  ;   ;VAT Clause Code     ;Code10        ;TableRelation="VAT Clause";
                                                   CaptionML=[ENU=VAT Clause Code;
                                                              FRA=Code clause TVA] }
    { 19  ;   ;Tax Category        ;Code10        ;CaptionML=[ENU=Tax Category;
                                                              FRA=Cat‚gorie de taxe] }
  }
  KEYS
  {
    {    ;VAT Identifier,VAT Calculation Type,Tax Group Code,Use Tax,Positive;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1% VAT;FRA=TVA %1%';
      Text001@1001 : TextConst 'ENU=VAT Amount;FRA=Montant TVA';
      Text002@1002 : TextConst 'ENU=%1 must not be negative.;FRA=%1 ne doit pas ˆtre n‚gatif.';
      InvoiceDiscAmtIsGreaterThanBaseAmtErr@1003 : TextConst '@@@=1 Invoice Discount Amount that should be set 2 Maximum Amount that you can assign;ENU=The maximum %1 that you can apply is %2.;FRA=Le %1 maximal que vous pouvez lettrer est %2.';
      Text004@1004 : TextConst 'ENU="%1 for %2 must not exceed %3 = %4.";FRA="La %1 du %2 ne doit pas d‚passer la %3 = %4."';
      Currency@1005 : Record 4;
      AllowVATDifference@1006 : Boolean;
      PricesIncludingVAT@1007 : Boolean;
      GlobalsInitialized@1008 : Boolean;
      Text005@1009 : TextConst 'ENU="%1 must not exceed %2 = %3.";FRA="%1 ne doit pas d‚passer %2 = %3."';

    PROCEDURE CheckVATDifference@11(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewPricesIncludingVAT@1002 : Boolean);
    VAR
      GLSetup@1003 : Record 98;
    BEGIN
      InitGlobals(NewCurrencyCode,NewAllowVATDifference,NewPricesIncludingVAT);
      IF NOT AllowVATDifference THEN
        TESTFIELD("VAT Difference",0);
      IF ABS("VAT Difference") > Currency."Max. VAT Difference Allowed" THEN
        IF NewCurrencyCode <> '' THEN
          ERROR(
            Text004,FIELDCAPTION("VAT Difference"),Currency.Code,
            Currency.FIELDCAPTION("Max. VAT Difference Allowed"),Currency."Max. VAT Difference Allowed")
        ELSE BEGIN
          IF GLSetup.GET THEN;
          IF ABS("VAT Difference") > GLSetup."Max. VAT Difference Allowed" THEN
            ERROR(
              Text005,FIELDCAPTION("VAT Difference"),
              GLSetup.FIELDCAPTION("Max. VAT Difference Allowed"),GLSetup."Max. VAT Difference Allowed");
        END;
    END;

    LOCAL PROCEDURE InitGlobals@4(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewPricesIncludingVAT@1002 : Boolean);
    BEGIN
      SetCurrency(NewCurrencyCode);
      AllowVATDifference := NewAllowVATDifference;
      PricesIncludingVAT := NewPricesIncludingVAT;
      GlobalsInitialized := TRUE;
    END;

    LOCAL PROCEDURE SetCurrency@15(CurrencyCode@1000 : Code[10]);
    BEGIN
      IF CurrencyCode = '' THEN
        Currency.InitRoundingPrecision
      ELSE
        Currency.GET(CurrencyCode);
    END;

    PROCEDURE InsertLine@1();
    VAR
      VATAmountLine@1000 : Record 290;
    BEGIN
      IF ("VAT Base" <> 0) OR ("Amount Including VAT" <> 0) THEN BEGIN
        Positive := "Line Amount" >= 0;
        VATAmountLine := Rec;
        IF FIND THEN BEGIN
          "Line Amount" := "Line Amount" + VATAmountLine."Line Amount";
          "Inv. Disc. Base Amount" := "Inv. Disc. Base Amount" + VATAmountLine."Inv. Disc. Base Amount";
          "Invoice Discount Amount" := "Invoice Discount Amount" + VATAmountLine."Invoice Discount Amount";
          Quantity := Quantity + VATAmountLine.Quantity;
          "VAT Base" := "VAT Base" + VATAmountLine."VAT Base";
          "Amount Including VAT" := "Amount Including VAT" + VATAmountLine."Amount Including VAT";
          "VAT Difference" := "VAT Difference" + VATAmountLine."VAT Difference";
          "VAT Amount" := "Amount Including VAT" - "VAT Base";
          "Calculated VAT Amount" := "Calculated VAT Amount" + VATAmountLine."Calculated VAT Amount";
          MODIFY;
        END ELSE BEGIN
          "VAT Amount" := "Amount Including VAT" - "VAT Base";
          INSERT;
        END;
      END;
    END;

    PROCEDURE GetLine@2(Number@1000 : Integer);
    BEGIN
      IF Number = 1 THEN
        FIND('-')
      ELSE
        NEXT;
    END;

    PROCEDURE VATAmountText@3() : Text[30];
    BEGIN
      IF COUNT = 1 THEN BEGIN
        FINDFIRST;
        IF "VAT %" <> 0 THEN
          EXIT(STRSUBSTNO(Text000,"VAT %"));
      END;
      EXIT(Text001);
    END;

    LOCAL PROCEDURE CopyFrom@8(VAR FromVATAmountLine@1000 : Record 290);
    BEGIN
      DELETEALL;
      IF FromVATAmountLine.FIND('-') THEN
        REPEAT
          Rec := FromVATAmountLine;
          INSERT;
        UNTIL FromVATAmountLine.NEXT = 0;
    END;

    PROCEDURE GetTotalLineAmount@10(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      LineAmount@1002 : Decimal;
    BEGIN
      IF SubtractVAT THEN
        SetCurrency(CurrencyCode);

      LineAmount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            LineAmount :=
              LineAmount + ROUND("Line Amount" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            LineAmount := LineAmount + "Line Amount";
        UNTIL NEXT = 0;

      EXIT(LineAmount);
    END;

    PROCEDURE GetTotalVATAmount@5() : Decimal;
    BEGIN
      CALCSUMS("VAT Amount");
      EXIT("VAT Amount");
    END;

    PROCEDURE GetTotalInvDiscAmount@9() : Decimal;
    BEGIN
      CALCSUMS("Invoice Discount Amount");
      EXIT("Invoice Discount Amount");
    END;

    PROCEDURE GetTotalInvDiscBaseAmount@6(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      InvDiscBaseAmount@1002 : Decimal;
    BEGIN
      IF SubtractVAT THEN
        SetCurrency(CurrencyCode);

      InvDiscBaseAmount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            InvDiscBaseAmount :=
              InvDiscBaseAmount +
              ROUND("Inv. Disc. Base Amount" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            InvDiscBaseAmount := InvDiscBaseAmount + "Inv. Disc. Base Amount";
        UNTIL NEXT = 0;
      EXIT(InvDiscBaseAmount);
    END;

    PROCEDURE GetTotalVATBase@14() : Decimal;
    BEGIN
      CALCSUMS("VAT Base");
      EXIT("VAT Base");
    END;

    PROCEDURE GetTotalAmountInclVAT@17() : Decimal;
    BEGIN
      CALCSUMS("Amount Including VAT");
      EXIT("Amount Including VAT");
    END;

    PROCEDURE GetTotalVATDiscount@19(CurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1000 : Boolean) : Decimal;
    VAR
      VATDiscount@1002 : Decimal;
    BEGIN
      SetCurrency(CurrencyCode);

      VATDiscount := 0;

      IF FIND('-') THEN
        REPEAT
          IF NewPricesIncludingVAT THEN
            VATDiscount :=
              VATDiscount +
              ROUND(
                ("Line Amount" - "Invoice Discount Amount") * "VAT %" / (100 + "VAT %"),
                Currency."Amount Rounding Precision",Currency.VATRoundingDirection) -
              "VAT Amount" + "VAT Difference"
          ELSE
            VATDiscount :=
              VATDiscount +
              ROUND(
                "VAT Base" * "VAT %" / 100,
                Currency."Amount Rounding Precision",Currency.VATRoundingDirection) -
              "VAT Amount";
        UNTIL NEXT = 0;
      EXIT(VATDiscount);
    END;

    PROCEDURE GetAnyLineModified@7() : Boolean;
    BEGIN
      IF FIND('-') THEN
        REPEAT
          IF Modified THEN
            EXIT(TRUE);
        UNTIL NEXT = 0;
      EXIT(FALSE);
    END;

    PROCEDURE SetInvoiceDiscountAmount@13(NewInvoiceDiscount@1000 : Decimal;NewCurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1002 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    VAR
      TotalInvDiscBaseAmount@1003 : Decimal;
      NewRemainder@1004 : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);
      TotalInvDiscBaseAmount := GetTotalInvDiscBaseAmount(FALSE,Currency.Code);
      IF TotalInvDiscBaseAmount = 0 THEN
        EXIT;
      FIND('-');
      REPEAT
        IF "Inv. Disc. Base Amount" <> 0 THEN BEGIN
          IF TotalInvDiscBaseAmount = 0 THEN
            NewRemainder := 0
          ELSE
            NewRemainder :=
              NewRemainder + NewInvoiceDiscount * "Inv. Disc. Base Amount" / TotalInvDiscBaseAmount;
          IF "Invoice Discount Amount" <> ROUND(NewRemainder,Currency."Amount Rounding Precision") THEN BEGIN
            VALIDATE(
              "Invoice Discount Amount",ROUND(NewRemainder,Currency."Amount Rounding Precision"));
            CalcVATFields(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);
            Modified := TRUE;
            MODIFY;
          END;
          NewRemainder := NewRemainder - "Invoice Discount Amount";
        END;
      UNTIL NEXT = 0;
    END;

    PROCEDURE SetInvoiceDiscountPercent@16(NewInvoiceDiscountPct@1000 : Decimal;NewCurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1002 : Boolean;CalcInvDiscPerVATID@1003 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    VAR
      NewRemainder@1004 : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);
      IF FIND('-') THEN
        REPEAT
          IF "Inv. Disc. Base Amount" <> 0 THEN BEGIN
            NewRemainder :=
              NewRemainder + NewInvoiceDiscountPct * "Inv. Disc. Base Amount" / 100;
            IF "Invoice Discount Amount" <> ROUND(NewRemainder,Currency."Amount Rounding Precision") THEN BEGIN
              VALIDATE(
                "Invoice Discount Amount",ROUND(NewRemainder,Currency."Amount Rounding Precision"));
              CalcVATFields(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);
              "VAT Difference" := 0;
              Modified := TRUE;
              MODIFY;
            END;
            IF CalcInvDiscPerVATID THEN
              NewRemainder := 0
            ELSE
              NewRemainder := NewRemainder - "Invoice Discount Amount";
          END;
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE GetCalculatedVAT@12(NewCurrencyCode@1000 : Code[10];NewPricesIncludingVAT@1001 : Boolean;NewVATBaseDiscPct@1002 : Decimal) : Decimal;
    BEGIN
      IF NOT GlobalsInitialized THEN
        InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);

      IF NewPricesIncludingVAT THEN
        EXIT(
          ROUND(
            ("Line Amount" - "Invoice Discount Amount") * "VAT %" / (100 + "VAT %") * (1 - NewVATBaseDiscPct / 100),
            Currency."Amount Rounding Precision",Currency.VATRoundingDirection));

      EXIT(
        ROUND(
          ("Line Amount" - "Invoice Discount Amount") * "VAT %" / 100 * (1 - NewVATBaseDiscPct / 100),
          Currency."Amount Rounding Precision",Currency.VATRoundingDirection));
    END;

    PROCEDURE CalcVATFields@23(NewCurrencyCode@1000 : Code[10];NewPricesIncludingVAT@1001 : Boolean;NewVATBaseDiscPct@1002 : Decimal);
    BEGIN
      IF NOT GlobalsInitialized THEN
        InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);

      "VAT Amount" := GetCalculatedVAT(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);

      IF NewPricesIncludingVAT THEN BEGIN
        IF NewVATBaseDiscPct = 0 THEN BEGIN
          "Amount Including VAT" := "Line Amount" - "Invoice Discount Amount";
          "VAT Base" := "Amount Including VAT" - "VAT Amount";
        END ELSE BEGIN
          "VAT Base" :=
            ROUND(
              ("Line Amount" - "Invoice Discount Amount") / (1 + "VAT %" / 100),
              Currency."Amount Rounding Precision");
          "Amount Including VAT" := "VAT Base" + "VAT Amount";
        END;
      END ELSE BEGIN
        "VAT Base" := "Line Amount" - "Invoice Discount Amount";
        "Amount Including VAT" := "VAT Base" + "VAT Amount";
      END;
      "Calculated VAT Amount" := "VAT Amount";
      "VAT Difference" := 0;
      Modified := TRUE;
    END;

    LOCAL PROCEDURE CalcValueLCY@18(Value@1003 : Decimal;PostingDate@1006 : Date;CurrencyCode@1005 : Code[10];CurrencyFactor@1004 : Decimal) : Decimal;
    VAR
      CurrencyExchangeRate@1000 : Record 330;
    BEGIN
      EXIT(CurrencyExchangeRate.ExchangeAmtFCYToLCY(PostingDate,CurrencyCode,Value,CurrencyFactor));
    END;

    PROCEDURE GetBaseLCY@20(PostingDate@1003 : Date;CurrencyCode@1002 : Code[10];CurrencyFactor@1001 : Decimal) : Decimal;
    BEGIN
      EXIT(ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor)));
    END;

    PROCEDURE GetAmountLCY@21(PostingDate@1000 : Date;CurrencyCode@1001 : Code[10];CurrencyFactor@1002 : Decimal) : Decimal;
    BEGIN
      EXIT(
        ROUND(CalcValueLCY("Amount Including VAT",PostingDate,CurrencyCode,CurrencyFactor)) -
        ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor)));
    END;

    PROCEDURE DeductVATAmountLine@22(VAR VATAmountLineDeduct@1001 : Record 290);
    BEGIN
      IF FINDSET THEN
        REPEAT
          VATAmountLineDeduct := Rec;
          IF VATAmountLineDeduct.FIND THEN BEGIN
            "VAT Base" -= VATAmountLineDeduct."VAT Base";
            "VAT Amount" -= VATAmountLineDeduct."VAT Amount";
            "Amount Including VAT" -= VATAmountLineDeduct."Amount Including VAT";
            "Line Amount" -= VATAmountLineDeduct."Line Amount";
            "Inv. Disc. Base Amount" -= VATAmountLineDeduct."Inv. Disc. Base Amount";
            "Invoice Discount Amount" -= VATAmountLineDeduct."Invoice Discount Amount";
            "Calculated VAT Amount" -= VATAmountLineDeduct."Calculated VAT Amount";
            "VAT Difference" -= VATAmountLineDeduct."VAT Difference";
            MODIFY;
          END;
        UNTIL NEXT = 0;
    END;

    BEGIN
    END.
  }
}


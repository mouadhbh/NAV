OBJECT Table 233 Item Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               ItemJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               ItemJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               ItemJnlLine.SETRANGE("Journal Batch Name",Name);
               ItemJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ItemJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               ItemJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE ItemJnlLine.FINDFIRST DO
                 ItemJnlLine.RENAME("Journal Template Name",Name,ItemJnlLine."Line No.");
             END;

    CaptionML=[ENU=Item Journal Batch;
               FRA=Nom feuille article];
    LookupPageID=Page262;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Item Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              FRA=Nom modŠle feuille];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              FRA=Nom];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  ItemJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  ItemJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  ItemJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              FRA=Code motif] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  ItemJnlTemplate.GET("Journal Template Name");
                                                                  IF ItemJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                ItemJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                ItemJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                ItemJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              FRA=Souches de nø validation] }
    { 21  ;   ;Template Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Item Journal Template".Type WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Template Type;
                                                              FRA=Type modŠle];
                                                   OptionCaptionML=[ENU=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod. Order;
                                                                    FRA=Article,Transfert,Inventaire,R‚‚valuation,Consommation,Production,Capacit‚,O.F.];
                                                   OptionString=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod. Order;
                                                   Editable=No }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Item Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              FRA=Abonnement];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;FRA=Seul le champ %1 est … renseigner dans les feuilles abonnement.';
      Text001@1001 : TextConst 'ENU=must not be %1;FRA=ne doit pas ˆtre %1';
      ItemJnlTemplate@1002 : Record 82;
      ItemJnlLine@1003 : Record 83;

    PROCEDURE SetupNewBatch@3();
    BEGIN
      ItemJnlTemplate.GET("Journal Template Name");
      "No. Series" := ItemJnlTemplate."No. Series";
      "Posting No. Series" := ItemJnlTemplate."Posting No. Series";
      "Reason Code" := ItemJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}


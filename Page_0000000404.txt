OBJECT Page 404 Check Preview
{
  OBJECT-PROPERTIES
  {
    Date=07/09/12;
    Time=12:00:00;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Check Preview;
               FRA=Aper‡u chŠque];
    LinksAllowed=No;
    SourceTable=Table81;
    DataCaptionExpr="Document No." + ' ' + CheckToAddr[1];
    PageType=Card;
    OnOpenPage=BEGIN
                 CompanyInfo.GET;
                 FormatAddr.Company(CompanyAddr,CompanyInfo);
               END;

    OnAfterGetRecord=BEGIN
                       CalcCheck;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group     ;
                CaptionML=[ENU=Payer;
                           FRA=Payeur] }

    { 15  ;2   ;Field     ;
                CaptionML=[ENU=Company Name;
                           FRA=Nom de la soci‚t‚];
                SourceExpr=CompanyAddr[1] }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Company Address;
                           FRA=Adresse soci‚t‚];
                SourceExpr=CompanyAddr[2] }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Company Address 2;
                           FRA=Adresse soci‚t‚ 2];
                SourceExpr=CompanyAddr[3] }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Company Post Code/City;
                           FRA=Code postal/Ville soci‚t‚];
                SourceExpr=CompanyAddr[4] }

    { 5   ;2   ;Field     ;
                SourceExpr="Document No." }

    { 19  ;2   ;Field     ;
                CaptionML=[ENU=Status;
                           FRA=Statut];
                SourceExpr=CheckStatusText }

    { 20  ;1   ;Group     ;
                CaptionML=[ENU=Amount;
                           FRA=Montant] }

    { 30  ;2   ;Group      }

    { 13  ;3   ;Field     ;
                Name=AmountText;
                CaptionML=[ENU=Amount Text;
                           FRA=Texte montant];
                CaptionClass=FORMAT(NumberText[1]) }

    { 14  ;3   ;Field     ;
                CaptionML=[ENU=Amount Text 2;
                           FRA=Texte montant 2];
                CaptionClass=FORMAT(NumberText[2]) }

    { 23  ;1   ;Group     ;
                CaptionML=[ENU=Payee;
                           FRA=B‚n‚ficiaire] }

    { 1902115401;2;Group  ;
                GroupType=FixedLayout }

    { 1900724701;3;Group  ;
                CaptionML=[ENU=Pay to the order of;
                           FRA=Payable …] }

    { 2   ;4   ;Field     ;
                CaptionML=[ENU=Pay-to Name;
                           FRA=Nom];
                SourceExpr=CheckToAddr[1] }

    { 8   ;4   ;Field     ;
                CaptionML=[ENU=Pay-to Address;
                           FRA=Adresse];
                SourceExpr=CheckToAddr[2] }

    { 9   ;4   ;Field     ;
                CaptionML=[ENU=Pay-to Address 2;
                           FRA=Adresse (2Šme ligne)];
                SourceExpr=CheckToAddr[3] }

    { 10  ;4   ;Field     ;
                CaptionML=[ENU=Pay-to Post Code/City;
                           FRA=CP/Ville];
                SourceExpr=CheckToAddr[4] }

    { 1900724501;3;Group  ;
                CaptionML=[ENU=Date;
                           FRA=Date] }

    { 6   ;4   ;Field     ;
                SourceExpr="Posting Date" }

    { 24  ;4   ;Field     ;
                SourceExpr=Text002;
                Visible=FALSE }

    { 26  ;4   ;Field     ;
                SourceExpr=Text002;
                Visible=FALSE }

    { 28  ;4   ;Field     ;
                SourceExpr=Text002;
                Visible=FALSE }

    { 1900724401;3;Group  ;
                CaptionML=[ENU=Amount;
                           FRA=Montant] }

    { 7   ;4   ;Field     ;
                SourceExpr=CheckAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 25  ;4   ;Field     ;
                SourceExpr=Text002;
                Visible=FALSE }

    { 27  ;4   ;Field     ;
                SourceExpr=Text002;
                Visible=FALSE }

    { 29  ;4   ;Field     ;
                SourceExpr=Text002;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Printed Check;FRA=ChŠque imprim‚';
      Text001@1001 : TextConst 'ENU=Not Printed Check;FRA=ChŠque non imprim‚';
      GenJnlLine@1002 : Record 81;
      Cust@1003 : Record 18;
      Vend@1004 : Record 23;
      BankAcc@1005 : Record 270;
      CompanyInfo@1006 : Record 79;
      CheckReport@1007 : Report 1401;
      FormatAddr@1008 : Codeunit 365;
      CheckToAddr@1009 : ARRAY [8] OF Text[50];
      CompanyAddr@1010 : ARRAY [8] OF Text[50];
      NumberText@1011 : ARRAY [2] OF Text[80];
      CheckStatusText@1012 : Text[30];
      CheckAmount@1013 : Decimal;
      Text002@1014 : TextConst 'ENU=Placeholder;FRA=ParamŠtre substituable';

    LOCAL PROCEDURE CalcCheck@1();
    BEGIN
      IF "Check Printed" THEN BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlLine.SETRANGE("Posting Date","Posting Date");
        GenJnlLine.SETRANGE("Document No.","Document No.");
        IF "Bal. Account No." = '' THEN
          GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::" ")
        ELSE
          GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::"Computer Check");
        GenJnlLine.SETRANGE("Check Printed",TRUE);
        CheckStatusText := Text000;
      END ELSE BEGIN
        GenJnlLine.RESET;
        GenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date","Document No.");
        GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlLine.SETRANGE("Posting Date","Posting Date");
        GenJnlLine.SETRANGE("Document No.","Document No.");
        GenJnlLine.SETRANGE("Account Type","Account Type");
        GenJnlLine.SETRANGE("Account No.","Account No.");
        GenJnlLine.SETRANGE("Bal. Account Type","Bal. Account Type");
        GenJnlLine.SETRANGE("Bal. Account No.","Bal. Account No.");
        GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type");
        CheckStatusText := Text001;
      END;

      CheckAmount := 0;
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          CheckAmount := CheckAmount + GenJnlLine.Amount;
        UNTIL GenJnlLine.NEXT = 0;

      IF CheckAmount < 0 THEN
        CheckAmount := 0;

      CheckReport.InitTextVariable;
      CheckReport.FormatNoText(NumberText,CheckAmount,GenJnlLine."Currency Code");

      CASE GenJnlLine."Account Type" OF
        GenJnlLine."Account Type"::"G/L Account":
          BEGIN
            CLEAR(CheckToAddr);
            CheckToAddr[1] := GenJnlLine.Description;
          END;
        GenJnlLine."Account Type"::Customer:
          BEGIN
            Cust.GET(GenJnlLine."Account No.");
            Cust.Contact := '';
            FormatAddr.Customer(CheckToAddr,Cust);
          END;
        GenJnlLine."Account Type"::Vendor:
          BEGIN
            Vend.GET(GenJnlLine."Account No.");
            Vend.Contact := '';
            FormatAddr.Vendor(CheckToAddr,Vend);
          END;
        GenJnlLine."Account Type"::"Bank Account":
          BEGIN
            BankAcc.GET(GenJnlLine."Account No.");
            BankAcc.Contact := '';
            FormatAddr.BankAcc(CheckToAddr,BankAcc);
          END;
        GenJnlLine."Account Type"::"Fixed Asset":
          GenJnlLine.FIELDERROR("Account Type");
      END;
    END;

    BEGIN
    END.
  }
}


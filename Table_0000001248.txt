OBJECT Table 1248 Ledger Entry Matching Buffer
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Ledger Entry Matching Buffer;
               FRA=Tampon correspondance ‚criture comptable];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              FRA=Nø s‚quence] }
    { 2   ;   ;Account Type        ;Option        ;CaptionML=[ENU=Account Type;
                                                              FRA=Type compte];
                                                   OptionCaptionML=[ENU=Customer,Vendor;
                                                                    FRA=Client,Fournisseur];
                                                   OptionString=Customer,Vendor }
    { 3   ;   ;Account No.         ;Code20        ;CaptionML=[ENU=Account No.;
                                                              FRA=Nø compte] }
    { 9   ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              FRA=Date d'‚ch‚ance] }
    { 10  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              FRA=Date comptabilisation] }
    { 11  ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              FRA=Nø document] }
    { 12  ;   ;External Document No.;Code35       ;CaptionML=[ENU=External Document No.;
                                                              FRA=Nø doc. externe] }
    { 20  ;   ;Remaining Amount    ;Decimal       ;CaptionML=[ENU=Remaining Amount;
                                                              FRA=Montant ouvert] }
    { 21  ;   ;Remaining Amt. Incl. Discount;Decimal;
                                                   CaptionML=[ENU=Remaining Amt. Incl. Discount;
                                                              FRA=Montant ouvert remise incl.] }
    { 22  ;   ;Pmt. Discount Due Date;Date        ;CaptionML=[ENU=Pmt. Discount Due Date;
                                                              FRA=Date d'‚ch‚ance d'escompte] }
  }
  KEYS
  {
    {    ;Entry No.,Account Type                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InsertFromCustomerLedgerEntry@1(CustLedgerEntry@1000 : Record 21;UseLCYAmounts@1001 : Boolean;VAR UsePaymentDiscounts@1002 : Boolean);
    BEGIN
      CLEAR(Rec);
      "Entry No." := CustLedgerEntry."Entry No.";
      "Account Type" := "Account Type"::Customer;
      "Account No." := CustLedgerEntry."Customer No.";
      "Due Date" := CustLedgerEntry."Due Date";
      "Posting Date" := CustLedgerEntry."Posting Date";
      "Document No." := CustLedgerEntry."Document No.";
      "External Document No." := CustLedgerEntry."External Document No.";

      IF UseLCYAmounts THEN
        "Remaining Amount" := CustLedgerEntry."Remaining Amt. (LCY)"
      ELSE
        "Remaining Amount" := CustLedgerEntry."Remaining Amount";

      "Pmt. Discount Due Date" := GetCustomerLedgerEntryDiscountDueDate(CustLedgerEntry);

      "Remaining Amt. Incl. Discount" := "Remaining Amount";
      IF "Pmt. Discount Due Date" > 0D THEN BEGIN
        IF UseLCYAmounts THEN
          "Remaining Amt. Incl. Discount" -=
            ROUND(CustLedgerEntry."Remaining Pmt. Disc. Possible" / CustLedgerEntry."Adjusted Currency Factor")
        ELSE
          "Remaining Amt. Incl. Discount" -= CustLedgerEntry."Remaining Pmt. Disc. Possible";
        UsePaymentDiscounts := TRUE;
      END;

      INSERT(TRUE);
    END;

    PROCEDURE InsertFromVendorLedgerEntry@4(VendorLedgerEntry@1000 : Record 25;UseLCYAmounts@1001 : Boolean;VAR UsePaymentDiscounts@1002 : Boolean);
    BEGIN
      CLEAR(Rec);
      "Entry No." := VendorLedgerEntry."Entry No.";
      "Account Type" := "Account Type"::Vendor;
      "Account No." := VendorLedgerEntry."Vendor No.";
      "Due Date" := VendorLedgerEntry."Due Date";
      "Posting Date" := VendorLedgerEntry."Posting Date";
      "Document No." := VendorLedgerEntry."Document No.";
      "External Document No." := VendorLedgerEntry."External Document No.";

      IF UseLCYAmounts THEN
        "Remaining Amount" := VendorLedgerEntry."Remaining Amt. (LCY)"
      ELSE
        "Remaining Amount" := VendorLedgerEntry."Remaining Amount";

      "Pmt. Discount Due Date" := GetVendorLedgerEntryDiscountDueDate(VendorLedgerEntry);

      "Remaining Amt. Incl. Discount" := "Remaining Amount";
      IF "Pmt. Discount Due Date" > 0D THEN BEGIN
        IF UseLCYAmounts THEN
          "Remaining Amt. Incl. Discount" -=
            ROUND(VendorLedgerEntry."Remaining Pmt. Disc. Possible" / VendorLedgerEntry."Adjusted Currency Factor")
        ELSE
          "Remaining Amt. Incl. Discount" -= VendorLedgerEntry."Remaining Pmt. Disc. Possible";
        UsePaymentDiscounts := TRUE;
      END;

      INSERT(TRUE);
    END;

    PROCEDURE GetApplicableRemainingAmount@6(BankAccReconciliationLine@1000 : Record 274;UsePaymentDiscounts@1001 : Boolean) : Decimal;
    BEGIN
      IF NOT UsePaymentDiscounts THEN
        EXIT("Remaining Amount");

      IF BankAccReconciliationLine."Transaction Date" > "Pmt. Discount Due Date" THEN
        EXIT("Remaining Amount");

      EXIT("Remaining Amt. Incl. Discount");
    END;

    PROCEDURE GetNoOfLedgerEntriesWithinRange@9(MinAmount@1001 : Decimal;MaxAmount@1000 : Decimal;TransactionDate@1002 : Date;UsePaymentDiscounts@1004 : Boolean) : Integer;
    BEGIN
      EXIT(GetNoOfLedgerEntriesInAmountRange(MinAmount,MaxAmount,TransactionDate,'>=%1&<=%2',UsePaymentDiscounts));
    END;

    PROCEDURE GetNoOfLedgerEntriesOutsideRange@10(MinAmount@1002 : Decimal;MaxAmount@1001 : Decimal;TransactionDate@1000 : Date;UsePaymentDiscounts@1003 : Boolean) : Integer;
    BEGIN
      EXIT(GetNoOfLedgerEntriesInAmountRange(MinAmount,MaxAmount,TransactionDate,'<%1|>%2',UsePaymentDiscounts));
    END;

    LOCAL PROCEDURE GetNoOfLedgerEntriesInAmountRange@23(MinAmount@1001 : Decimal;MaxAmount@1000 : Decimal;TransactionDate@1002 : Date;RangeFilter@1004 : Text;UsePaymentDiscounts@1005 : Boolean) : Integer;
    VAR
      NoOfEntreis@1003 : Integer;
    BEGIN
      SETFILTER("Remaining Amount",RangeFilter,MinAmount,MaxAmount);
      SETFILTER("Pmt. Discount Due Date",'<%1',TransactionDate);
      NoOfEntreis := COUNT;

      SETRANGE("Remaining Amount");

      IF UsePaymentDiscounts THEN BEGIN
        SETFILTER("Remaining Amt. Incl. Discount",RangeFilter,MinAmount,MaxAmount);
        SETFILTER("Pmt. Discount Due Date",'>=%1',TransactionDate);
        NoOfEntreis += COUNT;
        SETRANGE("Remaining Amt. Incl. Discount");
      END;

      SETRANGE("Pmt. Discount Due Date");

      EXIT(NoOfEntreis);
    END;

    LOCAL PROCEDURE GetCustomerLedgerEntryDiscountDueDate@2(CustLedgerEntry@1000 : Record 21) : Date;
    BEGIN
      IF CustLedgerEntry."Remaining Pmt. Disc. Possible" = 0 THEN
        EXIT(0D);

      IF CustLedgerEntry."Pmt. Disc. Tolerance Date" >= CustLedgerEntry."Pmt. Discount Date" THEN
        EXIT(CustLedgerEntry."Pmt. Disc. Tolerance Date");

      EXIT(CustLedgerEntry."Pmt. Discount Date");
    END;

    LOCAL PROCEDURE GetVendorLedgerEntryDiscountDueDate@3(VendorLedgerEntry@1000 : Record 25) : Date;
    BEGIN
      IF VendorLedgerEntry."Remaining Pmt. Disc. Possible" = 0 THEN
        EXIT(0D);

      IF VendorLedgerEntry."Pmt. Disc. Tolerance Date" >= VendorLedgerEntry."Pmt. Discount Date" THEN
        EXIT(VendorLedgerEntry."Pmt. Disc. Tolerance Date");

      EXIT(VendorLedgerEntry."Pmt. Discount Date");
    END;

    BEGIN
    END.
  }
}


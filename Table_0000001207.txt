OBJECT Table 1207 Direct Debit Collection
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00,NAVFR8.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Identifier,Created Date-Time;
    CaptionML=[ENU=Direct Debit Collection;
               FRA=Recouvrement pr‚lŠvement];
    LookupPageID=Page1207;
    DrillDownPageID=Page1207;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Integer       ;CaptionML=[ENU=No.;
                                                              FRA=Nø] }
    { 2   ;   ;Identifier          ;Code20        ;CaptionML=[ENU=Identifier;
                                                              FRA=Identifiant] }
    { 3   ;   ;Created Date-Time   ;DateTime      ;CaptionML=[ENU=Created Date-Time;
                                                              FRA=Date/heure cr‚ation] }
    { 4   ;   ;Created by User     ;Code50        ;TableRelation=User."User Name";
                                                   CaptionML=[ENU=Created by User;
                                                              FRA=Cr‚‚ par l'utilisateur] }
    { 5   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              FRA=Statut];
                                                   OptionCaptionML=[ENU=New,Canceled,File Created,Posted,Closed;
                                                                    FRA=Nouveau,Annul‚,Fichier cr‚‚,Valid‚,Cl“tur‚];
                                                   OptionString=New,Canceled,File Created,Posted,Closed }
    { 6   ;   ;No. of Transfers    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Direct Debit Collection Entry" WHERE (Direct Debit Collection No.=FIELD(No.)));
                                                   CaptionML=[ENU=No. of Transfers;
                                                              FRA=Nbre de transferts] }
    { 7   ;   ;To Bank Account No. ;Code20        ;TableRelation="Bank Account";
                                                   CaptionML=[ENU=To Bank Account No.;
                                                              FRA=Nø compte bancaire de destination] }
    { 8   ;   ;To Bank Account Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Bank Account".Name WHERE (No.=FIELD(To Bank Account No.)));
                                                   CaptionML=[ENU=To Bank Account Name;
                                                              FRA=Nom compte bancaire de destination] }
    { 9   ;   ;Message ID          ;Code35        ;CaptionML=[ENU=Message ID;
                                                              FRA=ID message] }
    { 10  ;   ;Partner Type        ;Option        ;CaptionML=[ENU=Partner Type;
                                                              FRA=Type partenaire];
                                                   OptionCaptionML=[ENU=" ,Company,Person";
                                                                    FRA=" ,Soci‚t‚,Personne"];
                                                   OptionString=[ ,Company,Person];
                                                   Editable=No }
    { 10800;  ;Source Table ID     ;Integer       ;CaptionML=[ENU=Source Table ID;
                                                              FRA=ID table source] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CloseQst@1000 : TextConst 'ENU=If you close the collection, you will not be able to register the payments from the collection. Do you want to close the collection anyway?;FRA=Si vous cl“turez la collection, vous ne pourrez pas enregistrer les paiements provenant de la collection. Voulez-vous cl“turer la collection malgr‚ toutÿ?';

    PROCEDURE CreateNew@1(NewIdentifier@1000 : Code[20];NewBankAccountNo@1001 : Code[20];PartnerType@1002 : Option);
    BEGIN
      RESET;
      LOCKTABLE;
      IF FINDLAST THEN;
      INIT;
      "No." += 1;
      Identifier := NewIdentifier;
      "Message ID" := Identifier;
      "Created Date-Time" := CURRENTDATETIME;
      "Created by User" := USERID;
      "To Bank Account No." := NewBankAccountNo;
      "Partner Type" := PartnerType;
      INSERT;
    END;

    PROCEDURE CloseCollection@2();
    VAR
      DirectDebitCollectionEntry@1000 : Record 1208;
    BEGIN
      IF Status IN [Status::Closed,Status::Canceled] THEN
        EXIT;
      IF NOT CONFIRM(CloseQst) THEN
        EXIT;

      IF Status = Status::New THEN
        Status := Status::Canceled
      ELSE
        Status := Status::Closed;
      MODIFY;

      DirectDebitCollectionEntry.SETRANGE("Direct Debit Collection No.","No.");
      DirectDebitCollectionEntry.SETRANGE(Status,DirectDebitCollectionEntry.Status::New);
      DirectDebitCollectionEntry.MODIFYALL(Status,DirectDebitCollectionEntry.Status::Rejected);
    END;

    PROCEDURE Export@5();
    VAR
      DirectDebitCollectionEntry@1000 : Record 1208;
    BEGIN
      DirectDebitCollectionEntry.SETRANGE("Direct Debit Collection No.","No.");
      IF DirectDebitCollectionEntry.FINDFIRST THEN
        DirectDebitCollectionEntry.ExportSEPA;
    END;

    PROCEDURE HasPaymentFileErrors@4() : Boolean;
    VAR
      GenJnlLine@1000 : Record 81;
    BEGIN
      SetErrorLink(GenJnlLine);
      EXIT(GenJnlLine.HasPaymentFileErrorsInBatch);
    END;

    PROCEDURE SetStatus@3(NewStatus@1000 : Option);
    BEGIN
      LOCKTABLE;
      FIND;
      Status := NewStatus;
      MODIFY;
    END;

    PROCEDURE SetErrorLink@1120000(VAR GenJnlLine@1120000 : Record 81);
    BEGIN
      IF "Source Table ID" <> 0 THEN BEGIN
        GenJnlLine."Journal Template Name" := '';
        GenJnlLine."Journal Batch Name" := FORMAT("Source Table ID");
        GenJnlLine."Document No." := Identifier;
        EXIT;
      END;

      GenJnlLine."Document No." := COPYSTR(FORMAT("No."),1,MAXSTRLEN(GenJnlLine."Document No."));
    END;

    PROCEDURE DeletePaymentFileErrors@6();
    VAR
      DirectDebitCollectionEntry@1000 : Record 1208;
    BEGIN
      DirectDebitCollectionEntry.SETRANGE("Direct Debit Collection No.","No.");
      IF DirectDebitCollectionEntry.FINDSET THEN
        REPEAT
          DirectDebitCollectionEntry.DeletePaymentFileErrors;
        UNTIL DirectDebitCollectionEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
}


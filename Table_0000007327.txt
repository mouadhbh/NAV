OBJECT Table 7327 Whse. Worksheet Name
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description,Location Code;
    OnInsert=BEGIN
               TestWMSLocation;
             END;

    OnModify=BEGIN
               TestWMSLocation;
             END;

    OnDelete=VAR
               WhseWkshLine@1000 : Record 7326;
             BEGIN
               WhseWkshLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
               WhseWkshLine.SETRANGE(Name,Name);
               WhseWkshLine.SETRANGE("Location Code","Location Code");
               WhseWkshLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               WhseWkshLine@1000 : Record 7326;
             BEGIN
               TestWMSLocation;
               WhseWkshLine.SETRANGE("Worksheet Template Name",xRec."Worksheet Template Name");
               WhseWkshLine.SETRANGE(Name,xRec.Name);
               WhseWkshLine.SETRANGE("Location Code",xRec."Location Code");
               WHILE WhseWkshLine.FINDFIRST DO
                 WhseWkshLine.RENAME("Worksheet Template Name",Name,"Location Code",WhseWkshLine."Line No.");
             END;

    CaptionML=[ENU=Whse. Worksheet Name;
               FRA=Nom feuille entrep.];
    LookupPageID=Page7346;
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Whse. Worksheet Template";
                                                   CaptionML=[ENU=Worksheet Template Name;
                                                              FRA=Nom modäle feuille] }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              FRA=Nom];
                                                   NotBlank=Yes }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              FRA=Code magasin] }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=DÇsignation] }
    { 21  ;   ;Template Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Whse. Worksheet Template".Type WHERE (Name=FIELD(Worksheet Template Name)));
                                                   CaptionML=[ENU=Template Type;
                                                              FRA=Type modäle];
                                                   OptionCaptionML=[ENU=Put-away,Pick,Movement;
                                                                    FRA=Rangement,PrÇlävement,Mouvement];
                                                   OptionString=Put-away,Pick,Movement;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Worksheet Template Name,Name,Location Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1001 : Record 14;
      WhseWkshTemplate@1002 : Record 7328;

    PROCEDURE SetupNewName@3();
    VAR
      WMSMgt@1000 : Codeunit 7302;
    BEGIN
      IF USERID <> '' THEN
        GetLocation(WMSMgt.GetDefaultLocation);

      "Location Code" := Location.Code;
    END;

    LOCAL PROCEDURE GetLocation@1(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        Location.INIT
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE TestWMSLocation@2();
    BEGIN
      WhseWkshTemplate.GET("Worksheet Template Name");
      IF WhseWkshTemplate.Type = WhseWkshTemplate.Type::Movement THEN BEGIN
        TESTFIELD("Location Code");
        GetLocation("Location Code");
        Location.TESTFIELD("Directed Put-away and Pick");
      END;
    END;

    BEGIN
    END.
  }
}


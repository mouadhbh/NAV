OBJECT Table 780 Certificate of Supply
{
  OBJECT-PROPERTIES
  {
    Date=15/09/15;
    Time=12:00:00;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Certificate of Supply;
               FRA=Certificat d'approvisionnement];
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              FRA=Type document];
                                                   OptionCaptionML=[ENU=Sales Shipment,Service Shipment,Return Shipment;
                                                                    FRA=Exp‚dition vente,Exp‚dition service,Exp‚dition retour];
                                                   OptionString=Sales Shipment,Service Shipment,Return Shipment }
    { 2   ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Type=FILTER(Sales Shipment)) "Sales Shipment Header".No.
                                                                 ELSE IF (Document Type=FILTER(Service Shipment)) "Service Shipment Header".No.
                                                                 ELSE IF (Document Type=FILTER(Return Shipment)) "Return Shipment Header".No.;
                                                   CaptionML=[ENU=Document No.;
                                                              FRA=Nø document] }
    { 3   ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                IF xRec.Status = Status THEN
                                                                  EXIT;

                                                                IF  "No." = '' THEN
                                                                  "No." := "Document No.";

                                                                IF Status = Status::Received THEN
                                                                  "Receipt Date" := WORKDATE
                                                                ELSE
                                                                  "Receipt Date" := 0D;

                                                                IF Status = Status::"Not Applicable" THEN
                                                                  "No." := ''
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              FRA=Statut];
                                                   OptionCaptionML=[ENU=Not Applicable,Required,Received,Not Received;
                                                                    FRA=Non applicable,Requis,Re‡u,Non re‡u];
                                                   OptionString=Not Applicable,Required,Received,Not Received }
    { 4   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF Status = Status::"Not Applicable" THEN BEGIN
                                                                  IF "No." <> '' THEN
                                                                    ERROR(NoCannotBeEnteredErr);
                                                                END ELSE
                                                                  IF "No." = '' THEN
                                                                    ERROR(NoCannotBeEmptyErr)
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              FRA=Nø] }
    { 5   ;   ;Receipt Date        ;Date          ;OnValidate=BEGIN
                                                                CheckRcptDate
                                                              END;

                                                   CaptionML=[ENU=Receipt Date;
                                                              FRA=Date de r‚ception] }
    { 6   ;   ;Printed             ;Boolean       ;CaptionML=[ENU=Printed;
                                                              FRA=Imprim‚] }
    { 7   ;   ;Customer/Vendor Name;Text50        ;CaptionML=[ENU=Customer/Vendor Name;
                                                              FRA=Nom client/fournisseur];
                                                   Editable=No }
    { 8   ;   ;Shipment Method Code;Code10        ;CaptionML=[ENU=Shipment Method Code;
                                                              FRA=Code condition livraison];
                                                   Editable=No }
    { 9   ;   ;Shipment/Posting Date;Date         ;CaptionML=[ENU=Shipment/Posting Date;
                                                              FRA=Date exp‚dition/comptabilisation];
                                                   Editable=No }
    { 10  ;   ;Ship-to Country/Region Code;Code10 ;CaptionML=[ENU=Ship-to Country/Region Code;
                                                              FRA=Code pays/r‚gion destinataire];
                                                   Editable=No }
    { 11  ;   ;Customer/Vendor No. ;Code20        ;CaptionML=[ENU=Customer/Vendor No.;
                                                              FRA=Nø client/fournisseur] }
    { 12  ;   ;Vehicle Registration No.;Text20    ;OnValidate=BEGIN
                                                                IF (Status = Status::Received) AND ("Vehicle Registration No." <> xRec."Vehicle Registration No.") THEN
                                                                  ERROR(STRSUBSTNO(VehicleRegNoCannotBeChangedErr,FIELDCAPTION("Vehicle Registration No."),Status::Received))
                                                              END;

                                                   CaptionML=[ENU=Vehicle Registration No.;
                                                              FRA=Nø enregistrement v‚hicule] }
  }
  KEYS
  {
    {    ;Document Type,Document No.              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RecDateCannotBeEmptyErr@1000 : TextConst 'ENU=The Receipt Date cannot be empty when Status is Received.;FRA=La Date r‚ception ne peut pas ˆtre vide si le Statut est Re‡u.';
      RecDateCannotBeEnteredErr@1001 : TextConst 'ENU=You can enter the Receipt Date only if the status of the Certificate of Supply is Received.;FRA=Vous ne pouvez entrer la Date r‚ception que si le statut du Certificat d''approvisionnement est Re‡u.';
      CertRecDateBeforeShipmPostDateErr@1003 : TextConst 'ENU=The Receipt Date of the certificate cannot be earlier than the Shipment/Posting Date.;FRA=La Date r‚ception du certificat ne peut pas ˆtre ant‚rieure … la Date exp‚dition/comptabilisation.';
      NoCannotBeEnteredErr@1002 : TextConst 'ENU=The No. field cannot be filled in when the status of the Certificate of Supply is set to Not Applicable.;FRA=Le champ Nø ne peut pas ˆtre renseign‚ si le statut du Certificat d''approvisionnement est d‚fini sur Non applicable.';
      NoCannotBeEmptyErr@1004 : TextConst 'ENU=The No. field cannot be empty when the status of the Certificate of Supply is set to Required, Received, or Not Received.;FRA=Le champ Nø ne peut pas ˆtre vide si le statut du Certificat d''approvisionnement est d‚fini sur Requis, Re‡u ou Non re‡u.';
      VehicleRegNoCannotBeChangedErr@1005 : TextConst 'ENU=The %1 field cannot be changed when the status of the Certificate of Supply is set to %2.;FRA=Le champ %1 ne peut pas ˆtre modifi‚ lorsque le statut du Certificat d''approvisionnement est d‚fini sur %2.';

    PROCEDURE Print@1();
    BEGIN
      REPORT.RUNMODAL(REPORT::"Certificate of Supply",TRUE,FALSE,Rec);
    END;

    PROCEDURE SetPrintedTrue@2();
    BEGIN
      IF Status = Status::"Not Applicable" THEN
        SetRequired("Document No.");
      Printed := TRUE;

      MODIFY;
    END;

    PROCEDURE SetRequired@3(CertificateNo@1000 : Code[20]);
    BEGIN
      Status := Status::Required;
      "No." := CertificateNo;
      "Receipt Date" := 0D;
      MODIFY
    END;

    LOCAL PROCEDURE CheckRcptDate@37();
    BEGIN
      IF Status = Status::Received THEN BEGIN
        IF "Receipt Date" = 0D THEN
          ERROR(RecDateCannotBeEmptyErr);
        IF "Shipment/Posting Date" > "Receipt Date" THEN
          ERROR(CertRecDateBeforeShipmPostDateErr);
      END ELSE
        IF "Receipt Date" <> 0D THEN
          ERROR(RecDateCannotBeEnteredErr);
    END;

    PROCEDURE InitFromSales@11(VAR SalesShipmentHeader@1000 : Record 110);
    BEGIN
      IF NOT GET("Document Type"::"Sales Shipment",SalesShipmentHeader."No.") THEN BEGIN
        INIT;
        "Document Type" := "Document Type"::"Sales Shipment";
        "Document No." := SalesShipmentHeader."No.";
        "Customer/Vendor Name" := SalesShipmentHeader."Ship-to Name";
        "Shipment Method Code" := SalesShipmentHeader."Shipment Method Code";
        "Shipment/Posting Date" := SalesShipmentHeader."Shipment Date";
        "Ship-to Country/Region Code" := SalesShipmentHeader."Ship-to Country/Region Code";
        "Customer/Vendor No." := SalesShipmentHeader."Bill-to Customer No.";
        INSERT(TRUE);
      END
    END;

    PROCEDURE InitFromPurchase@10(VAR ReturnShipmentHeader@1000 : Record 6650);
    BEGIN
      IF NOT GET("Document Type"::"Return Shipment",ReturnShipmentHeader."No.") THEN BEGIN
        INIT;
        "Document Type" := "Document Type"::"Return Shipment";
        "Document No." := ReturnShipmentHeader."No.";
        "Customer/Vendor Name" := ReturnShipmentHeader."Ship-to Name";
        "Shipment Method Code" := ReturnShipmentHeader."Shipment Method Code";
        "Shipment/Posting Date" := ReturnShipmentHeader."Posting Date";
        "Ship-to Country/Region Code" := ReturnShipmentHeader."Ship-to Country/Region Code";
        "Customer/Vendor No." := ReturnShipmentHeader."Pay-to Vendor No.";
        INSERT(TRUE);
      END
    END;

    PROCEDURE InitFromService@5(VAR ServiceShipmentHeader@1000 : Record 5990);
    BEGIN
      IF NOT GET("Document Type"::"Service Shipment",ServiceShipmentHeader."No.") THEN BEGIN
        INIT;
        "Document Type" := "Document Type"::"Service Shipment";
        "Document No." := ServiceShipmentHeader."No.";
        "Customer/Vendor Name" := ServiceShipmentHeader."Ship-to Name";
        "Shipment Method Code" := '';
        "Shipment/Posting Date" := ServiceShipmentHeader."Posting Date";
        "Ship-to Country/Region Code" := ServiceShipmentHeader."Ship-to Country/Region Code";
        "Customer/Vendor No." := ServiceShipmentHeader."Bill-to Customer No.";
        INSERT(TRUE);
      END
    END;

    PROCEDURE InitRecord@4(DocumentType@1000 : Option;DocumentNo@1001 : Code[20]);
    VAR
      SalesShipmentHeader@1004 : Record 110;
      ServiceShipmentHeader@1003 : Record 5990;
      ReturnShipmentHeader@1002 : Record 6650;
    BEGIN
      CASE DocumentType OF
        "Document Type"::"Sales Shipment":
          BEGIN
            SalesShipmentHeader.GET(DocumentNo);
            InitFromSales(SalesShipmentHeader);
          END;
        "Document Type"::"Service Shipment":
          BEGIN
            ServiceShipmentHeader.GET(DocumentNo);
            InitFromService(ServiceShipmentHeader);
          END;
        "Document Type"::"Return Shipment":
          BEGIN
            ReturnShipmentHeader.GET(DocumentNo);
            InitFromPurchase(ReturnShipmentHeader);
          END;
      END
    END;

    BEGIN
    END.
  }
}

